
&ИзменениеИКонтроль("РегламентноеЗаданиеПроверкаСМС")
Процедура B1_РегламентноеЗаданиеПроверкаСМС()

	#Удаление
	УстановитьПривилегированныйРежим(Истина);

	Если Не ОтправкаSMS.НастройкаОтправкиSMSВыполнена() Тогда
		ЗаписьЖурналаРегистрации("Отправка SMS", УровеньЖурналаРегистрации.Ошибка, , , НСтр(
		"ru = 'Не выполнены настройки отправки SMS.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;

	МетаданныеДокумента = Метаданные.Документы.смсСообщение;

	КодОшибки = 0;
	ПрерватьОбработку = Ложь;											

	//////////////////////////////////////////////////////////////
	// Отправка сообщений
	ТаблицаАдресатовСообщения = Новый ТаблицаЗначений;
	ТаблицаАдресатовСообщения.Колонки.Добавить("НомерПолучателя");
	ТаблицаАдресатовСообщения.Колонки.Добавить("НомерОтправителя");
	ТаблицаАдресатовСообщения.Колонки.Добавить("GUID");
	ТаблицаАдресатовСообщения.Колонки.Добавить("КодОшибки");
	ТаблицаАдресатовСообщения.Колонки.Добавить("НомерСтрокиДокумента");
	ТаблицаАдресатовСообщения.Колонки.Добавить("ТекстСообщения");

	ТаблицаТекстовСообщений = Новый ТаблицаЗначений;
	ТаблицаТекстовСообщений.Колонки.Добавить("ТекстСообщения");

	смсКоличествоСообщенийВОдномПакете = Константы.смсКоличествоСообщенийВОдномПакете.Получить();
	смсКоличествоСообщенийВОдномПакете = ?(смсКоличествоСообщенийВОдномПакете = 0, 9999999, смсКоличествоСообщенийВОдномПакете);
	// Выбираем сообщения для отправки
	РезультатЗапроса = ПолучитьСообщенияДляОтправки();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл

		ВыборкаРегистра = Выборка.Выбрать();
		ТаблицаАдресатовСообщения.Очистить();
		ТаблицаТекстовСообщений.Очистить();

		Пока ВыборкаРегистра.Следующий() Цикл

			ТекстСообщения       = ВыборкаРегистра.ТекстСообщения;
			ОтправлятьВТранслите = ВыборкаРегистра.Транслитерация;

			НовСтрока = ТаблицаАдресатовСообщения.Добавить();

			ЗаполнитьЗначенияСвойств(НовСтрока, ВыборкаРегистра, , );

			НовСтрокаТекст = ТаблицаТекстовСообщений.Добавить();
			НовСтрокаТекст.ТекстСообщения = ВыборкаРегистра.ТекстСообщения;

		КонецЦикла;

		Если ТаблицаАдресатовСообщения.Количество() = 0 Тогда

			Продолжить;

		КонецЕсли;

		ТаблицаТекстовСообщений.Свернуть("ТекстСообщения");

		НачатьТранзакцию();

		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(МетаданныеДокумента.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Сообщение);
			Блокировка.Заблокировать();

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.смсСостоянияСообщений.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Сообщение", Выборка.Сообщение);
			Блокировка.Заблокировать();

			Для Каждого СтрТекст Из ТаблицаТекстовСообщений Цикл
				Если СокрЛП(СтрТекст.ТекстСообщения) = "" Тогда
					Продолжить;
				КонецЕсли;	
				ПараметрыПоиска = Новый Структура;
				ПараметрыПоиска.Вставить("ТекстСообщения",СтрТекст.ТекстСообщения);
				ЕстьСтрокиАдресовСТекстом = ТаблицаАдресатовСообщения.НайтиСтроки(ПараметрыПоиска);
				МассивНомеров = Новый Массив;
				Сч = 0;
				Для Каждого СтрАдрес Из ЕстьСтрокиАдресовСТекстом Цикл
					Сч = Сч + 1;
					Если Сч <= смсКоличествоСообщенийВОдномПакете Тогда
						МассивНомеров.Добавить(СтрАдрес.НомерПолучателя);
					Иначе
						РезультатОтправки = ОтправкаSMS.ОтправитьSMS(МассивНомеров, СтрТекст.ТекстСообщения, СтрАдрес.НомерОтправителя, ОтправлятьВТранслите);
						Если РезультатОтправки.ОтправленныеСообщения.Количество() = 0 Тогда
							ОтменитьТранзакцию();
							Продолжить;
						КонецЕсли;
						ОтразитьРезультатыОтправкиSMSВДокументе(Выборка.Сообщение, РезультатОтправки);

						МассивНомеров.Очистить();
						МассивНомеров.Добавить(СтрАдрес.НомерПолучателя);

						Сч = 1;
					КонецЕсли;
				КонецЦикла;	
				//МассивНомеров = ТаблицаАдресатовСообщения.ВыгрузитьКолонку("НомерПолучателя");
				Если МассивНомеров.Количество() > 0 Тогда
					РезультатОтправки = ОтправкаSMS.ОтправитьSMS(МассивНомеров, СтрТекст.ТекстСообщения, СтрАдрес.НомерОтправителя, ОтправлятьВТранслите);

					Если РезультатОтправки.ОтправленныеСообщения.Количество() = 0 Тогда
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
							НачатьТранзакцию();
						КонецЕсли;
						Продолжить;
					КонецЕсли;

					ОтразитьРезультатыОтправкиSMSВДокументе(Выборка.Сообщение, РезультатОтправки);
				КонецЕсли;

			КонецЦикла;

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось отправить : %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Сообщение);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(
			ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации("Отправка SMS", УровеньЖурналаРегистрации.Предупреждение, МетаданныеДокумента,
			Выборка.Сообщение, ТекстСообщения);

		КонецПопытки;

	КонецЦикла;

	ТаблицаИзменившихсяСтатусов = Новый ТаблицаЗначений;
	ТаблицаИзменившихсяСтатусов.Колонки.Добавить("ИдентификаторСообщения");
	ТаблицаИзменившихсяСтатусов.Колонки.Добавить("СостояниеСообщения");

	Результат = ПолучитьСообщенияДляОбновления();

	ВыборкаДокументы = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаДокументы.Следующий() Цикл

		ТаблицаИзменившихсяСтатусов.Очистить();

		НачатьТранзакцию();
		Попытка

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.смсСостоянияСообщений.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Сообщение", ВыборкаДокументы.Сообщение);
			Блокировка.Заблокировать();

			ВыборкаИдентификаторы = ВыборкаДокументы.Выбрать();
			Пока ВыборкаИдентификаторы.Следующий() Цикл

				СостояниеСообщения = СостояниеСообщенияSMSСогласноСтатусуДоставки(ОтправкаSMS.СтатусДоставки(
				ВыборкаИдентификаторы.ИдентификаторСообщения));

				Если СостояниеСообщения <> ВыборкаИдентификаторы.СостояниеСообщения Тогда

					НоваяСтрока = ТаблицаИзменившихсяСтатусов.Добавить();
					НоваяСтрока.ИдентификаторСообщения  = ВыборкаИдентификаторы.ИдентификаторСообщения;
					НоваяСтрока.СостояниеСообщения 		= СостояниеСообщения;

				КонецЕсли;

			КонецЦикла;

			Если ТаблицаИзменившихсяСтатусов.Количество() = 0 Тогда
				Продолжить;
				ОтменитьТранзакцию();
			КонецЕсли;

			НаборЗаписей = РегистрыСведений.смсСостоянияСообщений.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Сообщение.Значение 		= ВыборкаДокументы.Сообщение;
			НаборЗаписей.Отбор.Сообщение.Использование 	= Истина;
			НаборЗаписей.Прочитать();

			ТаблицаЗаписей = НаборЗаписей.Выгрузить();

			Для Каждого ИзменившийсяСтатус Из ТаблицаИзменившихсяСтатусов Цикл

				МассивСтрок = ТаблицаЗаписей.НайтиСтроки(Новый Структура("ИдентификаторСообщения",
				ИзменившийсяСтатус.ИдентификаторСообщения));

				Для Каждого НайденнаяСтрока Из МассивСтрок Цикл

					НайденнаяСтрока.ИдентификаторСообщения 	= ИзменившийсяСтатус.ИдентификаторСообщения;
					НайденнаяСтрока.Статус					= ИзменившийсяСтатус.СостояниеСообщения;

				КонецЦикла;

			КонецЦикла;

			НаборЗаписей.Загрузить(ТаблицаЗаписей);
			НаборЗаписей.Записать();

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось обновить статусы доставки : %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ВыборкаДокументы.Сообщение);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(
			ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации("Отправка SMS", УровеньЖурналаРегистрации.Предупреждение, МетаданныеДокумента,
			ВыборкаДокументы.Сообщение, ТекстСообщения);

		КонецПопытки;

	КонецЦикла;
	#КонецУдаления
	#Вставка
	ОбработкаСсылка = Справочники.ДополнительныеВнешниеОбработки.НайтиПоРеквизиту("ИмяОбъекта", "ИнформированиеОбОстатках");
	ОбработкаСсылкаОбъект = ДополнительныеОтчетыИОбработки.ОбъектВнешнейОбработки(ОбработкаСсылка);
	ОбработкаСсылкаОбъект.ОтправитьИнформациюОбОстатках();
	#КонецВставки

КонецПроцедуры
