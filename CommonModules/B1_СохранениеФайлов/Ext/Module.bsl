
Процедура B1_СохранитьБаркодыВExcelФайл(ИмяФайла, ТипФайла, ОбъектСсылка) Экспорт
	
	НовыйТабличныйДокумент = Новый ТабличныйДокумент;                  
	ШапкаТаблицы = Новый Массив;
	ШапкаТаблицы.Добавить("Артикул WB");
	ШапкаТаблицы.Добавить("Баркод");
	ШапкаТаблицы.Добавить("Размер");
	ШапкаТаблицы.Добавить("! Баркода для одного артикула продавца необходимо указать в одной строке и через разделитель ;");
	
	СчетчикКолонок = 1;
	ПерваяСтрока = 1;
	ШрифтШапки = Новый Шрифт(,, Истина);
	Для каждого ЭлементШапки Из ШапкаТаблицы Цикл
		ОбластьШапки = НовыйТабличныйДокумент.Область(ПерваяСтрока, СчетчикКолонок);
		ОбластьШапки.Текст = ЭлементШапки;
		ОбластьШапки.Шрифт = ШрифтШапки;
		ОбластьШапки.ШиринаКолонки = 15;
		
	    СчетчикКолонок = СчетчикКолонок + 1;
	КонецЦикла;
		
	СтрокаФайла = ПерваяСтрока + 1;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументТовары.Номенклатура КАК Номенклатура,
		|	ДокументТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДокументТовары.Размер КАК Размер,
		|	ДокументТовары.СерияНоменклатуры.Наименование КАК СерияНоменклатурыНаименование
		|ИЗ
		|	Документ.%ИмяДокумента%.Товары КАК ДокументТовары
		|ГДЕ
		|	ДокументТовары.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры,
		|	Размер,
		|	СерияНоменклатурыНаименование
		|ИТОГИ ПО
		|	Номенклатура,
		|	ХарактеристикаНоменклатуры,
		|	Размер";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "%ИмяДокумента%", ОбъектСсылка.Метаданные().Имя);
	
	Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
	
		ВыборкаХарактеристикаНоменклатуры = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаХарактеристикаНоменклатуры.Следующий() Цикл
			
			Если ЗначениеЗаполнено(ВыборкаХарактеристикаНоменклатуры.ХарактеристикаНоменклатуры) Тогда
				//АртикулДляВБ = ВыборкаХарактеристикаНоменклатуры.Номенклатура.Артикул + ВыборкаХарактеристикаНоменклатуры.ХарактеристикаНоменклатуры.B1_ДопАртикулДляВБ;
				АртикулДляВБ = ВыборкаХарактеристикаНоменклатуры.ХарактеристикаНоменклатуры.B1_АртикулВБ;
			Иначе
				//АртикулДляВБ = ВыборкаХарактеристикаНоменклатуры.Номенклатура.Артикул + ВыборкаХарактеристикаНоменклатуры.Номенклатура.B1_ДопАртикулДляВБПоУмолчанию;	
				АртикулДляВБ = ВыборкаХарактеристикаНоменклатуры.Номенклатура.B1_АртикулВБ;	
			КонецЕсли; 
			
			ВыборкаРазмер = ВыборкаХарактеристикаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
			Пока ВыборкаРазмер.Следующий() Цикл
								
				РазмерВБ = ?(ЗначениеЗаполнено(ВыборкаРазмер.Размер), ВыборкаРазмер.Размер, "0");
				
				МассивШтрихкодов = Новый Массив;
				ОсновнойШтрихкодВБ = "";
				//ОсновнойШтрихкодВБ = ПолучитьОсновнойШтрихкодВБ(ВыборкаРазмер);
				//МассивШтрихкодов.Добавить(ОсновнойШтрихкодВБ);
				
				ВыборкаДетальныеЗаписи = ВыборкаРазмер.Выбрать();
	
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					Если ВыборкаДетальныеЗаписи.СерияНоменклатурыНаименование <> ОсновнойШтрихкодВБ Тогда
						МассивШтрихкодов.Добавить(ВыборкаДетальныеЗаписи.СерияНоменклатурыНаименование);
					КонецЕсли; 
				КонецЦикла;
				
				СвернутыйМассив = Новый Массив;

				Для Каждого Штрихкод Из МассивШтрихкодов Цикл
					Если СвернутыйМассив.Найти(Штрихкод) = Неопределено Тогда
						СвернутыйМассив.Добавить(Штрихкод);
					КонецЕсли;
				КонецЦикла;
				
				НовыйТабличныйДокумент.Область(СтрокаФайла, 1).Текст = Формат(АртикулДляВБ, "ЧГ=0");
				НовыйТабличныйДокумент.Область(СтрокаФайла, 2).Текст = СтрСоединить(СвернутыйМассив, ";");
				НовыйТабличныйДокумент.Область(СтрокаФайла, 3).Текст = СтрЗаменить(РазмерВБ, ".", ",");
				
				СтрокаФайла = СтрокаФайла + 1;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ШрифтДокумента = Новый Шрифт(,10);
	НовыйТабличныйДокумент.Область(1, 1, СтрокаФайла, СчетчикКолонок).Шрифт = ШрифтДокумента;
	НовыйТабличныйДокумент.Записать(ИмяФайла, ТипФайла);
		
КонецПроцедуры

Функция ПолучитьОсновнойШтрихкодВБ(СтрокаВыборки) Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СерииНоменклатуры.Наименование КАК Наименование
		|ИЗ
		|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.Владелец = &Владелец
		|	И СерииНоменклатуры.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
		|	И СерииНоменклатуры.Размер = &Размер
		|
		|УПОРЯДОЧИТЬ ПО
		|	СерииНоменклатуры.Код УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", СтрокаВыборки.Номенклатура);
	Запрос.УстановитьПараметр("Размер", СтрокаВыборки.Размер);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаВыборки.ХарактеристикаНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВозвращаемоеЗначение = ВыборкаДетальныеЗаписи.Наименование;	
	Иначе	
		ВозвращаемоеЗначение = "";	
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции
