&НаСервере
Функция ПолучитьЦенуНоменклатуры(СтрокаТЗ)
	
	ЦенаНоменклатуры = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
	|ГДЕ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен = &ТипЦен
	|	И ЦеныНоменклатурыСрезПоследних.Номенклатура = &Номенклатура
	|	И ЦеныНоменклатурыСрезПоследних.СерияНоменклатуры = &СерияНоменклатуры
	|	И ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
	|	И ЦеныНоменклатурыСрезПоследних.Размер = &Размер
	|	И ЦеныНоменклатурыСрезПоследних.Период <= &Период
	|	И ЦеныНоменклатурыСрезПоследних.Валюта = &Валюта";

	Запрос.УстановитьПараметр("Номенклатура", СтрокаТЗ.Номенклатура);
	Запрос.УстановитьПараметр("СерияНоменклатуры", СтрокаТЗ.СерияНоменклатуры);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаТЗ.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("Размер", СтрокаТЗ.Размер);
	Запрос.УстановитьПараметр("ТипЦен", ТипВыгружаемыхЦен); 
	Запрос.УстановитьПараметр("Период", КонецДня(ДатаПечатиИЦен));
	Запрос.УстановитьПараметр("Валюта", ВалютаВыгружаемыхЦен);

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЦенаНоменклатуры = ВыборкаДетальныеЗаписи.Цена;
	КонецЕсли;
	
	Возврат ЦенаНоменклатуры;

КонецФункции


&НаСервере
&ИзменениеИКонтроль("ЗаполнитьПоОтбору")
Процедура B1_ЗаполнитьПоОтбору(ДанныеПечати)

	Товары.Очистить();
	#Вставка
	Если ТипВыгружаемыхЦен = Справочники.ТипыЦенНоменклатуры.ПустаяСсылка() Тогда
		Сообщение = Новый СообщениеПользователю();
	    Сообщение.Текст = "Не выбран тип цен, цены не выгружаются.";
		Сообщение.Сообщить();
	КонецЕсли;
	
	Если ДатаПечатиИЦен = Дата(1,1,1) Тогда
		ДатаПечатиИЦен = ТекущаяДата();
	КонецЕсли;
	
	Если ВалютаВыгружаемыхЦен = Справочники.Валюты.ПустаяСсылка() Тогда
		ВалютаВыгружаемыхЦен = Справочники.Валюты.НайтиПоКоду(643) ;
	КонецЕсли;
	#КонецВставки
	
	ЕстьУсловиеПоДокументу = Ложь;
	УсловиеНаДокумент = Новый ПолеКомпоновкиДанных("Документ");
	Для Каждого Стр Из ОтборыСКД.Настройки.Отбор.Элементы Цикл
		Если Стр.Использование = Истина и Стр.ЛевоеЗначение = УсловиеНаДокумент Тогда
			ЕстьУсловиеПоДокументу = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ЕстьУсловиеПоДокументу = Ложь Тогда
		СхемаСКД = Обработки.ПечатьЭтикетокИЦенников.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Иначе
		СхемаСКД = Обработки.ПечатьЭтикетокИЦенников.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанныхТекущийДокумент");
	КонецЕсли;
	СхемаСКД.Параметры.ПоСправочнымДанным.Значение = НЕ ПоОстаткам;
	//Добавляем в выбранные поля
	Для Каждого ТекПараметр Из ПараметрыШаблона Цикл

		Если ТекПараметр.Представление = "ЦенаСтарая" Тогда
			Продолжить;
		ИначеЕсли Лев(ТекПараметр.Представление, 7) = "Адреса." Тогда
			СхемаСКД.Параметры["Нужен"+Сред(ТекПараметр.Представление, 8)].Значение = Истина;
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных(Сред(ТекПараметр.Представление, 8));
		ИначеЕсли Лев(ТекПараметр.Представление, 16) = "ДанныеДокумента." Тогда
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных(Сред(ТекПараметр.Представление, 17));
		ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "штрихкод" Тогда	
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных("СерияНоменклатуры.Наименование"); 
		ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "номерпаспорта" Тогда	
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных("СерияНоменклатуры.НомерПаспорта");
		ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "уин" Тогда	
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных("СерияНоменклатуры.УИН");	
		Иначе
			ВыбранноеПоле = СхемаСКД.НастройкиПоУмолчанию.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Заголовок     = ТекПараметр.Значение;
			ВыбранноеПоле.Использование = Истина; 
			ВыбранноеПоле.Поле          = Новый ПолеКомпоновкиДанных(ТекПараметр.Представление); 	
		КонецЕсли;	

	КонецЦикла;

	Для Каждого Стр Из ОтборыСКД.Настройки.Отбор.Элементы Цикл
		Если Стр.Использование = Истина Тогда
			НовОтбор = СхемаСКД.НастройкиПоУмолчанию.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовОтбор, Стр);
		КонецЕсли;
	КонецЦикла;

	Если НЕ ДанныеПечати = Неопределено Тогда
		НовОтбор = СхемаСКД.НастройкиПоУмолчанию.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовОтбор.Использование = Истина;
		НовОтбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СерияНоменклатуры");
		НовОтбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НовОтбор.ПравоеЗначение = ДанныеПечати.СерияНоменклатуры;

		СхемаСКД.Параметры.ОрганизацияПред.Значение = НастройкиПечатиОднойСтроки[5].Значение;
		СхемаСКД.Параметры.СкладПред.Значение = НастройкиПечатиОднойСтроки[6].Значение;
		СхемаСКД.Параметры.ЦенаВРозницеПред.Значение = ДанныеПечати.ЦенаВРознице;

	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
	СхемаСКД, СхемаСКД.НастройкиПоУмолчанию, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);

	ПараметрыСформатированием = ПолучитьПараметрыСФорматированием();
	Для Каждого Стр Из ТаблицаЗначений Цикл
		НовСтр = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Стр);
		#Вставка
		ЦенаНоменклатуры = ПолучитьЦенуНоменклатуры(Стр);
		НовСтр.Цена = ЦенаНоменклатуры;
		#КонецВставки
		НовСтр.Пометка = Истина;
		Для Каждого ТекПараметр Из ПараметрыШаблона Цикл
			НовСтрТабВычПолей = НовСтр.ТабВычПолей.Добавить();
			Если ТекПараметр.Представление = "ЦенаСтарая" Тогда
				Если ВариантЗначенияТипЦен = 0 Тогда
					Если СокрЛП(ЕдиницаИзмененияФормула) = "%" Тогда						
						НовСтрТабВычПолей.Значение = Стр.Цена * (100 + Число(ЗнакИзмененияФормула)
						* ВеличинаИзмененияФормула) / 100;
					Иначе					
						НовСтрТабВычПолей.Значение = Стр.Цена + Число(ЗнакИзмененияФормула) * ВеличинаИзмененияФормула;
					КонецЕсли;
				ИначеЕсли ВариантЗначенияТипЦен = 1 Тогда	
					Если СокрЛП(ЕдиницаИзмененияФормула) = "%" Тогда						
						НовСтрТабВычПолей.Значение = 0;
					Иначе					
						НовСтрТабВычПолей.Значение = Число(ЗнакИзмененияФормула) * ВеличинаИзмененияФормула;
					КонецЕсли;
				КонецЕсли;
				НовСтрТабВычПолей.Значение = Ценообразование.ОкруглитьЦену(НовСтрТабВычПолей.Значение, ПорядокОкругления, ОкруглятьВБольшуюСторону);
			ИначеЕсли ТекПараметр.Представление = "ЦенаСтараяЗаГрамм" Тогда
				Если ВариантЗначенияТипЦен = 0 Тогда
					Если СокрЛП(ЕдиницаИзмененияФормула) = "%" Тогда						
						НовСтрТабВычПолей.Значение = Стр.Цена * (100 + Число(ЗнакИзмененияФормула)
						* ВеличинаИзмененияФормула) / 100;
					Иначе					
						НовСтрТабВычПолей.Значение = Стр.Цена + Число(ЗнакИзмененияФормула) * ВеличинаИзмененияФормула;
					КонецЕсли;
				ИначеЕсли ВариантЗначенияТипЦен = 1 Тогда	
					Если СокрЛП(ЕдиницаИзмененияФормула) = "%" Тогда						
						НовСтрТабВычПолей.Значение = 0;
					Иначе					
						НовСтрТабВычПолей.Значение = Число(ЗнакИзмененияФормула) * ВеличинаИзмененияФормула;
					КонецЕсли;
				КонецЕсли;
				НовСтрТабВычПолей.Значение = ?(Стр.Вес = 0, 0, НовСтрТабВычПолей.Значение/Стр.Вес);
				НовСтрТабВычПолей.Значение = Ценообразование.ОкруглитьЦену(НовСтрТабВычПолей.Значение, ПорядокОкругления, ОкруглятьВБольшуюСторону);
			ИначеЕсли ТекПараметр.Представление = "ХарактеристикаНоменклатуры.НаименованиеБезПокрытияИДопМатериалов" Тогда
				Если ЗначениеЗаполнено(Стр.ХарактеристикаНоменклатуры) И ЗначениеЗаполнено(Стр.ХарактеристикаНоменклатуры.Покрытие) Тогда
					ТекНаименование = СокрЛП(Стр.ХарактеристикаНоменклатуры);
					Если СтрНачинаетсяС(ТекНаименование, "Покрытие:") Тогда 
						НовСтрТабВычПолей.Значение = "";
					Иначе
						НовСтрТабВычПолей.Значение = Лев(ТекНаименование, СтрНайти(ТекНаименование, ", покрытие: ") -1);
					КонецЕсли;
				Иначе	
					НовСтрТабВычПолей.Значение = СокрЛП(Стр.ХарактеристикаНоменклатуры);
				КонецЕсли;
			ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "штрихкод" Тогда
				НовСтрТабВычПолей.Значение = Стр.СерияНоменклатурыНаименование;
			ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "номерпаспорта" Тогда
				НовСтрТабВычПолей.Значение = Стр.СерияНоменклатурыНомерПаспорта;
			ИначеЕсли ВходитВСписокПолейШтрихКод(ТекПараметр.Представление) = "уин" Тогда
				НовСтрТабВычПолей.Значение = Стр.СерияНоменклатурыУИН;	
			ИначеЕсли Лев(ТекПараметр.Представление, 7) = "Адреса." Тогда
				НовСтрТабВычПолей.Значение = Стр[Сред(ТекПараметр.Представление, 8)];
			ИначеЕсли Лев(ТекПараметр.Представление, 16) = "ДанныеДокумента." Тогда
				НовСтрТабВычПолей.Значение = Стр[Сред(ТекПараметр.Представление, 17)];	
			ИначеЕсли СтрНайти(ТекПараметр.Представление, "[<") > 0 Тогда	
				ИмяПоля = СтрЗаменить(ТекПараметр.Представление, "[", "_");
				ИмяПоля = СтрЗаменить(ИмяПоля, "<", "_");
				ИмяПоля = СтрЗаменить(ИмяПоля, "]", "_");
				ИмяПоля = СтрЗаменить(ИмяПоля, ">", "_");
				ИмяПоля = СтрЗаменить(ИмяПоля, ".", "");
				НовСтрТабВычПолей.Значение = Стр[ИмяПоля];
			#Вставка
			ИначеЕсли ТекПараметр.Представление = "Цена" И ЗначениеЗаполнено(ЦенаНоменклатуры) Тогда	
				НовСтрТабВычПолей.Значение = ЦенаНоменклатуры;
			#КонецВставки
			Иначе
				ИмяПоля = СтрЗаменить(ТекПараметр.Представление, ".", "");
				НовСтрТабВычПолей.Значение = Стр[ИмяПоля];
			КонецЕсли;	

			НовСтрТабВычПолей.Поле = ТекПараметр.Значение;
			НовСтрТабВычПолей.ЗаголовокПоля = ТекПараметр.Представление;
			ЕстьФорматирование = ПараметрыСформатированием.НайтиПоЗначению(ТекПараметр.Значение);
			Если ЕстьФорматирование <> Неопределено Тогда
				НовСтрТабВычПолей.Значение = Формат(НовСтрТабВычПолей.Значение, ЕстьФорматирование.Представление);
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("СформироватьДокументНаПечать")
Функция B1_СформироватьДокументНаПечать(ДанныеПечати)

	Эталон = Обработки.ПечатьЭтикетокИЦенников.ПолучитьМакет("Эталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Если Объект.ФорматПечати = 1 Тогда //принтер этикеток.
		КоличествоСтрок   = 1;
		КоличествоКолонок = 1;
	ИначеЕсли Объект.ФорматПечати = 2 Тогда //А4 по колонкам.
		КоличествоСтрок   = КоличествоПоВертикали;
		КоличествоКолонок = КоличествоПоГоризонтали;
	КонецЕсли;

	//СписокНоменклатуры = Новый ТаблицаЗначений;
	Если ДанныеПечати = Неопределено Тогда
		ТоварыЗначение = РеквизитФормыВЗначение("Товары");
		Отбор		 = Новый Структура("Пометка", Истина);
		СписокПечати = ТоварыЗначение.НайтиСтроки(Отбор);
	Иначе

		Объект.ИмяПринтераЭтикеток = НастройкиПечатиОднойСтроки[0].Значение;
		Объект.ТипЭтикетки = НастройкиПечатиОднойСтроки[1].Значение;
		Объект.ФорматПечати = НастройкиПечатиОднойСтроки[2].Значение;
		Объект.Книжная = НастройкиПечатиОднойСтроки[3].Значение;
		Объект.СразуНаПринтер = НастройкиПечатиОднойСтроки[4].Значение;

		ПоОстаткам = Ложь;
		
		УстановитьШаблонПечати();

		Если ШаблонПечати.ОбластьПечати = Неопределено Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не задана область печати в макете этикетки!");
			Возврат Неопределено;
		КонецЕсли;

		ЗаполнитьПоОтбору(ДанныеПечати);
		
		ТоварыЗначение = РеквизитФормыВЗначение("Товары");
		Отбор		 = Новый Структура("Пометка", Истина);
		СписокПечати = ТоварыЗначение.НайтиСтроки(Отбор);

	КонецЕсли;

	ДокументПечати = Новый ТабличныйДокумент;
	ДокументПечати.ОриентацияСтраницы =  ?(Объект.Книжная = 1, ОриентацияСтраницы.Портрет, ОриентацияСтраницы.Ландшафт);
	ДокументПечати.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПечатьШК" + СокрЛП(ИмяКомпьютера());

	НомерСтрока = 1;
	НомерКолонка = 0;

	ШаблонПечатиЕсть = Объект.ТипЭтикетки.Шаблон.Получить();

	// Добавляем в документ область для каждой печатаемой строки.
	Для Каждого ЭлементПечати Из СписокПечати Цикл

			Область = ШаблонПечати.ПолучитьОбласть(ШаблонПечати.ОбластьПечати.Верх, ШаблонПечати.ОбластьПечати.Лево,
				ШаблонПечати.ОбластьПечати.Низ, ШаблонПечати.ОбластьПечати.Право);

			Для Каждого ТекСтрТабВычПолей Из ЭлементПечати.ТабВычПолей Цикл
				ИмяШК = ВходитВСписокПолейШтрихКод(ТекСтрТабВычПолей.ЗаголовокПоля);
				Если ИмяШК <> "" Тогда		
					Для Каждого Рисунок Из Область.Рисунки Цикл
						Если СтрНачинаетсяС(НРег(Рисунок.Имя), ИмяШК) = Истина или СтрНачинаетсяС(НРег(Рисунок.Имя), "серияноменклатуры."+ИмяШК) = Истина Тогда
							
							НастройкаШК = ШаблонПечатиЕсть.ТаблицаНастроекШтрихкодов.Найти(Рисунок.Имя, "ИмяШтрикода");
							ПараметрыШтрихкода = Новый Структура;
							Если НастройкаШК.ТипКода = 1 Тогда
								ПараметрыШтрихкода.Вставить("Ширина",	Макс(Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе), 220));
							Иначе
								ПараметрыШтрихкода.Вставить("Ширина",	Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
							КонецЕсли;

							ПараметрыШтрихкода.Вставить("Высота",	Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
							
							Если НастройкаШК.ТипКода = 16 Тогда
								МинСторона = Мин(ПараметрыШтрихкода.Ширина, ПараметрыШтрихкода.Высота);
								ПараметрыШтрихкода.Ширина = МинСторона;
								ПараметрыШтрихкода.Высота = МинСторона;
							КонецЕсли;
							
							ПараметрыШтрихкода.Вставить("УровеньКоррекцииQR", НастройкаШК.УровеньКоррекцииQR);
							ПараметрыШтрихкода.Вставить("Штрихкод", ТекСтрТабВычПолей.Значение);
							ПараметрыШтрихкода.Вставить("ТипКода", НастройкаШК.ТипКода);
							ПараметрыШтрихкода.Вставить("ОтображатьТекст",		НастройкаШК.ОтображатьТекст);							
							ПараметрыШтрихкода.Вставить("РазмерШрифта",			НастройкаШК.РазмерШрифта);
							ПараметрыШтрихкода.Вставить("УголПоворота", НастройкаШК.УголПоворота);
							
							РезультатОперации = ГенерацияШтрихкода.ИзображениеШтрихкода(ПараметрыШтрихкода);
							Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
								Рисунок.Картинка = РезультатОперации.Картинка;
							КонецЕсли;	
							#Вставка
							Если СтрНайти(Объект.ТипЭтикетки.Наименование,"Шаблон Большой") И НастройкаШК.ТипКода = 1 Тогда
								Рисунок.Ширина = "48";
							КонецЕсли;
							#КонецВставки
							
						КонецЕсли;
						
					КонецЦикла;
					#Вставка
					Если ТекСтрТабВычПолей.ЗаголовокПоля = "УИНЛЕВ8" Тогда
						Если ЗначениеЗаполнено(ТекСтрТабВычПолей.Значение) И СтрДлина(ТекСтрТабВычПолей.Значение) > 7 Тогда
							ТекСтрТабВычПолей.Значение = Лев(ТекСтрТабВычПолей.Значение, 8);
						КонецЕсли;
					ИначеЕсли ТекСтрТабВычПолей.ЗаголовокПоля = "УИНПРАВ8" Тогда
						Если ЗначениеЗаполнено(ТекСтрТабВычПолей.Значение) И СтрДлина(ТекСтрТабВычПолей.Значение) > 7 Тогда
							ТекСтрТабВычПолей.Значение = Прав(ТекСтрТабВычПолей.Значение, 8);
						КонецЕсли;
					КонецЕсли;
					#КонецВставки
					//заполним параметр с именем картинки, если он есть
					СтруктураЗаполнения = Новый Структура(ТекСтрТабВычПолей.Поле, СокрЛП(ТекСтрТабВычПолей.Значение));
					Область.Параметры.Заполнить(СтруктураЗаполнения); 
				Иначе	
					Область.Параметры[ТекСтрТабВычПолей.Поле] = СокрЛП(ТекСтрТабВычПолей.Значение);
				КонецЕсли;
			КонецЦикла;		
			
			НужноКоличество = ?(ЭлементПечати.Количество = 0, 1, ЭлементПечати.Количество);
			Для Сч = 1 По НужноКоличество Цикл
				НомерКолонка = НомерКолонка + 1;
				Если НомерКолонка > КоличествоКолонок Тогда
					НомерСтрока = НомерСтрока + 1;
					Если НомерСтрока > КоличествоСтрок Тогда
						ДокументПечати.ВывестиГоризонтальныйРазделительСтраниц();
						ДокументПечати.Вывести(Область);
						НомерСтрока = 1;
						НомерКолонка = 1;
					Иначе
						ДокументПечати.Вывести(Область);
						НомерКолонка = 1;
					КонецЕсли;
				Иначе
					ДокументПечати.Присоединить(Область);
				КонецЕсли;
				
			КонецЦикла;

	КонецЦикла;

	Возврат ДокументПечати;

КонецФункции

&НаКлиенте
Функция ПолучитьИмяПринтераЭтикеток()

   ИмяПринтера = "";
   Попытка
	   COMОбъект     = Новый COMОбъект("WbemScripting.SWbemLocator");
	   COMСоединение = COMОбъект.ConnectServer(".");
	   
	   УстановкаОбъект   = COMСоединение.InstancesOf("Win32_Printer");
	   Для Каждого ЭлементОбъект Из УстановкаОбъект Цикл
		   мИмяПринтера = СокрЛП(ЭлементОбъект.Caption);
		   Если СтрНайти(мИмяПринтера, "Intermec PC43t") > 0 Тогда
			   ИмяПринтера = мИмяПринтера;
			   Прервать;
		   КонецЕсли;
       КонецЦикла;
   Исключение
   КонецПопытки;

   Возврат ИмяПринтера;

КонецФункции

&НаКлиенте
&ИзменениеИКонтроль("ПечатьЭтикеток")
Процедура B1_ПечатьЭтикеток(Товары)

	ДокументПечати = СформироватьДокументНаПечать();

	Если ДокументПечати <> Неопределено Тогда

		Если Не ПустаяСтрока(Объект.ИмяПринтераЭтикеток) Тогда
			ДокументПечати.ИмяПринтера = СокрЛП(Объект.ИмяПринтераЭтикеток);
			#Вставка
		Иначе
			ИмяПринтера = ПолучитьИмяПринтераЭтикеток();
			Если ЗначениеЗаполнено(ИмяПринтера) Тогда
				Объект.ИмяПринтераЭтикеток = ИмяПринтера;
				ДокументПечати.ИмяПринтера = ИмяПринтера;
				ИмяПринтераЭтикеток = ИмяПринтера;
			КонецЕсли;
			#КонецВставки
		КонецЕсли;

		ДокументПечати.ПолеСверху              = 0;
		ДокументПечати.ПолеСлева               = 0;
		ДокументПечати.ПолеСнизу               = 0;
		ДокументПечати.ПолеСправа              = 0;
		ДокументПечати.РазмерКолонтитулаСверху = 0;
		ДокументПечати.РазмерКолонтитулаСнизу  = 0;

		Если Объект.СразуНаПринтер Тогда

			ДокументПечати.Напечатать();

		Иначе

			ДокументПечати.Защита              = Ложь;
			ДокументПечати.ТолькоПросмотр      = Истина;
			ДокументПечати.ОтображатьСетку     = Ложь;
			ДокументПечати.ОтображатьЗаголовки = Ложь;

			ДокументПечати.Показать("Печать этикеток товаров");

		КонецЕсли;

	КонецЕсли;
КонецПроцедуры
