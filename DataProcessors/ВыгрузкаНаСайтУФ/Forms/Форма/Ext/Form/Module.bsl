&НаКлиенте
Процедура ЗаполнитьТаблицуЗначений(РабочаяТаблицаЗначений, РабочаяКолонка, Значение)
	РабочаяТаблицаЗначений.Очистить();
	Для каждого ВыбранноеЗначение Из Значение Цикл
		НоваяСтрока = РабочаяТаблицаЗначений.Добавить();	
		НоваяСтрока[РабочаяКолонка] = ВыбранноеЗначение;
	КонецЦикла; 
КонецПроцедуры

&НаСервере
Функция РезультатВыбораЦенВСтроку(Знач РезультатВыбора)
	Результат = "";
	Для каждого ТипЦены Из РезультатВыбора Цикл
		Результат = Результат + ТипЦены.ВидЦены.Наименование + "; ";
	КонецЦикла; 
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПриЗакрытииФормыВыбора(Значение, ДопПараметр) Экспорт
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Если ДопПараметр = "ВыборСкладов" Тогда
			ЗаполнитьТаблицуЗначений(СкладыТаблицаЗначений, "Склад", Значение);
		ИначеЕсли ДопПараметр = "ВыборНоменклатуры" Тогда
			ЗаполнитьТаблицуЗначений(НоменклатураТаблицаЗначений, "Номенклатура", Значение);
		ИначеЕсли ДопПараметр = "ВыборВидовЦен" Тогда
			ЗаполнитьТаблицуЗначений(РезультатВыбораЦен, "ВидЦены", Значение);
			ВидыЦен = РезультатВыбораЦенВСтроку(РезультатВыбораЦен);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборСкладов(Команда)
	ПараметрыВыбора = ПолучитьПараметрыВыбора();
	ОбработкаВыбора = Новый ОписаниеОповещения("ПриЗакрытииФормыВыбора", ЭтаФорма, "ВыборСкладов");
	ОткрытьФорму("Справочник.Склады.ФормаВыбора", ПараметрыВыбора, Элементы.СкладыТаблицаЗначений,,,, ОбработкаВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокСкладов(Команда)
	СкладыТаблицаЗначений.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ВыборНоменклатуры(Команда)
	ПараметрыВыбора = ПолучитьПараметрыВыбора();
	ОбработкаВыбора = Новый ОписаниеОповещения("ПриЗакрытииФормыВыбора", ЭтаФорма, "ВыборНоменклатуры");
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыВыбора, Элементы.НоменклатураТаблицаЗначений,,,, ОбработкаВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСписокНоменклатуры(Команда)
	НоменклатураТаблицаЗначений.Очистить();
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыВыбора()
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("РежимВыбора", Истина);
	ПараметрыВыбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыВыбора.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Возврат ПараметрыВыбора;
КонецФункции

&НаСервере
Процедура УстановитьСвойствоНаСервере()
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.УстановитьДанные(ЭтотОбъект);

	Если Не ЗначениеЗаполнено(Свойство) Тогда
		Сообщение.Текст = "Не выбрано свойство.";
		Сообщение.Поле = "Свойство";
		Сообщение.Сообщить();
		Возврат;
	ИначеЕсли Свойство.ТипЗначения <> Новый ОписаниеТипов("Булево") Тогда
		Сообщение.Текст = "Тип выбранного совойства не булево.";
		Сообщение.Поле = "Свойство";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если Не СкладыТаблицаЗначений.Количество() Тогда
		Сообщение.Текст = "Не выбраны склады.";
		Сообщение.Поле = "СкладыТаблицаЗначений";
		Сообщение.Сообщить();
		Возврат;
	Иначе
		ВыбранныеСклады = СкладыТаблицаЗначений.Выгрузить();
	КонецЕсли;
	
	ЗаписываемоеЗначениеСвойства = Истина;
	Счетчик = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Объект КАК ОбъектСвойства,
		|	ЗначенияСвойствОбъектов.Значение КАК ЗначениеСвойства
		|ПОМЕСТИТЬ НомеклатураСвойствоАктивность
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|		ПО Номенклатура.Ссылка = ЗначенияСвойствОбъектов.Объект
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Свойство = &СвойствоТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПартииТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки КАК ПартииТоваровНаСкладахОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ НомеклатураСвойствоАктивность КАК НомеклатураСвойствоАктивность
		|		ПО ПартииТоваровНаСкладахОстатки.Номенклатура = НомеклатураСвойствоАктивность.ОбъектСвойства
		|ГДЕ
		|	(НомеклатураСвойствоАктивность.ЗначениеСвойства <> &ИсключаемоеЗначениеСвойства
		|			ИЛИ НомеклатураСвойствоАктивность.ЗначениеСвойства ЕСТЬ NULL )
		|	И ПартииТоваровНаСкладахОстатки.КоличествоОстаток > 0
		|	И ПартииТоваровНаСкладахОстатки.Склад В(&ВыбранныеСклады)";
	
	Запрос.УстановитьПараметр("СвойствоТовара", Свойство);
	Запрос.УстановитьПараметр("ИсключаемоеЗначениеСвойства", ЗаписываемоеЗначениеСвойства);
	Запрос.УстановитьПараметр("ВыбранныеСклады", ВыбранныеСклады.ВыгрузитьКолонку("Склад"));
		
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаписьСвойства = РегистрыСведений.ЗначенияСвойствОбъектов.СоздатьМенеджерЗаписи();
		ЗаписьСвойства.Свойство = Свойство;
		ЗаписьСвойства.Значение = ЗаписываемоеЗначениеСвойства;
		ЗаписьСвойства.Объект = ВыборкаДетальныеЗаписи.Номенклатура;
		ЗаписьСвойства.Записать();
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Если Счетчик > 0 Тогда
		Сообщение.Текст = "Свойство установлено у " + Счетчик + " товаров.";
		Сообщение.Сообщить()
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСвойство(Команда)
	УстановитьСвойствоНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ВидыЦенНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыВыбора = ПолучитьПараметрыВыбора();
	ОбработкаВыбора = Новый ОписаниеОповещения("ПриЗакрытииФормыВыбора", ЭтаФорма, "ВыборВидовЦен");
	ОткрытьФорму("Справочник.ТипыЦенНоменклатуры.ФормаВыбора", ПараметрыВыбора, Элементы.ВидыЦен,,,, ОбработкаВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ВидыЦенОчистка(Элемент, СтандартнаяОбработка)
	РезультатВыбораЦен.Очистить();
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ВидыЦен = РезультатВыбораЦенВСтроку(РезультатВыбораЦен);
КонецПроцедуры

&НаСервере
Функция ВыгрузитьДанныеНаСервере()

	ВозвращаемаяСтруктура = Новый Структура;
	
	Сообщение = Новый СообщениеПользователю();
	Сообщение.УстановитьДанные(ЭтотОбъект);
	
	Если Не СохранитьВФайл И (Не ЗначениеЗаполнено(СерверАдрес) Или Не ЗначениеЗаполнено(СерверПорт) 
		Или Не ЗначениеЗаполнено(СерверЛогин) Или Не ЗначениеЗаполнено(СерверПароль)) Тогда
		Сообщение.Текст = "Заполнены не все данные FTP подключения.";
		Сообщение.Сообщить();
		Возврат ВозвращаемаяСтруктура;
	КонецЕсли;
	
	Если Не СкладыТаблицаЗначений.Количество() Тогда
		Сообщение.Текст = "Не выбраны склады.";
		Сообщение.Поле = "СкладыТаблицаЗначений";
		Сообщение.Сообщить();
		Возврат ВозвращаемаяСтруктура;

	КонецЕсли;
	
	Если Не РезультатВыбораЦен.Количество() Тогда
		Сообщение.Текст = "Не заполнены виды цен.";
		Сообщение.Поле = "ВидыЦен";
		Сообщение.Сообщить();
		Возврат ВозвращаемаяСтруктура;
	КонецЕсли;
	
	ВыбранныеСкладыМассив = СкладыТаблицаЗначений.Выгрузить().ВыгрузитьКолонку("Склад");
	ВыбраннаяНоменклатураМассив = НоменклатураТаблицаЗначений.Выгрузить().ВыгрузитьКолонку("Номенклатура");
	ВыбранныеЦеныМассив = РезультатВыбораЦен.Выгрузить().ВыгрузитьКолонку("ВидЦены");

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХарактеристикиНоменклатуры.Владелец КАК НоменклатураСсылка,
	|	ХарактеристикиНоменклатуры.Ссылка КАК ХарактеристикаНоменклатурыСсылка
	|ПОМЕСТИТЬ ВсяНоменклатураХарактеристики
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НоменклатураТоваров.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|ИЗ
	|	Справочник.Номенклатура КАК НоменклатураТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсяНоменклатураХарактеристики.НоменклатураСсылка КАК НоменклатураСсылка,
	|	ВсяНоменклатураХарактеристики.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка,
	|	ПартииТоваровНаСкладахОстатки.Склад КАК ОстаткиСкладСсылка,
	|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ НоменклатураДляСайта
	|ИЗ
	|	ВсяНоменклатураХарактеристики КАК ВсяНоменклатураХарактеристики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки КАК ПартииТоваровНаСкладахОстатки
	|		ПО ВсяНоменклатураХарактеристики.НоменклатураСсылка = ПартииТоваровНаСкладахОстатки.Номенклатура
	|			И ВсяНоменклатураХарактеристики.ХарактеристикаНоменклатурыСсылка = ПартииТоваровНаСкладахОстатки.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ВыбраннаяНоменклатураКоличество > 0
	|				ТОГДА ВсяНоменклатураХарактеристики.НоменклатураСсылка В ИЕРАРХИИ (&ВыбраннаяНоменклатура)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ВыбранныеСкладыКоличество > 0
	|				ТОГДА ПартииТоваровНаСкладахОстатки.Склад В (&ВыбранныеСклады)
	|						И ПартииТоваровНаСкладахОстатки.КоличествоОстаток > 0
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен КАК ТипЦенСсылка,
	|	НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка,
	|	НоменклатураДляСайта.НоменклатураСсылка КАК НоменклатураСсылка,
	|	МАКСИМУМ(ЦеныНоменклатурыСрезПоследних.Период) КАК Период
	|ПОМЕСТИТЬ ЦеныПоследниеПериод
	|ИЗ
	|	НоменклатураДляСайта КАК НоменклатураДляСайта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
	|		ПО НоменклатураДляСайта.НоменклатураСсылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка = ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен В(&РезультатВыбораЦен)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен,
	|	НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка,
	|	НоменклатураДляСайта.НоменклатураСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен КАК ТипЦенСсылка,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена,
	|	НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка,
	|	НоменклатураДляСайта.НоменклатураСсылка КАК НоменклатураСсылка,
	|	ЦеныНоменклатурыСрезПоследних.Период КАК Период
	|ПОМЕСТИТЬ ЦеныПоследниеВсеЦены
	|ИЗ
	|	НоменклатураДляСайта КАК НоменклатураДляСайта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
	|		ПО НоменклатураДляСайта.НоменклатураСсылка = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка = ЦеныНоменклатурыСрезПоследних.ХарактеристикаНоменклатуры
	|ГДЕ
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен В(&РезультатВыбораЦен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦеныПоследниеВсеЦены.ТипЦенСсылка КАК ТипЦенСсылка,
	|	ЦеныПоследниеВсеЦены.Цена КАК Цена,
	|	ЦеныПоследниеВсеЦены.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка,
	|	ЦеныПоследниеВсеЦены.НоменклатураСсылка КАК НоменклатураСсылка
	|ИЗ
	|	ЦеныПоследниеПериод КАК ЦеныПоследниеПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПоследниеВсеЦены КАК ЦеныПоследниеВсеЦены
	|		ПО (ЦеныПоследниеВсеЦены.ТипЦенСсылка = ЦеныПоследниеПериод.ТипЦенСсылка)
	|			И (ЦеныПоследниеВсеЦены.ХарактеристикаНоменклатурыСсылка = ЦеныПоследниеПериод.ХарактеристикаНоменклатурыСсылка)
	|			И (ЦеныПоследниеВсеЦены.НоменклатураСсылка = ЦеныПоследниеПериод.НоменклатураСсылка)
	|			И (ЦеныПоследниеВсеЦены.Период = ЦеныПоследниеПериод.Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка,
	|	НоменклатураДляСайта.НоменклатураСсылка КАК НоменклатураСсылка,
	|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ПартииТоваровНаСкладахОстатки.Склад КАК СкладСсылка
	|ИЗ
	|	НоменклатураДляСайта КАК НоменклатураДляСайта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Остатки КАК ПартииТоваровНаСкладахОстатки
	|		ПО НоменклатураДляСайта.НоменклатураСсылка = ПартииТоваровНаСкладахОстатки.Номенклатура
	|			И НоменклатураДляСайта.ОстаткиСкладСсылка = ПартииТоваровНаСкладахОстатки.Склад
	|ГДЕ
	|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НоменклатураДляСайта.НоменклатураСсылка КАК НоменклатураСсылка,
	|	НоменклатураДляСайта.ХарактеристикаНоменклатурыСсылка КАК ХарактеристикаНоменклатурыСсылка
	|ИЗ
	|	НоменклатураДляСайта КАК НоменклатураДляСайта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК СкладСсылка
	|ИЗ
	|	Справочник.Склады КАК Склады";
	
	//Запрос.УстановитьПараметр("СвойствоТовара", ПолеВводаСвойство);	
	Запрос.УстановитьПараметр("РезультатВыбораЦен", ВыбранныеЦеныМассив);
	Запрос.УстановитьПараметр("ВыбраннаяНоменклатура", ВыбраннаяНоменклатураМассив);
	Запрос.УстановитьПараметр("ВыбраннаяНоменклатураКоличество", ВыбраннаяНоменклатураМассив.Количество());
	Запрос.УстановитьПараметр("ВыбранныеСклады", ВыбранныеСкладыМассив);
	Запрос.УстановитьПараметр("ВыбранныеСкладыКоличество", ВыбранныеСкладыМассив.Количество());		
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаДетальныеЗаписи = РезультатЗапроса[4].Выбрать();
	
	ЦеныНоменклатуры = Новый Соответствие;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЦенаСтруктура = Новый Структура("id_PriceType", СОКРЛП(ВыборкаДетальныеЗаписи.ТипЦенСсылка.УникальныйИдентификатор()));
		ЦенаСтруктура.Вставить("Цена", Окр(ВыборкаДетальныеЗаписи.Цена,0));
		УИДЭлемента = ПолучитьУИДЭлемента(ВыборкаДетальныеЗаписи);		
		ЦеныМассивСуществующий = ЦеныНоменклатуры.Получить(УИДЭлемента);
		Если ЦеныМассивСуществующий <> Неопределено Тогда
			ЦеныМассив = ЦеныМассивСуществующий;
		Иначе	
			ЦеныМассив = Новый Массив;		
		КонецЕсли;
	    ЦеныМассив.Добавить(ЦенаСтруктура);
		ЦеныНоменклатуры.Вставить(УИДЭлемента, ЦеныМассив);
	КонецЦикла;	
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[5].Выбрать();
	ЗадействованныеСклады = Новый Соответствие;
	ОстаткиНоменклатуры = Новый Соответствие;
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СкладУИД = ВыборкаДетальныеЗаписи.СкладСсылка.УникальныйИдентификатор();
		Если ЗадействованныеСклады.Получить(СкладУИД) = Неопределено Тогда
			ЗадействованныеСклады.Вставить(СкладУИД, ВыборкаДетальныеЗаписи.СкладСсылка);
		КонецЕсли; 
		ОстаткиСтруктура = Новый Структура("id_Stock", СОКРЛП(СкладУИД));
		ОстаткиСтруктура.Вставить("Остаток", ВыборкаДетальныеЗаписи.КоличествоОстаток);
		УИДЭлемента = ПолучитьУИДЭлемента(ВыборкаДетальныеЗаписи);
		ОстаткиМассивСуществующий = ОстаткиНоменклатуры.Получить(УИДЭлемента);
		Если ОстаткиМассивСуществующий <> Неопределено Тогда
			ОстаткиМассив = ОстаткиМассивСуществующий;
		Иначе	
			ОстаткиМассив = Новый Массив;		
		КонецЕсли;
	    ОстаткиМассив.Добавить(ОстаткиСтруктура);
		
		ОстаткиНоменклатуры.Вставить(УИДЭлемента, ОстаткиМассив);
	КонецЦикла;	

	ПредложенияМассив = Новый Массив;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[6].Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТоварСтруктура = Новый Структура;
		УИДЭлемента = ПолучитьУИДЭлемента(ВыборкаДетальныеЗаписи);
		ТоварСтруктура.Вставить("id", СОКРЛП(УИДЭлемента));
		ТоварСтруктура.Вставить("id_product", СОКРЛП(ВыборкаДетальныеЗаписи.НоменклатураСсылка.УникальныйИдентификатор()));
		ТоварСтруктура.Вставить("Artikul", ПреобразоватьСтрокуJSON(СОКРЛП(ВыборкаДетальныеЗаписи.НоменклатураСсылка.Артикул)));
		ТоварСтруктура.Вставить("product_name", ПреобразоватьСтрокуJSON(ВыборкаДетальныеЗаписи.НоменклатураСсылка.Наименование));
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатурыСсылка) Тогда
			ТоварСтруктура.Вставить("name", ПреобразоватьСтрокуJSON(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатурыСсылка.Наименование));
		Иначе
			ТоварСтруктура.Вставить("name", ПреобразоватьСтрокуJSON(ВыборкаДетальныеЗаписи.НоменклатураСсылка.Наименование));
		КонецЕсли;
		//ТоварСтруктура.Вставить("Active", ВыборкаДетальныеЗаписи.ЗначениеСвойства);
		ТоварСтруктура.Вставить("Active", Истина);
		ТоварСтруктура.Вставить("УказанаЦенаЗаИзделие", Истина);
		ЦеныМассивСуществующий = ЦеныНоменклатуры.Получить(УИДЭлемента);
		Если ЦеныМассивСуществующий <> Неопределено Тогда
			ЦеныМассив = ЦеныМассивСуществующий;
		Иначе	
			ЦеныМассив = Новый Массив;		
		КонецЕсли;
        ТоварСтруктура.Вставить("Цены", ЦеныМассив);
		ОстаткиМассивСуществующий = ОстаткиНоменклатуры.Получить(УИДЭлемента);
		Если ОстаткиМассивСуществующий <> Неопределено Тогда
			ОстаткиМассив = ОстаткиМассивСуществующий;
		Иначе	
			ОстаткиМассив = Новый Массив;		
		КонецЕсли;
		ТоварСтруктура.Вставить("Остатки", ОстаткиМассив);
		ПредложенияМассив.Добавить(ТоварСтруктура);
	КонецЦикла;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[7].Выбрать();
	ВсеСклады = Новый Соответствие;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл	
		ВсеСклады.Вставить(ВыборкаДетальныеЗаписи.СкладСсылка.УникальныйИдентификатор(), ВыборкаДетальныеЗаписи.СкладСсылка);
	КонецЦикла;
	
	ПолнаяВыгрузка = Новый Структура("ПолнаяВыгрузка", Истина);
	ПолнаяВыгрузка.Вставить("Предложения", ПредложенияМассив); 
	КлассификаторДляВыгрузки = Новый Структура;
	КлассификаторДляВыгрузки.Вставить("Склады", ПолучитьМассивВыбранныхЭлементов(ВсеСклады));
	КлассификаторДляВыгрузки.Вставить("ВидыЦен", ПолучитьМассивВыбранныхЭлементов(ВыбранныеЦеныМассив));
	
	ОбъектФормы = РеквизитФормыВЗначение("Объект");
	Если СохранитьВФайл Тогда
		ВозвращаемаяСтруктура.Вставить("Предложения", ОбъектФормы.СформироватьСтрокуJSON(ПолнаяВыгрузка));
		ВозвращаемаяСтруктура.Вставить("Классификатор", ОбъектФормы.СформироватьСтрокуJSON(КлассификаторДляВыгрузки));		
	Иначе	
	    ФайлПолнаяВыгрузкаДляFTP = Новый Структура("Имя", "offers.json");
		ФайлПолнаяВыгрузкаДляFTP.Вставить("Каталог", "/json/");
		ФайлПолнаяВыгрузкаДляFTP.Вставить("СтрокаJSON", ОбъектФормы.СформироватьСтрокуJSON(ПолнаяВыгрузка));
		ФайлПолнаяВыгрузкаДляFTP.Вставить("ИмяДляСообщения", "Предложения");
		
		ФайлКлассификаторДляFTP = Новый Структура("Имя", "structure.json");
		ФайлКлассификаторДляFTP.Вставить("Каталог", "/json/");
		ФайлКлассификаторДляFTP.Вставить("СтрокаJSON", ОбъектФормы.СформироватьСтрокуJSON(КлассификаторДляВыгрузки));
		ФайлКлассификаторДляFTP.Вставить("ИмяДляСообщения", "Классификатор");
		
		ФайлыДляFTP = Новый Массив();
		ФайлыДляFTP.Добавить(ФайлПолнаяВыгрузкаДляFTP);
		ФайлыДляFTP.Добавить(ФайлКлассификаторДляFTP);
		
		СохранитьФайлыНаFTP(ФайлыДляFTP);
	КонецЕсли;
	
	Возврат ВозвращаемаяСтруктура;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьДанные(Команда) Экспорт
	ВозвращаемаяСтруктура = ВыгрузитьДанныеНаСервере();
	Если СохранитьВФайл Тогда
		Для каждого СтрокаJSON Из ВозвращаемаяСтруктура Цикл
			СохранитьФайлВыгрузки(СтрокаJSON.Значение, СтрокаJSON.Ключ + "-" + Формат(ТекущаяДата(), "ДЛФ=D") + ".json");
		КонецЦикла; 
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьУИДЭлемента(ВыборкаДетальныеЗаписи)
	Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатурыСсылка) Тогда
		УИДЭлемента = ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатурыСсылка.УникальныйИдентификатор();
	Иначе	
	    УИДЭлемента = ВыборкаДетальныеЗаписи.НоменклатураСсылка.УникальныйИдентификатор();
	КонецЕсли;

	Возврат УИДЭлемента;
КонецФункции 

&НаСервере
Функция ПреобразоватьСтрокуJSON(Знач СторкаJSON)
	СторкаJSON = СтрЗаменить(СторкаJSON, "\", "");
	СторкаJSON = СтрЗаменить(СторкаJSON, """", "");
	Возврат СторкаJSON;
КонецФункции

&НаСервере
Функция ПолучитьМассивВыбранныхЭлементов(МассивЭлементов)
	МассивВозвращаемый = Новый Массив;
	ТипСоответствия = Тип("КлючИЗначение");
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		Если ТипЗнч(ЭлементМассива) = ТипСоответствия Тогда
			ЭлементМассиваСсылка = ЭлементМассива.Значение;
		Иначе
			ЭлементМассиваСсылка = ЭлементМассива;	
		КонецЕсли;
		ЭлементВозвращаемый = Новый Структура("id", СОКРЛП(ЭлементМассиваСсылка.УникальныйИдентификатор()));	
		ЭлементВозвращаемый.Вставить("name", ЭлементМассиваСсылка.Наименование); 
	    МассивВозвращаемый.Добавить(ЭлементВозвращаемый);
	КонецЦикла;
	Возврат МассивВозвращаемый;
КонецФункции

&НаКлиенте
Процедура СохранитьФайлВыгрузки(ТекстФайла, ИмяФайла, ФильтрФайла = "Файл JSON(*.json)|*.json", КодировкаФайла = "UTF-8")
	
	ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогСохраненияФайла.ПолноеИмяФайла = ИмяФайла;                
	ДиалогСохраненияФайла.Фильтр = ФильтрФайла; 
	ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
	Если ДиалогСохраненияФайла.Выбрать() Тогда
		ИмяТекстовогоФайла = ДиалогСохраненияФайла.ПолноеИмяФайла;
		//Запись UTF-8 без BOM
		ЗаписьТекстаВФайл = Новый ЗаписьТекста(ИмяТекстовогоФайла, КодировкаТекста.ANSI);
		ЗаписьТекстаВФайл.Закрыть();
		//Записываем в пустой ANSI файл строку с кодировкой UTF-8, в итоге получется UTF-8 без BOM
		ЗаписьТекстаВФайл = Новый ЗаписьТекста(ИмяТекстовогоФайла, КодировкаФайла,, Истина, Символы.ПС);
		ЗаписьТекстаВФайл.Записать(ТекстФайла);
		ЗаписьТекстаВФайл.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьФайлыНаFTP(ФайлыДляОтправки)

	Сообщение = Новый СообщениеПользователю;
		
	ЗащищенноеСоединение = ?(СерверСоединениеFTPS, Новый ЗащищенноеСоединениеOpenSSL(), Неопределено);
	
	Попытка
		FTPСервер = Новый FTPСоединение(СерверАдрес, СерверПорт, СерверЛогин, СерверПароль,,,, ЗащищенноеСоединение);
	Исключение
		Сообщение.Текст = "Не удалось соединиться с сервером";
		Сообщение.Сообщить();
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	Для каждого ФайлДляОтправки Из ФайлыДляОтправки Цикл
		ВременныйФайл = ПолучитьИмяВременногоФайла();
		//Запись UTF-8 без BOM
		ЗаписьТекстаВФайл = Новый ЗаписьТекста(ВременныйФайл, КодировкаТекста.ANSI);
		ЗаписьТекстаВФайл.Закрыть();
		//Записываем в пустой ANSI файл строку с кодировкой UTF-8, в итоге получется UTF-8 без BOM
		ЗаписьТекстаВФайл = Новый ЗаписьТекста(ВременныйФайл,,, Истина, Символы.ПС);
		ЗаписьТекстаВФайл.Записать(ФайлДляОтправки.СтрокаJSON);
		ЗаписьТекстаВФайл.Закрыть();
		Попытка
			FTPСервер.Записать(ВременныйФайл, ФайлДляОтправки.Каталог + ФайлДляОтправки.Имя);
		Исключение
			Сообщение.Текст = "Не удалось записать файл " + ФайлДляОтправки.Имя;
			Сообщение.Сообщить();
			Сообщение.Текст = ОписаниеОшибки();
			Сообщение.Сообщить();
			Возврат;
		КонецПопытки;
		Сообщение.Текст = "Выгрузка прошла успешно " + ФайлДляОтправки.ИмяДляСообщения;
		Сообщение.Сообщить();
		УдалитьФайлы(ВременныйФайл);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьТаймер(Команда) Экспорт
	
	Если ЗначениеЗаполнено(ЗначениеТаймера) Тогда
		СохранитьВФайл = Ложь;
		ПодключитьОбработчикОжидания("ВыгрузитьДанныеТаймер", ЗначениеТаймера, Ложь);
		Сообщить("Включен запуск по таймеру через каждые " + Строка(ЗначениеТаймера) + " секунд.");
	Иначе
		ОстановитьТаймер(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОстановитьТаймер(Команда)
	ОтключитьОбработчикОжидания("ВыгрузитьДанныеТаймер");
	Сообщить("Отключен запуск по таймеру.");
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДанныеТаймер()
	ВыгрузитьДанные(Неопределено);
КонецПроцедуры
