
&НаСервере
Функция ПолучитьИЗаполнитьДанныеНаСервере()
	
	ПараметрыКоманды = Новый Структура("ДатаНачала,ДатаОкончания,ДетальныйОтчет");
	
	ПараметрыКоманды.ДетальныйОтчет = Ложь;
	
	ПараметрыКоманды.ДатаНачала = ПериодЗапрашиваемыхДанных.ДатаНачала;
	ПараметрыКоманды.ДатаОкончания = ?(ЗначениеЗаполнено(ПериодЗапрашиваемыхДанных.ДатаОкончания), ПериодЗапрашиваемыхДанных.ДатаОкончания, ТекущаяДата());	
	
	ДанныеЗагрузки = ПолучитьЗаказы(ПараметрыКоманды);
	
	Возврат ДанныеЗагрузки;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьИЗаполнитьДанные(Команда)
	Если ЗначениеЗаполнено(ПериодЗапрашиваемыхДанных.ДатаНачала) Тогда
		ДанныеЗагрузки = ПолучитьИЗаполнитьДанныеНаСервере();
		
		ПараметрыЗакрытия = Новый Структура();
		ПараметрыЗакрытия.Вставить("ДанныеЗагрузки", ДанныеЗагрузки);
		ПараметрыЗакрытия.Вставить("ПериодЗапрашиваемыхДанных", ПериодЗапрашиваемыхДанных);
		ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	Иначе	
	    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не указана дата начала.");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыДокумента(ДанныеФормы, ДанныеЗагрузки)

	Если Не ДанныеЗагрузки.Ошибка И ЗначениеЗаполнено(ДанныеЗагрузки.Список) Тогда
		
		Для каждого ЭлементВыгрузки Из ДанныеЗагрузки.Список Цикл
			
			НоваяСерияНоменклатуры = НайтиСериюНоменклатуры(ЭлементВыгрузки.ШтрихКод, ДанныеФормы.Склад, ЭлементВыгрузки.Количество);
			
			Если ЗначениеЗаполнено(НоваяСерияНоменклатуры) Тогда
				НоваяСтрока = ДанныеФормы.Товары.Добавить();
				НоваяСтрока.СерияНоменклатуры = НоваяСерияНоменклатуры;
				НоваяСтрока.Номенклатура = НоваяСерияНоменклатуры.Владелец;
				НоваяСтрока.ХарактеристикаНоменклатуры = НоваяСерияНоменклатуры.ХарактеристикаНоменклатуры;
				НоваяСтрока.Вес = НоваяСерияНоменклатуры.Вес;
				НоваяСтрока.Размер = НоваяСерияНоменклатуры.Размер;
				НоваяСтрока.Сумма = ЭлементВыгрузки.Сумма;
				НоваяСтрока.Цена = ЭлементВыгрузки.Цена;
				
				НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.НДС20;
				НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * Ценообразование.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС)/100;
				
				НоваяСтрока.Количество = ?(ЭлементВыгрузки.ТипДокумента = "Продажа", ЭлементВыгрузки.Количество, -1 * ЭлементВыгрузки.Количество);
			КонецЕсли; 
			
		КонецЦикла;
		
	    ЭтотОбъект.Модифицированность = Истина;
		
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Функция НайтиСериюНоменклатуры(СерияКод, Склад, КоличествоНаСкладе)
	
	ВозвращаемаяСерия = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры КАК СерияНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(, Склад = &Склад) КАК ПартииТоваровНаСкладахОстатки
		|ГДЕ
		|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток >= &КоличествоОстаток
		|	И ПартииТоваровНаСкладахОстатки.СерияНоменклатуры.Наименование = &Наименование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры.Код";
	
	Запрос.УстановитьПараметр("Наименование", СерияКод);
	Запрос.УстановитьПараметр("Склад", Склад); 
	Запрос.УстановитьПараметр("КоличествоОстаток", КоличествоНаСкладе); 
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВозвращаемаяСерия = ВыборкаДетальныеЗаписи.СерияНоменклатуры;		
	КонецЕсли;
	
	Возврат ВозвращаемаяСерия;

КонецФункции

&НаСервере
Функция ПолучитьЗаказы(ПараметрыКоманды)
	
	Список = Новый ТаблицаЗначений;
	ОкруглениеДоЗнака = 2;
	
	ТипСтроки = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(250, ДопустимаяДлина.Переменная));
	ТипКоличество = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Любой));
	ТипСумма = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, ОкруглениеДоЗнака, ДопустимыйЗнак.Любой));
	
	Список.Колонки.Добавить("ТипДокумента", ТипСтроки);
	Список.Колонки.Добавить("ДатаОперации", ТипСтроки);
	Список.Колонки.Добавить("ДатаЗаказа", ТипСтроки);
	Список.Колонки.Добавить("ДатаПродажи", ТипСтроки);
	Список.Колонки.Добавить("Наименование", ТипСтроки);
	Список.Колонки.Добавить("Бренд", ТипСтроки);
	Список.Колонки.Добавить("Размер", ТипСтроки);
	Список.Колонки.Добавить("Артикул", ТипСтроки);
	Список.Колонки.Добавить("ШтрихКод", ТипСтроки);
	Список.Колонки.Добавить("Количество", ТипКоличество);
	Список.Колонки.Добавить("Цена", ТипСумма);
	Список.Колонки.Добавить("Сумма", ТипСумма);
	Список.Колонки.Добавить("СуммаВознаграждения", ТипСумма);
	Список.Колонки.Добавить("СуммаНДСВознаграждения", ТипСумма);
	Список.Колонки.Добавить("УИН", ТипСтроки);
	Список.Колонки.Добавить("НомерПоставки", ТипКоличество);
	Список.Колонки.Добавить("ИДЗаказа", ТипСтроки);
	Список.Колонки.Добавить("ОбоснованиеДляОплаты", ТипСтроки);
	
	СтрокаОтветаСтруктура = Новый Структура();
	
	Для каждого Колонка Из Список.Колонки Цикл
		СтрокаОтветаСтруктура.Вставить(Колонка.Имя);
	КонецЦикла;
	
	ОбрабатываемыеТипыДокументов = Новый Структура("Продажа, Возврат", "Продажа", "Возврат");
	ОбрабатываемыеКоррекции = Новый Структура("КоррекцияПродаж, КоррекцияВозвратов", "Коррекция продаж", "Коррекция возвратов");
	
	Результат = Новый Структура("Ошибка, ОписаниеОшибки, АдресТаблицыВХранилище", Истина, "", "");
	
	Таймаут = 60;
	limit = 10000;										
	rrdid = 0;
	
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	
	СтруктураURI = СтруктураURI( АдресAPI() );
	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт,,,, Таймаут, ЗащищенноеСоединение);
	
	ЗаголовкиЗапроса = Новый Соответствие;
	ЗаголовкиЗапроса.Вставить("Authorization", ПолучитьТокенАвторизации());
	ЗаголовкиЗапроса.Вставить("Content-type", "application/json");
	
	Пока rrdid >= 0 Цикл
		
		АдресРесурса = СтрШаблон("/%1?dateFrom=%2&limit=%3&rrdid=%4&dateTo=%5", 
		    СтруктураURI.ПутьНаСервере,
			Формат(ПараметрыКоманды.ДатаНачала, "ДФ=yyyy-MM-dd"),
			Формат(limit, "ЧН=0; ЧГ="), 
			Формат(rrdid, "ЧН=0; ЧГ="), 
			Формат(ПараметрыКоманды.ДатаОкончания, "ДФ=yyyy-MM-dd"));
			
		КодироватьСтроку(АдресРесурса, СпособКодированияСтроки.КодировкаURL);
		
		rrdid = -1;
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, ЗаголовкиЗапроса);
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
		
		Если Не ТипЗнч(HTTPОтвет) = Тип("HTTPОтвет") Тогда
			Результат.Ошибка = Истина;
			Результат.ОписаниеОшибки = Нстр("ru = 'Не удалось выполнить запрос'");
			Прервать;
		КонецЕсли;
		
		СтрокаОтветаПолная = HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);

		Если HTTPОтвет.КодСостояния = 200 Тогда
			Результат.Ошибка = Ложь;
			Если СтрокаОтветаПолная = "null" Тогда
				Результат.ОписаниеОшибки = "Получены пустые данные от ВБ";
			КонецЕсли;
		Иначе
			Результат.ОписаниеОшибки = СтрШаблон(Нстр("ru = 'Код: %1'"), HTTPОтвет.КодСостояния); 
			Если Не ПустаяСтрока(СтрокаОтветаПолная) Тогда
				Результат.ОписаниеОшибки = СтрШаблон(Нстр("ru = '%1%2%3'"), 
					Результат.ОписаниеОшибки, Символы.ПС, СтрокаОтветаПолная); 
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат.ОписаниеОшибки) Тогда
			Результат.Ошибка = Истина;
			Прервать;
		КонецЕсли; 
		
		Если Не ПустаяСтрока(СтрокаОтветаПолная) Тогда
			
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(СтрокаОтветаПолная);
			СтруктураОтвета = ПрочитатьJSON(ЧтениеJSON, Истина);
			
			Если ТипЗнч(СтруктураОтвета) = Тип("Массив") Тогда
				
				Для Каждого СтрокаОтвета Из СтруктураОтвета Цикл
					
					Если Не ТипЗнч(СтрокаОтвета) = Тип("Соответствие") Тогда
						Продолжить;
					КонецЕсли;	
					
					ОчиститьЗначенияСтруктуры(СтрокаОтветаСтруктура);
					
					rrdid =  СтрокаОтвета.Получить("rrd_id");
					//СтрокаОтветаСтруктура.Количество    =  СтрокаОтвета.Получить("quantity");
					//quantity - количество операций, которые указаны в столбце «Обоснование для оплаты»
					СтрокаОтветаСтруктура.Количество    = 1;
					СтрокаОтветаСтруктура.Сумма         = Окр(СтрокаОтвета.Получить("ppvz_for_pay"), ОкруглениеДоЗнака);
					СтрокаОтветаСтруктура.ТипДокумента  = СтрокаОтвета.Получить("doc_type_name");
					СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = СтрокаОтвета.Получить("supplier_oper_name");
					
					Если (Не СтрокаОтветаСтруктура.ТипДокумента = ОбрабатываемыеТипыДокументов.Продажа И 
							Не СтрокаОтветаСтруктура.ТипДокумента = ОбрабатываемыеТипыДокументов.Возврат) 
							Или (Не СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеТипыДокументов.Продажа И 
							Не СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеТипыДокументов.Возврат И
							Не СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияПродаж И
							Не СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияВозвратов)
							Или СтрокаОтветаСтруктура.Сумма = 0 Тогда
						Продолжить;
					КонецЕсли;						
					
					СтрокаОтветаСтруктура.НомерПоставки          = СтрокаОтвета.Получить("gi_id");
					СтрокаОтветаСтруктура.ИДЗаказа               = СтрокаОтвета.Получить("srid");
					СтрокаОтветаСтруктура.ШтрихКод               = СтрокаОтвета.Получить("barcode");
					СтрокаОтветаСтруктура.УИН                    = СтрокаОтвета.Получить("kiz");
					//СтрокаОтветаСтруктура.СуммаВознаграждения    = Окр(СтрокаОтвета.Получить("ppvz_vw"), ОкруглениеДоЗнака);
					СтрокаОтветаСтруктура.СуммаВознаграждения    = 0;
					//СтрокаОтветаСтруктура.СуммаНДСВознаграждения = Окр(СтрокаОтвета.Получить("ppvz_vw_nds"), ОкруглениеДоЗнака);
					СтрокаОтветаСтруктура.СуммаНДСВознаграждения = 0;
					СтрокаОтветаСтруктура.ДатаПродажи            = СтрокаОтвета.Получить("sale_dt");
					
					Если ПараметрыКоманды.ДетальныйОтчет Тогда
						СтрокаОтветаСтруктура.Наименование  = СтрокаОтвета.Получить("subject_name");
						СтрокаОтветаСтруктура.Бренд         = СтрокаОтвета.Получить("brand_name");
						СтрокаОтветаСтруктура.Размер        = СтрокаОтвета.Получить("ts_name");
						СтрокаОтветаСтруктура.Артикул       = СтрокаОтвета.Получить("sa_name");	
						СтрокаОтветаСтруктура.ДатаОперации  = СтрокаОтвета.Получить("rr_dt");
						СтрокаОтветаСтруктура.ДатаЗаказа    = СтрокаОтвета.Получить("order_dt");
					КонецЕсли;					
					
					Если СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияПродаж ИЛИ
							СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияВозвратов Тогда
						
						Отбор = Новый Структура();
						Отбор.Вставить("ИДЗаказа", СтрокаОтветаСтруктура.ИДЗаказа);
						Отбор.Вставить("НомерПоставки", СтрокаОтветаСтруктура.НомерПоставки);
						Если ЗначениеЗаполнено(СтрокаОтветаСтруктура.УИН) Тогда
							Отбор.Вставить("УИН", СтрокаОтветаСтруктура.УИН);
						КонецЕсли;
						Если ЗначениеЗаполнено(СтрокаОтветаСтруктура.ШтрихКод) Тогда
							Отбор.Вставить("ШтрихКод", СтрокаОтветаСтруктура.ШтрихКод)
						КонецЕсли;
						
						Если СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияПродаж Тогда
							Отбор.Вставить("ОбоснованиеДляОплаты", ОбрабатываемыеТипыДокументов.Продажа);	
						ИначеЕсли СтрокаОтветаСтруктура.ОбоснованиеДляОплаты = ОбрабатываемыеКоррекции.КоррекцияВозвратов Тогда	
						    Отбор.Вставить("ОбоснованиеДляОплаты", ОбрабатываемыеТипыДокументов.Возврат);
						КонецЕсли;
						
						НайденныеСтроки = Список.НайтиСтроки(Отбор);
						
						Если НайденныеСтроки.Количество() = 1 Тогда
							ЭтоВозвратМножитель  = ?(СтрокаОтветаСтруктура.ТипДокумента = ОбрабатываемыеТипыДокументов.Возврат, -1, 1);
							НайденныеСтроки[0].Сумма = НайденныеСтроки[0].Сумма + СтрокаОтветаСтруктура.Сумма * ЭтоВозвратМножитель;		
							НайденныеСтроки[0].СуммаВознаграждения = НайденныеСтроки[0].СуммаВознаграждения + СтрокаОтветаСтруктура.СуммаВознаграждения * ЭтоВозвратМножитель; 
							НайденныеСтроки[0].СуммаНДСВознаграждения = НайденныеСтроки[0].СуммаНДСВознаграждения +	СтрокаОтветаСтруктура.СуммаНДСВознаграждения * ЭтоВозвратМножитель;
							//НайденныеСтроки[0].Цена = Окр(НайденныеСтроки[0].Сумма / НайденныеСтроки[0].Количество, ОкруглениеДоЗнака);
							НайденныеСтроки[0].Цена = 0;
						КонецЕсли;
												
						Продолжить;
					КонецЕсли; 
					
					НоваяСтрока = Список.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтветаСтруктура); 
					//НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / НоваяСтрока.Количество, ОкруглениеДоЗнака);
					НоваяСтрока.Цена = 0;
				КонецЦикла;
				
				Если СтруктураОтвета.Количество() < limit или rrdid = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СделатьПаузу(70);
			
	КонецЦикла;
	
	Если ПараметрыКоманды.ДетальныйОтчет Тогда
		//Список.Сортировать("Наименование, Бренд, Размер, Артикул, ШтрихКод, ДатаОперации, Цена");
		//Список.Сортировать("ДатаОперации, ТипДокумента Убыв, Наименование, Бренд, Размер, Артикул, ШтрихКод, Цена");
		Список.Сортировать("ДатаПродажи");
	Иначе
		//Список.Свернуть   ("ТипДокумента, Наименование, Бренд, Размер, Артикул, ШтрихКод, Цена", "Количество, Сумма, Комиссия");
		//Список.Сортировать("ТипДокумента Убыв, Наименование, Бренд, Размер, Артикул, ШтрихКод, Цена");
		Список.Сортировать("ДатаПродажи");
	КонецЕсли;
	
	Результат.АдресТаблицыВХранилище = ПоместитьВоВременноеХранилище(Список);   
	
	Возврат Результат; 
			
КонецФункции

&НаСервере
Процедура СделатьПаузу(ЗадержкаСекунд)
		КомандаWindows = "Timeout /T " + Формат(ЗадержкаСекунд, "ЧГ=0") + " /NoBreak";
		WshShell = Новый COMОбъект("WScript.Shell"); 
		WshShell.Run(КомандаWindows, 0, -1);
КонецПроцедуры  

&НаСервере
Функция ПолучитьТокенАвторизации()
	Возврат Справочники.B1_ПредопределенныеЗначения.ПолучитьПредопределенноеЗначение("WBApiТокен");
КонецФункции

&НаСервере
Функция ОчиститьЗначенияСтруктуры(ОбрабатываемаяСтруктура)
	
	Для Каждого ЭлементСтруктуры Из ОбрабатываемаяСтруктура Цикл
		ОбрабатываемаяСтруктура[ЭлементСтруктуры.Ключ] = Неопределено;
	КонецЦикла;
	
	Возврат ОбрабатываемаяСтруктура;
КонецФункции

&НаСервере
Функция СтруктураURI(Знач СтрокаURI)
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема 
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = ВРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// Строка соединения и путь на сервере.
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// Информация пользователя и имя сервера.
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@", НаправлениеПоиска.СКонца);
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = Неопределено;
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = СтрокуВЧисло( Сред(ИмяСервера, Позиция + 1) );
	КонецЕсли;
	Если Порт = Неопределено Тогда
		СтандартныеПорты = СтандартныеПортыИнтернетПротоколов();
		Если Не СтандартныеПорты.Свойство(Схема) = Неопределено Тогда
			Порт = СтандартныеПорты[Схема];
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", Порт);
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТаблицаЗначенийВМассив(ТаблицаЗначений) Экспорт
    
    Массив = Новый Массив;
	
    СтруктураСтрокой = "";
    НужнаЗапятая = Ложь;
    Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
        Если НужнаЗапятая Тогда
            СтруктураСтрокой = СтруктураСтрокой + ",";
        КонецЕсли;
        СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
        НужнаЗапятая = Истина;
	КонецЦикла;
	
    Для Каждого Строка Из ТаблицаЗначений Цикл
        НоваяСтрока = Новый Структура(СтруктураСтрокой);
        ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
        Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	
    Возврат Массив;

КонецФункции

&НаСервере
Функция СтрокуВЧисло(СтрокаСЧислом)
	
	МассивСтрок = СтрРазделить(СтрокаСЧислом, "0123456789", Ложь);
    Для Каждого ТекСтрока из МассивСтрок Цикл 
        СтрокаСЧислом = СтрЗаменить(СтрокаСЧислом, ТекСтрока, "");
    КонецЦикла;
    
    Возврат ?(ПустаяСтрока(СтрокаСЧислом), Неопределено, Число(СтрокаСЧислом));
	
КонецФункции

&НаСервере
Функция АдресAPI() Экспорт
	
	Возврат "https://statistics-api.wildberries.ru/api/v5/supplier/reportDetailByPeriod";
	
КонецФункции

&НаСервере
Функция СтандартныеПортыИнтернетПротоколов()
	
	Результат = Новый Структура();
	
	Результат.Вставить("IMAP",  143);
	Результат.Вставить("POP3",  110);
	Результат.Вставить("SMTP",  25);
	Результат.Вставить("HTTP",  80);
	Результат.Вставить("HTTPS", 443);
	Результат.Вставить("FTP",   21);
	Результат.Вставить("FTPS",  21);
	Результат.Вставить("WS",    80);
	Результат.Вставить("WSS",   443);
	
	Возврат Новый ФиксированнаяСтруктура(Результат);
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеИзВыгрузки(ЗагруженныеДанные, Партнер)

	Товары              = ПолучитьТаблицу();
	ТоварыВозвращенные  = ПолучитьТаблицу();
	
	Для Каждого Элемент Из ЗагруженныеДанные Цикл
		
		Если Элемент.Количество > 0 Тогда
			
			Товар = Товары.Добавить();
			
			Товар.НомерСтроки         = Элемент.НомерСтроки;
			Товар.Наименование        = Элемент.Наименование;
			Товар.Артикул             = Элемент.Артикул;
			Товар.ШтрихКод            = Элемент.ШтрихКод;
			Товар.Количество          = Элемент.Количество;
			Товар.СуммаПередачи       = Элемент.Сумма;
			Товар.СуммаРеализации     = Элемент.Сумма;
			Товар.СуммаВознаграждения = Элемент.Комиссия;
			
		Иначе
			
			Товар = ТоварыВозвращенные.Добавить();
			
			Товар.НомерСтроки         = Элемент.НомерСтроки;
			Товар.Наименование        = Элемент.Наименование;
			Товар.Артикул             = Элемент.Артикул;
			Товар.ШтрихКод            = Элемент.ШтрихКод;
			Товар.Количество          = Элемент.Количество * -1;
			Товар.СуммаПередачи       = Элемент.Сумма      * -1;
			Товар.СуммаРеализации     = Элемент.Сумма      * -1;
			Товар.СуммаВознаграждения = Элемент.Комиссия   * -1;
			
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат Новый Структура("Товары, ТоварыВозвращенные", 
		ТаблицаЗначенийВМассив(ДополнитьНоменклатурой(Товары            , Партнер)), 
		ТаблицаЗначенийВМассив(ДополнитьНоменклатурой(ТоварыВозвращенные, Партнер)));

КонецФункции

&НаСервере
Функция ПолучитьТаблицу()

	КвалификаторыСтроки = Новый КвалификаторыСтроки(100);
	КвалификаторыЧисла  = Новый КвалификаторыЧисла(15,3);
	
	ТипСтроки = Новый ОписаниеТипов("Строка" ,,,, КвалификаторыСтроки);
	ТипЧисла  = Новый ОписаниеТипов("Число"  ,,,  КвалификаторыЧисла );
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("НомерСтроки"            , ТипЧисла  );
	Результат.Колонки.Добавить("Наименование"           , ТипСтроки );
	Результат.Колонки.Добавить("Артикул"                , ТипСтроки );
	Результат.Колонки.Добавить("ШтрихКод"               , ТипСтроки );
	Результат.Колонки.Добавить("Количество"             , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаПередачи"          , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаНДСПередачи"       , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаРеализации"        , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаНДСРеализации"     , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаВознаграждения"    , ТипЧисла  );
	Результат.Колонки.Добавить("СуммаНДСВознаграждения" , ТипЧисла  );

    Возврат Результат;
					
КонецФункции

&НаСервере
Функция ДополнитьНоменклатурой(Таблица, Партнер)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗ", Таблица);
	Запрос.УстановитьПараметр("ВладелецНоменклатуры", Партнер);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.НомерСтроки КАК НомерСтроки,
		|	ТЗ.Наименование КАК Наименование,
		|	ТЗ.Артикул КАК Артикул,
		|	ТЗ.ШтрихКод КАК ШтрихКод,
		|	ТЗ.Количество КАК Количество,
		|	ТЗ.СуммаПередачи КАК СуммаПередачи,
		|	ТЗ.СуммаНДСПередачи КАК СуммаНДСПередачи,
		|	ТЗ.СуммаРеализации КАК СуммаРеализации,
		|	ТЗ.СуммаНДСРеализации КАК СуммаНДСРеализации,
		|	ТЗ.СуммаВознаграждения КАК СуммаВознаграждения,
		|	ТЗ.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения
		|ПОМЕСТИТЬ Таблица
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрСведенийШтрихкодыНоменклатуры.Номенклатура, ЕСТЬNULL(СправочникНоменклатураКонтрагентов.Номенклатура, ЕСТЬNULL(СправочникНоменклатура.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))))) КАК Номенклатура,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрСведенийШтрихкодыНоменклатуры.Характеристика, ЕСТЬNULL(СправочникНоменклатураКонтрагентов.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)))) КАК Характеристика,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрСведенийШтрихкодыНоменклатуры.Упаковка, ЕСТЬNULL(СправочникНоменклатураКонтрагентов.Упаковка, ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))))) КАК Упаковка,
		|	Таблица.Наименование КАК Наименование,
		|	Таблица.Артикул КАК Артикул,
		|	Таблица.ШтрихКод КАК ШтрихКод,
		|	Таблица.Количество КАК Количество,
		|	Таблица.СуммаПередачи КАК СуммаПередачи,
		|	Таблица.СуммаНДСПередачи КАК СуммаНДСПередачи,
		|	Таблица.СуммаРеализации КАК СуммаРеализации,
		|	Таблица.СуммаНДСРеализации КАК СуммаНДСРеализации,
		|	Таблица.СуммаВознаграждения КАК СуммаВознаграждения,
		|	Таблица.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения
		|ПОМЕСТИТЬ СводнаяТаблица
		|ИЗ
		|	Таблица КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК РегистрСведенийШтрихкодыНоменклатуры
		|		ПО (РегистрСведенийШтрихкодыНоменклатуры.Штрихкод = Таблица.ШтрихКод)
		|			И (НЕ РегистрСведенийШтрихкодыНоменклатуры.Номенклатура.ПометкаУдаления)
		|			И (Таблица.ШтрихКод <> """")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Артикул = Таблица.Артикул)
		|			И (НЕ СправочникНоменклатура.ПометкаУдаления)
		|			И (СправочникНоменклатура.Артикул <> """")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ПО (СправочникНоменклатураКонтрагентов.Идентификатор = (ВЫРАЗИТЬ(""WB#"" + Таблица.Артикул + ""#"" + Таблица.ШтрихКод + ""#"" + Таблица.Наименование КАК СТРОКА(300))))
		|			И (СправочникНоменклатураКонтрагентов.ВладелецНоменклатуры = &ВладелецНоменклатуры)
		|			И (НЕ СправочникНоменклатураКонтрагентов.ПометкаУдаления)
		|			И (НЕ СправочникНоменклатураКонтрагентов.Недействителен)
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.НомерСтроки,
		|	Таблица.Наименование,
		|	Таблица.Артикул,
		|	Таблица.ШтрихКод,
		|	Таблица.Количество,
		|	Таблица.СуммаПередачи,
		|	Таблица.СуммаНДСПередачи,
		|	Таблица.СуммаРеализации,
		|	Таблица.СуммаНДСРеализации,
		|	Таблица.СуммаВознаграждения,
		|	Таблица.СуммаНДСВознаграждения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СправочникНоменклатура.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА НЕ СправочникНоменклатура.Ссылка ЕСТЬ NULL
		|				И СправочникНоменклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ХарактеристикиИспользуются,
		|	СводнаяТаблица.Характеристика КАК Характеристика,
		|	СводнаяТаблица.Упаковка КАК Упаковка,
		|	СводнаяТаблица.Наименование КАК Наименование,
		|	СводнаяТаблица.Артикул КАК Артикул,
		|	СводнаяТаблица.ШтрихКод КАК ШтрихКод,
		|	СводнаяТаблица.Количество КАК Количество,
		|	СводнаяТаблица.СуммаПередачи КАК СуммаПередачи,
		|	СводнаяТаблица.СуммаНДСПередачи КАК СуммаНДСПередачи,
		|	СводнаяТаблица.СуммаРеализации КАК СуммаРеализации,
		|	СводнаяТаблица.СуммаНДСРеализации КАК СуммаНДСРеализации,
		|	СводнаяТаблица.СуммаВознаграждения КАК СуммаВознаграждения,
		|	СводнаяТаблица.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
		|	СправочникНоменклатура.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	СводнаяТаблица КАК СводнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = СводнаяТаблица.Номенклатура)
		|
		|УПОРЯДОЧИТЬ ПО
		|	СводнаяТаблица.НомерСтроки";
		
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеИзExcelНаСервере(ДвоичныеДанные, Расширение)
	
	ФайлEXCELНаСервере = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные.Записать(ФайлEXCELНаСервере);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ФайлEXCELНаСервере, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	УдалитьФайлы(ФайлEXCELНаСервере);
	
	ОбластьТаблицы = ТабличныйДокумент.Область(1, 1, ТабличныйДокумент.ВысотаТаблицы, ТабличныйДокумент.ШиринаТаблицы);
	
	Список = Новый ТаблицаЗначений;
	
	Список.Колонки.Добавить("НомерСтроки");
	Список.Колонки.Добавить("ТипДокумента");
	Список.Колонки.Добавить("ДатаОперации");
	Список.Колонки.Добавить("ДатаЗаказа");
	Список.Колонки.Добавить("ДатаПродажи");
	Список.Колонки.Добавить("Наименование");
	Список.Колонки.Добавить("Бренд");
	Список.Колонки.Добавить("Размер");
	Список.Колонки.Добавить("Артикул");
	Список.Колонки.Добавить("ШтрихКод");
	Список.Колонки.Добавить("Количество");
	Список.Колонки.Добавить("Цена");
	Список.Колонки.Добавить("Сумма");
	Список.Колонки.Добавить("Комиссия");
	
	НомерСтроки = 0;
	
	Для СтрокаТЗЧисло = 2 по ТабличныйДокумент.ВысотаТаблицы Цикл
		
		СтрокаТЗ = Строка(СтрЗаменить(СтрокаТЗЧисло, Символы.НПП, ""));
		
		КоличествоТоваров = Число(ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C14").ТекущаяОбласть.Значение);
		
		Если КоличествоТоваров Тогда
		
			НомерСтроки = НомерСтроки + 1;
			НоваяСтрокаТЗ = Список.Добавить();
			
			НоваяСтрокаТЗ.НомерСтроки = НомерСтроки;
			НоваяСтрокаТЗ.ТипДокумента = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C10").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.ДатаОперации = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C12").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.ДатаЗаказа = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C12").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.ДатаПродажи = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C13").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.Наименование = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C3").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.Бренд = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C5").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.Размер = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C8").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.Артикул = ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C6").ТекущаяОбласть.Текст;
			НоваяСтрокаТЗ.ШтрихКод = СтрЗаменить(ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C9").ТекущаяОбласть.Текст, Символы.НПП, "");
			НоваяСтрокаТЗ.Количество = ?(НоваяСтрокаТЗ.ТипДокумента = "Возврат", -1 * КоличествоТоваров, КоличествоТоваров);
			НоваяСтрокаТЗ.Цена = Число(ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C16").ТекущаяОбласть.Значение);
			НоваяСтрокаТЗ.Сумма = КоличествоТоваров * НоваяСтрокаТЗ.Цена;
			НоваяСтрокаТЗ.Комиссия = Число(ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C27").ТекущаяОбласть.Значение) + 
									 Число(ТабличныйДокумент.ПолучитьОбласть("R"+СтрокаТЗ+"C28").ТекущаяОбласть.Значение);
								 
		КонецЕсли;
		
	КонецЦикла;
	
	//Список.Сортировать("Наименование, Бренд, Размер, Артикул, ШтрихКод, ДатаОперации, Цена");
	Список.Сортировать("ТипДокумента Убыв, Наименование, Бренд, Размер, Артикул, ШтрихКод, Цена");
	
	Результат = Новый Структура("Ошибка, ОписаниеОшибки, Список", Ложь, "", ТаблицаЗначенийВМассив(Список));
	
	Возврат Результат; 

КонецФункции

&НаКлиенте
Процедура ПолучитьДанныеИзExcel(Команда)
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);                             
	ДиалогВыбора.Заголовок = "Открытие файла";
	ДиалогВыбора.МножественныйВыбор = Ложь;

	Если ДиалогВыбора.Выбрать() Тогда
				
		Файл = Новый Файл(ДиалогВыбора.ПолноеИмяФайла);
		
		Если Файл.Существует() Тогда
			ДвоичныеДанные = Новый ДвоичныеДанные(ДиалогВыбора.ПолноеИмяФайла);	
			ДанныеExcel = ПолучитьДанныеИзExcelНаСервере(ДвоичныеДанные, Файл.Расширение);
			
			ПараметрыЗакрытия = Новый Структура();
			ПараметрыЗакрытия.Вставить("ДанныеЗагрузки", ДанныеExcel);
			ЭтаФорма.Закрыть(ПараметрыЗакрытия);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
