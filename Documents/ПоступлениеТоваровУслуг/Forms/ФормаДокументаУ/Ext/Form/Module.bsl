
&НаСервере
Процедура B1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ЗаполнитьШК", "Заполнить ШК", "B1_ЗаполнитьШК");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ЗаполнитьШК", Элементы.ПодменюЗаполнить, "Заполнить ШК", "B1_ЗаполнитьШК");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ЗаполнитьУИН", "Заполнить УИН", "B1_ЗаполнитьУИН");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ЗаполнитьУИН", Элементы.ПодменюЗаполнить, "Заполнить УИН", "B1_ЗаполнитьУИН");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ЗаполнитьСерииДляМеталлов", "Заполнить серии для металлов", "B1_ЗаполнитьСерииДляМеталлов");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ЗаполнитьСерииДляМеталлов", Элементы.ПодменюЗаполнить, "Заполнить серии для металлов", "B1_ЗаполнитьСерииДляМеталлов");
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьШК(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	МассивТоваров = ПолучитьТЧТовары();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МассивТоваров", МассивТоваров);
	ПараметрыОткрытия.Вставить("ИсходныйДокумент", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("B1_ЗаполнитьШК_Завершение", ЭтаФорма);
	
	ОткрытьФорму("Обработка.ЗаполнениеШтрихкодовВТабличнойЧасти.Форма.ФормаОбработки", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//ОткрытьФорму("Обработка.ЗаполнениеШтрихкодовВТабличнойЧасти.Форма.ФормаОбработки", ПараметрыОткрытия, ЭтаФорма, , , ,  , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьУИН(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	МассивТоваров = ПолучитьТЧТовары();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МассивТоваров", МассивТоваров);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("B1_ЗаполнитьШК_Завершение", ЭтаФорма);
	
	ОткрытьФорму("Обработка.B1_ЗаполнениеУИНВСерияхНоменклатуры.Форма.Форма", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьШК_Завершение(ОсновныеПараметры, ДопПараметры) Экспорт
	
	Для каждого СтрокаТовара Из Объект.Товары Цикл
		ОповеститьОбИзменении(СтрокаТовара.СерияНоменклатуры);
	КонецЦикла;
	
	Элементы.Товары.Обновить();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТЧТовары()
	
	МассивТоваров = Новый Массив;
	
	Для каждого ТекСтрока Из Объект.Товары Цикл
		
		СтрДанных = Новый Структура;
		СтрДанных.Вставить("Номенклатура",  ТекСтрока.Номенклатура);
		СтрДанных.Вставить("ХарактеристикаНоменклатуры",  ТекСтрока.ХарактеристикаНоменклатуры);
		СтрДанных.Вставить("Размер",  ТекСтрока.Размер);
		СтрДанных.Вставить("СерияНоменклатуры",  ТекСтрока.СерияНоменклатуры);
		
		МассивТоваров.Добавить(СтрДанных);
		
	КонецЦикла;
	
	Возврат МассивТоваров;
	
КонецФункции

&НаКлиенте
Процедура B1_ЗаполнитьСерииДляМеталлов(Элемент)
	
	B1_ЗаполнитьСерииДляМеталлов_Сервер();
	
КонецПроцедуры

&НаСервере
Процедура B1_ЗаполнитьСерииДляМеталлов_Сервер()
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	мМеталл = Перечисления.ВидыНоменклатуры.Металл;
	
	Для каждого ТекСтрока Из Объект.Товары Цикл
		
		Если Не ЗначениеЗаполнено(ТекСтрока.СерияНоменклатуры) И ТекСтрока.Номенклатура.ВидНоменклатуры = мМеталл Тогда
			// Создать серию номенклатуры
			
			СерияНоменклатуры = Справочники.СерииНоменклатуры.СоздатьЭлемент();
			СерияНоменклатуры.Владелец = ТекСтрока.Номенклатура;
			СерияНоменклатуры.Записать();
			
			ТекСтрока.СерияНоменклатуры = СерияНоменклатуры.Ссылка;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

