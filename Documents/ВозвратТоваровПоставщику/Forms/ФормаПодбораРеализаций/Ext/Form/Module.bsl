
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
		|	РеализацияТоваровУслуг.Дата КАК Дата,
		|	РеализацияТоваровУслуг.Номер КАК Номер,
		|	РеализацияТоваровУслуг.СуммаДокумента КАК Сумма,
		|	РеализацияТоваровУслуг.ВалютаДокумента КАК Валюта,
		|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
		|	РеализацияТоваровУслуг.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	РеализацияТоваровУслуг.Сделка КАК Сделка,
		|	РеализацияТоваровУслуг.Организация КАК Организация,
		|	РеализацияТоваровУслуг.Подразделение КАК Подразделение,
		|	РеализацияТоваровУслуг.Комментарий КАК Комментарий
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Проведен
		|	И НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|	И НЕ РеализацияТоваровУслуг.Ссылка В (ВЫБРАТЬ 
		|											РеализацииОснования.Реализация
		|										  ИЗ 
		|											Документ.ВозвратТоваровПоставщику.РеализацииОснования КАК РеализацииОснования 
		|										  ГДЕ 
		|											РеализацииОснования.Ссылка.Проведен)
		|УПОРЯДОЧИТЬ ПО
		|	Дата";
	
	ТаблицаРеализаций.Загрузить(Запрос.Выполнить().Выгрузить());
			
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОтмеченные(Команда)
	
	МассивРеализаций = Новый Массив;
	
	Для каждого СтрокаТаблицыРеализаций Из ТаблицаРеализаций Цикл
		
		Если СтрокаТаблицыРеализаций.Отметка Тогда
		
			МассивРеализаций.Добавить(СтрокаТаблицыРеализаций.Ссылка);	
		
		КонецЕсли; 
		
	КонецЦикла; 
	
	Закрыть(МассивРеализаций);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры
