
&НаСервере
Процедура B1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	НоваяКомандаЗаполнитьПоРеализациям = Команды.Добавить("ЗаполнитьПоРеализациям");
	НоваяКомандаЗаполнитьПоРеализациям.Действие = "ЗаполнитьПоРеализациям";
	НоваяКомандаЗаполнитьПоРеализациям.Заголовок = НСтр("ru = 'Заполнить по реализациям'");
	
	КомандаЗаполнитьПоРеализациям = Элементы.Добавить("ЗаполнитьПоРеализациям", Тип("КнопкаФормы"), Элементы.Товары.КоманднаяПанель);
	КомандаЗаполнитьПоРеализациям.ИмяКоманды = "ЗаполнитьПоРеализациям";
	КомандаЗаполнитьПоРеализациям.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	
	СтраницаРеализацииОснования = Элементы.Добавить("СтраницаРеализацииОснования", Тип("ГруппаФормы"), Элементы.ОсновнаяПанель);
	СтраницаРеализацииОснования.Заголовок = НСтр("ru = 'Реализации-основания'");
	СтраницаРеализацииОснования.Вид = ВидГруппыФормы.Страница;
	
	ТаблицаРеализацииОснования = Элементы.Добавить("РеализацииОснования", Тип("ТаблицаФормы"), СтраницаРеализацииОснования);
	ТаблицаРеализацииОснования.ПутьКДанным = "Объект.РеализацииОснования";
	ТаблицаРеализацииОснования.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
	Об = РеквизитФормыВЗначение("Объект");
	ТабЧасти = Об.Метаданные().ТабличныеЧасти;	
	
	Для каждого КолонкаОбъекта Из ТабЧасти.РеализацииОснования.СтандартныеРеквизиты Цикл
		
		ИмяКолонки = "РеализацииОснования" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТаблицаРеализацииОснования);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.РеализацииОснования." + КолонкаОбъекта.Имя;	
	
	КонецЦикла;
	
	Для каждого КолонкаОбъекта Из ТабЧасти.РеализацииОснования.Реквизиты Цикл
		
		ИмяКолонки = "РеализацииОснования" + КолонкаОбъекта.Имя;
		
		ЭлементыТаблицыФормы = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТаблицаРеализацииОснования);
		ЭлементыТаблицыФормы.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементыТаблицыФормы.ПутьКДанным = "Объект.РеализацииОснования." + КолонкаОбъекта.Имя;	
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоРеализациям()

	Если Объект.Товары.Количество() > 0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнениеИзРеализацийТабличнныхЧастейЗавершение", ЭтотОбъект),
			"Очистить табличную часть перед заполнением?", РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;

	ЗаполнениеИзРеализацийТабличнныхЧастейФрагмент();

КонецПроцедуры // ()

&НаКлиенте
Процедура ЗаполнениеИзРеализацийТабличнныхЧастейЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		Объект.РеализацииОснования.Очистить();
	КонецЕсли;

	ЗаполнениеИзРеализацийТабличнныхЧастейФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеИзРеализацийТабличнныхЧастейФрагмент()

	ОткрытьФорму("Документ.ВозвратТоваровПоставщику.Форма.ФормаПодбораРеализаций", , , , , ,
		Новый ОписаниеОповещения("ЗаполнениеИзРеализацийТабличнныхЧастейФрагментЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеИзРеализацийТабличнныхЧастейФрагментЗавершение(Результат, ДополнительныеПараметры) Экспорт

	МассивДокументов = Результат;

	Если МассивДокументов = Неопределено Тогда 
		Возврат;
	КонецЕсли;

	Для каждого Документ Из МассивДокументов Цикл
		
		НовСтр = Объект.РеализацииОснования.Добавить();
		НовСтр.Реализация = Документ;
		
		ювЗагрузитьВТабличнуюЧасть(Документ);

	КонецЦикла;
	
	ОбновлениеОтображенияКлиент();
	
КонецПроцедуры

&НаСервере
Процедура B1_ПередЗаписьюНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Отказ и ПараметрыЗаписи.РежимЗаписи	= РежимЗаписиДокумента.Проведение Тогда
	
		Для каждого СтрокаРеализации Из ТекущийОбъект.РеализацииОснования Цикл
		
			Если СтрокаРеализации.Реализация.Проведен ИЛИ НЕ СтрокаРеализации.Реализация.ПометкаУдаления Тогда
			
				ОбРеализация = СтрокаРеализации.Реализация.ПолучитьОбъект();
				
				Если ОбРеализация.Проведен Тогда
				
					Попытка
						
						ОбРеализация.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				
					Исключение
						
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось распровести " + ОбРеализация, ОбРеализация.Ссылка);
						
					КонецПопытки;	
				
				КонецЕсли;
				
				Если НЕ ОбРеализация.ПометкаУдаления Тогда
				
					Попытка
				
						ОбРеализация.ПометкаУдаления = Истина;
						ОбРеализация.Записать(РежимЗаписиДокумента.Запись);
				
					Исключение
						
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось установить пометку удаления " + ОбРеализация, ОбРеализация.Ссылка);
						
					КонецПопытки;	
				
				КонецЕсли; 				 
			
			КонецЕсли; 	
		
		КонецЦикла; 	
	
	КонецЕсли; 
	
КонецПроцедуры
