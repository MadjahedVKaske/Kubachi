
&НаКлиенте
Процедура B1_ЗаполнитьПоДокументуВместо(Команда)
	
	Если Объект.Товары.Количество() > 0 Тогда
		ПоказатьВопросОчисткиТабЧасти("ЗаполнитьПоДокументуОчиститьЗавершение");
	Иначе	
		ОткрытьФормуВыбораДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		ЭтотОбъект.Модифицированность = Истина;
		ОткрытьФормуВыбораДокумента();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораДокумента()

	ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.B1_ФормаВыбораДокумента", , ЭтотОбъект, , ,
		, Новый ОписаниеОповещения("ЗаполнениеПоДокументуВыборЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПоДокументуВыборЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора <> Неопределено И ЗначениеЗаполнено(РезультатВыбора.ДокументИсточникЗаполнения) Тогда
		ЗаполнениеПоДокументуСервер(РезультатВыбора.ДокументИсточникЗаполнения);
		ОбновлениеОтображенияКлиент();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнениеПоДокументуСервер(ДокументИсточникЗаполнения)
	
	ТабЗначенийДокументаИсточника = ДокументИсточникЗаполнения.Товары.Выгрузить();
	
	Если ТабЗначенийДокументаИсточника.Количество() Тогда
		Объект.Товары.Загрузить(ТабЗначенийДокументаИсточника);
		ЭтотОбъект.Модифицированность = Истина;
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура B1_ЗаполнитьПоСкладуВместоНаСервере(СтруктураПараметров)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПартииТоваровНаСкладахОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровНаСкладахОстатки.Размер КАК Размер,
		|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ПартииТоваровНаСкладахОстатки.ВесОстаток КАК ВесОстаток
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(, Склад  = &Склад) КАК ПартииТоваровНаСкладахОстатки
		|ГДЕ
		|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток > 0";
	
	Если ЗаполнитьСериямиСУИН Тогда
		Запрос.Текст = Запрос.Текст + " И ПартииТоваровНаСкладахОстатки.СерияНоменклатуры.УИН <> """"";	
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрока = Объект.Товары.Добавить();
		НоваяСтрока.СерияНоменклатуры = ВыборкаДетальныеЗаписи.СерияНоменклатуры;
		НоваяСтрока.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		НоваяСтрока.ХарактеристикаНоменклатуры = ВыборкаДетальныеЗаписи.ХарактеристикаНоменклатуры;
		НоваяСтрока.Вес = ВыборкаДетальныеЗаписи.ВесОстаток;
		НоваяСтрока.Размер = ВыборкаДетальныеЗаписи.Размер;
		НоваяСтрока.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток;
		НоваяСтрока.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		НоваяСтрока.Цена = 1;
		ОбработкаТабличныхЧастейСерверУ.РассчитатьСуммуТабЧасти(НоваяСтрока, ПредопределенноеЗначение(
		"Документ.РеализацияТоваровУслуг.ПустаяСсылка"), СтруктураПараметров);
		
	КонецЦикла;
	
	ЭтотОбъект.Модифицированность = Истина;
		
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоСкладуВместо(Команда)
	Если ЗначениеЗаполнено(Объект.Склад) Тогда;
		Если Объект.Товары.Количество() > 0 Тогда
			ПоказатьВопросОчисткиТабЧасти("ЗаполнитьПоСкладуОчиститьЗавершение", ЗаполнитьСериямиСУИН);
		Иначе	
			B1_ЗаполнитьПоСкладуВместоНаСервере(ПолучитьСтруктураПараметров());
			ОбновлениеОтображенияКлиент();
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не выбран склад.");
	КонецЕсли;
	
	ЗаполнитьСериямиСУИН = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСкладуОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		ЭтотОбъект.Модифицированность = Истина;
		ЗаполнитьСериямиСУИН = ДополнительныеПараметры;
		B1_ЗаполнитьПоСкладуВместоНаСервере(ПолучитьСтруктураПараметров());
		ОбновлениеОтображенияКлиент();
	КонецЕсли;
	ЗаполнитьСериямиСУИН = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросОчисткиТабЧасти(ИмяФункцииОбработчика, ДопПараметры = Неопределено)
	ПоказатьВопрос(Новый ОписаниеОповещения(ИмяФункцииОбработчика, ЭтотОбъект, ДопПараметры),
		"Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоСкладуСУИНВместо(Команда)
	ЗаполнитьСериямиСУИН = Истина;
	B1_ЗаполнитьПоСкладуВместо(Неопределено);
КонецПроцедуры


&НаСервере
Процедура B1_ЗаполнитьПоФайлуВБВместоНаСервере(ИмяФайла)
				
	Файл = Новый Файл(ИмяФайла);
	
	Если Файл.Существует() Тогда

		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
		
		ОбластьПоиска = ТабличныйДокумент.Область(9, 2, 12, 10);
		СтолбцыПоиска = Новый Структура();
		СтолбцыПоиска.Вставить("Штрихкод", Новый Массив(2));
		СтолбцыПоиска.Штрихкод[0] = "баркод";
		
		СтолбцыПоиска.Вставить("АртикулДляВБ", Новый Массив(2));
		СтолбцыПоиска.АртикулДляВБ[0] = "артикул продавца";
		
		СтолбцыПоиска.Вставить("Размер", Новый Массив(2));
		СтолбцыПоиска.Размер[0] = "сорт, размер";
		
		СтолбцыПоиска.Вставить("УИН", Новый Массив(2));
		СтолбцыПоиска.УИН[0] = "КИЗ";
		
		СтолбцыПоиска.Вставить("Количество", Новый Массив(2));
		СтолбцыПоиска.Количество[0] = "кол-во";

		НомерСтрокиНачалаДанных = 0;
		
		Для каждого СтолбецПоиска Из СтолбцыПоиска Цикл
			
			ОбластьСтолбцаПоиска = ТабличныйДокумент.НайтиТекст(СтолбецПоиска.Значение[0], , ОбластьПоиска, , Истина, , Истина);
			Если ЗначениеЗаполнено(ОбластьСтолбцаПоиска) Тогда
				СтолбецПоиска.Значение[1] = ОбластьСтолбцаПоиска.Право;
				Если Не ЗначениеЗаполнено(НомерСтрокиНачалаДанных) Тогда
					НомерСтрокиНачалаДанных = ОбластьСтолбцаПоиска.Низ + 3;	
				КонецЕсли; 
			Иначе	
			    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найден столбец " + СтолбецПоиска.Значение[0]);
				Возврат;
			КонецЕсли;

		КонецЦикла;
		
		Если СтрНайти(НРег(ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы, 1).Текст), "итого") Тогда
			КоличествоСтрокТаблицы = ТабличныйДокумент.ВысотаТаблицы - 1;
		Иначе
		    КоличествоСтрокТаблицы = ТабличныйДокумент.ВысотаТаблицы;
		КонецЕсли;

		Для НомерСтроки = НомерСтрокиНачалаДанных по КоличествоСтрокТаблицы Цикл
						
			СтруктураИзСтроки = Новый Структура("Штрихкод, АртикулДляВБ, Размер, УИН, Количество");
			СтруктураИзСтроки.Штрихкод = ТабличныйДокумент.Область(НомерСтроки, СтолбцыПоиска.Штрихкод[1]).Текст;
			СтруктураИзСтроки.АртикулДляВБ = ТабличныйДокумент.Область(НомерСтроки, СтолбцыПоиска.АртикулДляВБ[1]).Текст;
			СтруктураИзСтроки.Размер = ТабличныйДокумент.Область(НомерСтроки, СтолбцыПоиска.Размер[1]).Текст;
			СтруктураИзСтроки.УИН = ТабличныйДокумент.Область(НомерСтроки, СтолбцыПоиска.УИН[1]).Текст;
			СтруктураИзСтроки.Количество = ТабличныйДокумент.Область(НомерСтроки, СтолбцыПоиска.Количество[1]).Текст;
			
			СерияНоменклатуры = B1_НайтиСериюПоУИН(СтруктураИзСтроки.УИН);
			
			Если Не СерияНоменклатуры.Пустая() Тогда
				МассивОшибок = ПроверитьСтрокуФайла(СерияНоменклатуры, СтруктураИзСтроки);
				Если МассивОшибок.Количество() Тогда
					Для каждого СтрокаОшибки Из МассивОшибок Цикл
						ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю(СтрокаОшибки + " в строке " + Строка(НомерСтроки - НомерСтрокиНачалаДанных + 1));
					КонецЦикла; 
					Продолжить;
				Иначе
				    НоваяСтрокаТЗ = Объект.Товары.Добавить();
					НоваяСтрокаТЗ.СерияНоменклатуры = СерияНоменклатуры;
					НоваяСтрокаТЗ.Номенклатура = СерияНоменклатуры.Владелец;
					НоваяСтрокаТЗ.Размер = СерияНоменклатуры.Размер;
					НоваяСтрокаТЗ.ХарактеристикаНоменклатуры = СерияНоменклатуры.ХарактеристикаНоменклатуры;
					НоваяСтрокаТЗ.Количество = СтруктураИзСтроки.Количество;
					НоваяСтрокаТЗ.Вес = СерияНоменклатуры.Вес;
					НоваяСтрокаТЗ.СтавкаНДС = Перечисления.СтавкиНДС.НДС20;
				КонецЕсли;
			Иначе
				ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдена серия номенклатуры по УИН " + СтруктураИзСтроки.УИН + " в строке " + Строка(НомерСтроки - НомерСтрокиНачалаДанных + 1));
				Продолжить;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция B1_НайтиСериюПоУИН(УИН)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СерииНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.УИН = &УИН
		|
		|УПОРЯДОЧИТЬ ПО
		|	СерииНоменклатуры.Код УБЫВ";
	
	Запрос.УстановитьПараметр("УИН", УИН);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Ссылка;	
	Иначе	
		Возврат Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;

КонецФункции

&НаСервере
Функция ПроверитьСтрокуФайла(СерияНоменклатуры, СтруктураИзСтроки)
		
	МассивОшибок = Новый Массив();
	
	Если СерияНоменклатуры.Наименование <> СтруктураИзСтроки.Штрихкод Тогда
		МассивОшибок.Добавить("Штрихкод в файле не соответствует имени серии для УИН " + СтруктураИзСтроки.УИН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СерияНоменклатуры.Размер.Наименование) Тогда
		РазмерФормат = СтрЗаменить(СерияНоменклатуры.Размер.Наименование, ".", ",");
		РазмерФормат = СтрЗаменить(РазмерФормат, ",0", "");
	Иначе
	    РазмерФормат = "0";
	КонецЕсли;
		
	Если РазмерФормат <> СтруктураИзСтроки.Размер Тогда
		МассивОшибок.Добавить("Размер в файле не соответствует размеру серии для УИН " + СтруктураИзСтроки.УИН);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СерияНоменклатуры.ХарактеристикаНоменклатуры) Тогда
		Если Строка(СерияНоменклатуры.Владелец.Артикул + СерияНоменклатуры.ХарактеристикаНоменклатуры.B1_ДопАртикулДляВБ) <> СтруктураИзСтроки.АртикулДляВБ Тогда
			МассивОшибок.Добавить("Артикул в файле не соответствует артикулу характеристики для УИН " + СтруктураИзСтроки.УИН);
		КонецЕсли;
	ИначеЕсли Строка(СерияНоменклатуры.Владелец.Артикул + СерияНоменклатуры.Владелец.B1_ДопАртикулДляВБПоУмолчанию) <> СтруктураИзСтроки.АртикулДляВБ Тогда
		МассивОшибок.Добавить("Артикул в файле не соответствует артикулу номенклатуры для УИН " + СтруктураИзСтроки.УИН);
	КонецЕсли;
	
	Возврат МассивОшибок;

КонецФункции

&НаКлиенте
Процедура B1_ЗаполнитьПоФайлуВБВместо(Команда)
	Если Объект.Товары.Количество() > 0 Тогда
		ПоказатьВопросОчисткиТабЧасти("ЗаполнитьПоФайлуВБОчиститьЗавершение");
	Иначе	
		B1_ЗаполнитьПоФайлуВБВыборФайла();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоФайлуВБОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		ЭтотОбъект.Модифицированность = Истина;
		B1_ЗаполнитьПоФайлуВБВыборФайла();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоФайлуВБВыборФайла() Экспорт
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);                             
	ДиалогВыбора.Заголовок = "Открытие файла";
	ДиалогВыбора.МножественныйВыбор = Ложь;

	Если ДиалогВыбора.Выбрать() Тогда
		B1_ЗаполнитьПоФайлуВБВместоНаСервере(ДиалогВыбора.ПолноеИмяФайла);			
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура B1_СохранитьВExcelФайлПослеНаСервере(ИмяФайла, ТипФайла)
	B1_СохранениеФайлов.B1_СохранитьБаркодыВExcelФайл(ИмяФайла, ТипФайла, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура B1_СохранитьВExcelФайлПосле(Команда)
	Если ПроверитьМодифицированностьИЗаполненность() Тогда
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогСохраненияФайла.ПолноеИмяФайла = "Баркоды Wildberries " + Объект.Номер + ".xlsx";                
		ДиалогСохраненияФайла.Фильтр = "Файл Excel(*.xlsx)|*.xlsx"; 
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
		
		Если ДиалогСохраненияФайла.Выбрать() Тогда
			B1_СохранитьВExcelФайлПослеНаСервере(ДиалогСохраненияФайла.ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПроверитьМодифицированностьИЗаполненность()
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Необходимо записать документ.");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Объект.Товары.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Таблица цен пустая.");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат	Истина;
КонецФункции
