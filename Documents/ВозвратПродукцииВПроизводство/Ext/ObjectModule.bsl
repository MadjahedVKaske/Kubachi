
&После("ОбработкаЗаполнения")
Процедура B1_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("Основание") И 
		ТипЗнч(ДанныеЗаполнения.Основание) = Тип("ДокументСсылка.ПреобразованиеВЛом") Тогда
		
		ЗаполнитьДокументНаОснованииПреобразованияВЛом(ДанныеЗаполнения.Основание);	
	
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииПреобразованияВЛом(Знач ДанныеЗаполнения)
	
	B1_ДокументОснованиеПреобразованиеВЛом = ДанныеЗаполнения;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", B1_ДокументОснованиеПреобразованиеВЛом);
	Запрос.УстановитьПараметр("Организация", B1_ДокументОснованиеПреобразованиеВЛом.Организация);
	Запрос.УстановитьПараметр("Склад", B1_ДокументОснованиеПреобразованиеВЛом.Склад);
	ТекСтавкаНДС = ОбработкаТабличнойЧастиТоварыПовтИсп.ЗаполнитьСтавкуНДСПоУмолчанию(B1_ДокументОснованиеПреобразованиеВЛом.Организация,
		НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("СтавкаНДС", ТекСтавкаНДС);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Организация КАК Организация,
	|	Документ.Склад КАК Склад,
	|	НЕ Документ.Проведен КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ПреобразованиеВЛом КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Товары.Размер КАК Размер,
	|	Товары.СерияНоменклатуры КАК СерияНоменклатуры,
	|	Товары.Количество КАК Количество,
	|	Товары.Вес КАК Вес,
	|	Товары.Стоимость КАК Сумма,
	|	Товары.СтоимостьБезНДС КАК СтоимостьБезНДС
	|ИЗ
	|	Документ.ПреобразованиеВЛом.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &Ссылка";

	Результат = Запрос.ВыполнитьПакет();

	Выборка = Результат[0].Выбрать();
	Выборка.Следующий();

	ОбщегоНазначенияЮССервер.ПроверитьВозможностьВводаНаОсновании(Выборка.Ссылка, , Ложь, );

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);

	ТаблицаТоваров = Результат[1].Выгрузить();

	Для Каждого СтрокаТоваров Из ТаблицаТоваров Цикл

		НоваяСтрока = Товары.Добавить();

		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
		
		СуммаНДС = ?(ЗначениеЗаполнено(СтрокаТоваров.СтоимостьБезНДС), СтрокаТоваров.Сумма - СтрокаТоваров.СтоимостьБезНДС, 0);
		
		НоваяСтрока.СтавкаНДС = ОбработкаТабличнойЧастиТоварыКлиентСервер.ПолучитьСтавкуНДСПоСумме(СтрокаТоваров.Сумма,
			СуммаНДС, Истина);			
		
		// Заполнение реквизитов табличной части.
		ОбработкаТабличныхЧастей.ПриИзмененииСуммыТабЧасти(НоваяСтрока, ЭтотОбъект, глЗначениеПеременной(
			"глТекущийПользователь"));
		
		// Расчет реквизитов табличной части.
		//ОбработкаТабличныхЧастей.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, ЭтотОбъект);

	КонецЦикла;


КонецПроцедуры