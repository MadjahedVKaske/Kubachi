
&НаКлиенте
Процедура B1_РассчитатьЦеныПосле(Команда)
	Если Объект.Товары.Количество() > 0 Тогда
		ПоказатьВопросДаНет("Цены будут изменены. Продолжить?", "РассчетЦенОчисткаЗавершение");
	Иначе	
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Таблица цен пустая.");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РассчетЦенОчисткаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуПараметровЦен();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросДаНет(ТекстВопроса, ИмяФункцииОбработчика, ДопПараметры = Неопределено)
	ПоказатьВопрос(Новый ОписаниеОповещения(ИмяФункцииОбработчика, ЭтотОбъект, ДопПараметры),
		ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуПараметровЦен()
	ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.B1_ФормаРассчетаЦен", , ЭтотОбъект, , ,
		, Новый ОписаниеОповещения("РассчетЦенВыборЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура РассчетЦенВыборЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора <> Неопределено 
		И ЗначениеЗаполнено(РезультатВыбора.ТипЦены) 
		И ЗначениеЗаполнено(РезультатВыбора.ЦенаЗаГрамм)
		И ЗначениеЗаполнено(РезультатВыбора.Валюта) Тогда

		РассчетЦенСервер(РезультатВыбора);
		ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура РассчетЦенСервер(ПараметрыЗаполнения)
	
	Если Не Объект.Товары.Количество() Тогда
		Возврат;
	КонецЕсли;
		
	Отбор = Новый Структура();
	Отбор.Вставить("ТипЦен",ПараметрыЗаполнения.ТипЦены);
	НайденныеСтроки = ЭтотОбъект.ВыбранныеТипыЦен.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() Тогда
		Если Не НайденныеСтроки[0].Выбрана Тогда
			НайденныеСтроки[0].Выбрана = Истина;
			ОбновитьТаблицуЦенНаСервере();
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найден тип цен.");
		Возврат;
	КонецЕсли;
	
	ПрефиксТипаЦен = "ТипЦен" + ПараметрыЗаполнения.ТипЦены.Код;
	
	Для каждого СтрокаЦен Из ЭтотОбъект.ТаблицаЦен Цикл
		
		УчитыватьРазмерПриЗамене = ПараметрыЗаполнения.УчитыватьРазмерПриЗамене;
		
		Если НЕ ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры) И ЗначениеЗаполнено(СтрокаЦен.Номенклатура) И ПараметрыЗаполнения.ЗаполнитьСерииНоменклатуры Тогда
			
			Если НЕ ЗначениеЗаполнено(СтрокаЦен.ХарактеристикаНоменклатуры) Тогда
				ХарактеристикиНоменклатуры = НайтиХарактеристикиПоНоменклатуре(СтрокаЦен.Номенклатура);
				
				Если ХарактеристикиНоменклатуры.Количество() > 1 Тогда
					ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("У номенклатуры " + СтрокаЦен.Номенклатура + " найдено более одной характеристики. Серия не установлена.");
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтрокаЦен.СерияНоменклатуры = НайтиСериюПоНоменклатуре(СтрокаЦен);
			
			Если Не ЗначениеЗаполнено(СтрокаЦен.Размер) Тогда
				УчитыватьРазмерПриЗамене = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры) Тогда
			Если ЗначениеЗаполнено(ПараметрыЗаполнения.ТипПоискаСерий) Тогда
				СтрокаЦен.СерияНоменклатуры = НайтиСериюСВесомПоТипу(СтрокаЦен.СерияНоменклатуры, ПараметрыЗаполнения.ТипПоискаСерий, УчитыватьРазмерПриЗамене);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаЦен.ХарактеристикаНоменклатуры) И ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры.ХарактеристикаНоменклатуры) Тогда
				СтрокаЦен.ХарактеристикаНоменклатуры = СтрокаЦен.СерияНоменклатуры.ХарактеристикаНоменклатуры;	
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры.Вес)Тогда
				СтрокаЦен[ПрефиксТипаЦен] = Окр(СтрокаЦен.СерияНоменклатуры.Вес * ПараметрыЗаполнения.ЦенаЗаГрамм, -1) + ПараметрыЗаполнения.СуммаПрибавления;
				СтрокаЦен[ПрефиксТипаЦен + "Валюта"] = ПараметрыЗаполнения.Валюта;
				Если ПараметрыЗаполнения.СуммаПрибавления = 0 Тогда
					ЗнакПрибавленияДопРасходов = "";
				ИначеЕсли ПараметрыЗаполнения.СуммаПрибавления > 0 Тогда
					ЗнакПрибавленияДопРасходов = " + ";
				Иначе
				    ЗнакПрибавленияДопРасходов = " - ";
				КонецЕсли;
				СтрокаЦен[ПрефиксТипаЦен + "ФормулаРасчета"] = Строка(Формат(СтрокаЦен.СерияНоменклатуры.Вес, "ЧДЦ=3; ЧГ=")) + " * " + Строка(Формат(ПараметрыЗаполнения.ЦенаЗаГрамм, "ЧДЦ=2; ЧГ="))
					+ ЗнакПрибавленияДопРасходов + Строка(Формат(ПараметрыЗаполнения.СуммаПрибавления, "ЧДЦ=2; ЧГ="));
				Если ПараметрыЗаполнения.ЗаполнитьПроцентСкидки Тогда
				    СтрокаЦен[ПрефиксТипаЦен + "ПроцентСкидкиНаценки"] = ПараметрыЗаполнения.ПроцентСкидкиНаценки;
				КонецЕсли;
				
				Если ПараметрыЗаполнения.ЗаполнитьРазмерыИзСерий И ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры)
					И ЗначениеЗаполнено(СтрокаЦен.СерияНоменклатуры.Размер) Тогда
				    СтрокаЦен.Размер = СтрокаЦен.СерияНоменклатуры.Размер;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;
	
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция НайтиСериюСВесомПоТипу(СерияНоменклатуры, ТипПоискаСерий, УчитыватьРазмер = Ложь)
	ВозвращаемаяСерия = СерияНоменклатуры;
	
	Если ЗначениеЗаполнено(ТипПоискаСерий) Тогда
		
		Если ТипПоискаСерий = 1 ИЛИ ТипПоискаСерий = 3 Тогда
		
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	СерииНоменклатуры.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
				|ГДЕ
				|	СерииНоменклатуры.Вес > 0
				|	И СерииНоменклатуры.Владелец = &Владелец
				|	И СерииНоменклатуры.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
				|	И ВЫБОР
				|			КОГДА &Размер = ЗНАЧЕНИЕ(Справочник.Размер.ПустаяСсылка)
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ СерииНоменклатуры.Размер = &Размер
				|		КОНЕЦ
				|	И НЕ СерииНоменклатуры.ПометкаУдаления
				|
				|УПОРЯДОЧИТЬ ПО
				|	СерииНоменклатуры.Вес";
			
			Если  ТипПоискаСерий = 3 Тогда
				Запрос.Текст = Запрос.Текст + " УБЫВ";
			КонецЕсли; 
		
		КонецЕсли;
		
		Если ТипПоискаСерий = 2 Тогда
	
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	СерииНоменклатуры.Вес КАК Вес,
				|	СерииНоменклатуры.Ссылка КАК Ссылка
				|ПОМЕСТИТЬ ТаблицаСерий
				|ИЗ
				|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
				|ГДЕ
				|	СерииНоменклатуры.Вес > 0
				|	И СерииНоменклатуры.Владелец = &Владелец
				|	И СерииНоменклатуры.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
				|	И ВЫБОР
				|			КОГДА &Размер = ЗНАЧЕНИЕ(Справочник.Размер.ПустаяСсылка)
				|				ТОГДА ИСТИНА
				|			ИНАЧЕ СерииНоменклатуры.Размер = &Размер
				|		КОНЕЦ
				|	И НЕ СерииНоменклатуры.ПометкаУдаления
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	СРЕДНЕЕ(ТаблицаСерий.Вес) КАК СреднийВес
				|ПОМЕСТИТЬ ТаблицаСреднегоВеса
				|ИЗ
				|	ТаблицаСерий КАК ТаблицаСерий
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	МАКСИМУМ(ТаблицаСерий.Вес) КАК Вес
				|ПОМЕСТИТЬ ТаблицаБлизкихВесов
				|ИЗ
				|	ТаблицаСерий КАК ТаблицаСерий,
				|	ТаблицаСреднегоВеса КАК ТаблицаСреднегоВеса
				|ГДЕ
				|	ТаблицаСерий.Вес <= ТаблицаСреднегоВеса.СреднийВес
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	МИНИМУМ(ТаблицаСерий.Вес)
				|ИЗ
				|	ТаблицаСерий КАК ТаблицаСерий,
				|	ТаблицаСреднегоВеса КАК ТаблицаСреднегоВеса
				|ГДЕ
				|	ТаблицаСерий.Вес >= ТаблицаСреднегоВеса.СреднийВес
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ ПЕРВЫЕ 1
				|	ТаблицаСерий.Ссылка КАК Ссылка,
				|	ВЫБОР
				|		КОГДА ТаблицаСерий.Вес - ТаблицаСреднегоВеса.СреднийВес < 0
				|			ТОГДА (ТаблицаСерий.Вес - ТаблицаСреднегоВеса.СреднийВес) * -1
				|		ИНАЧЕ ТаблицаСерий.Вес - ТаблицаСреднегоВеса.СреднийВес
				|	КОНЕЦ КАК Разница
				|ИЗ
				|	ТаблицаСреднегоВеса КАК ТаблицаСреднегоВеса,
				|	ТаблицаБлизкихВесов КАК ТаблицаБлизкихВесов
				|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСерий КАК ТаблицаСерий
				|		ПО ТаблицаБлизкихВесов.Вес = ТаблицаСерий.Вес
				|
				|УПОРЯДОЧИТЬ ПО
				|	Разница";				
		КонецЕсли;	
		
		Запрос.УстановитьПараметр("Владелец", СерияНоменклатуры.Владелец);
		Если УчитыватьРазмер Тогда
			Запрос.УстановитьПараметр("Размер", СерияНоменклатуры.Размер);
		Иначе
		    Запрос.УстановитьПараметр("Размер", Справочники.Размер.ПустаяСсылка());
		КонецЕсли;
		Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СерияНоменклатуры.ХарактеристикаНоменклатуры);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() И ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Ссылка) Тогда
			ВозвращаемаяСерия = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;

	КонецЕсли;
	
	Возврат ВозвращаемаяСерия;
КонецФункции

&НаСервере
Функция НайтиСериюПоНоменклатуре(СтрокаТЧ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СерииНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
		|ГДЕ
		|	СерииНоменклатуры.Владелец = &Владелец
		|	И ВЫБОР
		|			КОГДА &ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ СерииНоменклатуры.ХарактеристикаНоменклатуры = &ХарактеристикаНоменклатуры
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &Размер = ЗНАЧЕНИЕ(Справочник.Размер.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ СерииНоменклатуры.Размер = &Размер
		|		КОНЕЦ
		|	И НЕ СерииНоменклатуры.ПометкаУдаления
		|	И СерииНоменклатуры.Вес > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	СерииНоменклатуры.Код УБЫВ";
	
	Запрос.УстановитьПараметр("Владелец", СтрокаТЧ.Номенклатура);
	Запрос.УстановитьПараметр("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
	Запрос.УстановитьПараметр("Размер", СтрокаТЧ.Размер);

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВозвращаемаяСерия = ВыборкаДетальныеЗаписи.Ссылка;
	Иначе	
	    ВозвращаемаяСерия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;	
	
	Возврат ВозвращаемаяСерия;
КонецФункции

&НаСервере
Функция НайтиХарактеристикиПоНоменклатуре(Номенклатура)
	
	МассивХарактеристик = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХарактеристикиНоменклатуры.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|ГДЕ
		|	ХарактеристикиНоменклатуры.Владелец = &Владелец
		|	И НЕ ХарактеристикиНоменклатуры.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Владелец", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивХарактеристик.Добавить(ВыборкаДетальныеЗаписи.Ссылка);	
	КонецЦикла;
	
	Возврат МассивХарактеристик;
КонецФункции

&НаСервере
Процедура СохранитьТаблицуЗначенийExcel(ВыбранныйТипЦен, ИмяФайла, ТипФайла)
	
	НоваяТаблицаЗначений = Новый ТаблицаЗначений;
	
	ОписаниеСтрока = Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(10));
	ОписаниеЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 0));
	
	НоваяТаблицаЗначений.Колонки.Добавить("Wildberries_id", ОписаниеЧисло);
	НоваяТаблицаЗначений.Колонки.Добавить("Размер", ОписаниеСтрока);
	НоваяТаблицаЗначений.Колонки.Добавить("Цена", ОписаниеЧисло);
	
	Для каждого СтрокаТЗ Из ЭтотОбъект.ТаблицаЦен Цикл
		НоваяСтрока = НоваяТаблицаЗначений.Добавить();
		НоваяСтрока.Wildberries_id = СтрокаТЗ.Номенклатура.B1_АртикулВБ;
	    НоваяСтрока.Размер = СтрокаТЗ.Размер;
		НоваяСтрока.Цена = СтрокаТЗ["ТипЦен" + ВыбранныйТипЦен.Код];
	КонецЦикла;
	
	ПострПечать = Новый ПостроительОтчета;
	ПострПечать.ИсточникДанных = Новый ОписаниеИсточникаДанных(НоваяТаблицаЗначений);
	ТабДок = Новый ТабличныйДокумент;                  
	ПострПечать.Вывести(ТабДок);
	ТабДок.Записать(ИмяФайла, ТипФайла);
	
КонецПроцедуры 

&НаКлиенте
Процедура B1_СохранитьВExcelПосле(Команда)
	Если Не ПроверитьМодифицированностьИЗаполненность() Тогда
		Возврат;
	КонецЕсли; 
	
	ОткрытьФормуВыбораТипаЦены("СохранитьВExcelЗавершение");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораТипаЦены(ПроцедураОбработкиВыбора)
	ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.B1_ФормаВыбораТипаЦены", , ЭтотОбъект, , ,
		, Новый ОписаниеОповещения(ПроцедураОбработкиВыбора, ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВExcelЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора <> Неопределено И ЗначениеЗаполнено(РезультатВыбора.ТипЦены) Тогда
		
		Отбор = Новый Структура();
		Отбор.Вставить("ТипЦен",РезультатВыбора.ТипЦены);
		НайденныеСтроки = ЭтотОбъект.ВыбранныеТипыЦен.НайтиСтроки(Отбор);
		
		Если Не НайденныеСтроки.Количество() ИЛИ Не НайденныеСтроки[0].Выбрана Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найден тип цен.");
			Возврат;
		КонецЕсли;
		
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогСохраненияФайла.ПолноеИмяФайла = "Цены Wildberries " + Формат(ТекущаяДата(), "ДЛФ=D") + ".xlsx";                
		ДиалогСохраненияФайла.Фильтр = "Файл Excel(*.xlsx)|*.xlsx"; 
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
		
		Если ДиалогСохраненияФайла.Выбрать() Тогда
			СохранитьТаблицуЗначенийExcel(РезультатВыбора.ТипЦены, ДиалогСохраненияФайла.ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		КонецЕсли;

	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура B1_ВыгрузитьЦеныВWildberriesПосле(Команда)
	Если Не ПроверитьМодифицированностьИЗаполненность() Тогда
		Возврат;
	КонецЕсли; 
	
	ПоказатьВопросДаНет("Цены будут выгружены в Wildberries. Продолжить?", "ВыгрузитьЦеныВВБВопрос");
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЦеныВВБВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуВыбораТипаЦены("ВыгрузитьЦеныВВБЗавершение");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЦеныВВБЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора <> Неопределено И ЗначениеЗаполнено(РезультатВыбора.ТипЦены) Тогда
		
		Отбор = Новый Структура();
		Отбор.Вставить("ТипЦен",РезультатВыбора.ТипЦены);
		НайденныеСтроки = ЭтотОбъект.ВыбранныеТипыЦен.НайтиСтроки(Отбор);
		
		Если Не НайденныеСтроки.Количество() ИЛИ Не НайденныеСтроки[0].Выбрана Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найден тип цен.");
			Возврат;
		КонецЕсли;
		
		B1_ВыгрузитьЦеныВWildberriesПослеНаСервере(РезультатВыбора.ТипЦены);

	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура B1_ВыгрузитьЦеныВWildberriesПослеНаСервере(ВыбранныйТипЦен)

	КоличествоЭлементовВПакете = 10;
	
	ПакетыДанныхМассив = Новый Массив;
	МассивДанныхJSON = Новый Массив;
	НоменклатураБезАртикулаВБ = Новый Массив;
	НоменклатураБезЦен = Новый Массив;
	НоменклатураБезРанееЗагруженныхЦен = Новый Массив;
	СчетчикДляОтправления = 0;
	
	МассивИДРанееЗагруженныхЦен = ПолучитьВБИДЗагруженныхЦен();
	Если Не МассивИДРанееЗагруженныхЦен.Количество() Тогда
	    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Ошибка, не удалось получить ранее загруженные цены.");
		Возврат;
	КонецЕсли;

	Для каждого СтрокаЦен Из ЭтотОбъект.ТаблицаЦен Цикл
		
		Если СтрокаЦен.Номенклатура.B1_АртикулВБ > 0 Тогда
			
			Если МассивИДРанееЗагруженныхЦен.Найти(СтрокаЦен.Номенклатура.B1_АртикулВБ) <> Неопределено Тогда
				ЦенаИзделия = Окр(СтрокаЦен["ТипЦен" + ВыбранныйТипЦен.Код]);
				
				Если ЦенаИзделия > 0 Тогда
					ДанныеЗаписи = Новый Структура;
					ДанныеЗаписи.Вставить("nmId", СтрокаЦен.Номенклатура.B1_АртикулВБ);
					ДанныеЗаписи.Вставить("price", ЦенаИзделия);
					МассивДанныхJSON.Добавить(ДанныеЗаписи);
					СчетчикДляОтправления = СчетчикДляОтправления + 1;
				Иначе	
					НоменклатураБезЦен.Добавить(СтрокаЦен.Номенклатура);
				КонецЕсли;
			Иначе
				НоменклатураБезРанееЗагруженныхЦен.Добавить(СтрокаЦен.Номенклатура);
			КонецЕсли;
		Иначе
		    НоменклатураБезАртикулаВБ.Добавить(СтрокаЦен.Номенклатура);		
		КонецЕсли;
		
		Если СчетчикДляОтправления >= КоличествоЭлементовВПакете Тогда
			ПакетыДанныхМассив.Добавить(МассивДанныхJSON);
			МассивДанныхJSON = Новый Массив;
			СчетчикДляОтправления = 0;
		КонецЕсли; 
				
	КонецЦикла;
	
	Если МассивДанныхJSON.Количество() Тогда
		ПакетыДанныхМассив.Добавить(МассивДанныхJSON);
	КонецЕсли;
	
	НомерПакета = 0;
	СчетчикОтправленных = 0;	
	Для каждого ПакетДанных Из ПакетыДанныхМассив Цикл
		НомерПакета = НомерПакета + 1;
		СчетчикОтправленных =  СчетчикОтправленных + ОтправитьПакет(ПакетДанных, НомерПакета);
	КонецЦикла;
	
	Если СчетчикОтправленных Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Отправлено цен: " + СчетчикОтправленных);
	КонецЕсли;
		
	Если НоменклатураБезЦен.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не отправлено элементов номенклатуры без цены: " + НоменклатураБезЦен.Количество());
	КонецЕсли;
	
	Если НоменклатураБезАртикулаВБ.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не отправлено элементов номенклатуры без артикулов WB: " + НоменклатураБезАртикулаВБ.Количество());
	КонецЕсли;
	
	Если НоменклатураБезРанееЗагруженныхЦен.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не отправлено элементов номенклатуры без ранее выгруженных цен: " + НоменклатураБезРанееЗагруженныхЦен.Количество());
		Для каждого СтрокаНоменклатуры  Из НоменклатураБезРанееЗагруженныхЦен Цикл
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Ошибка, ранее не устанавливались цены для изделия " + СтрокаНоменклатуры);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ОтправитьПакет(ПакетМассив, НомерПакета)

	СчетчикОтправленных = 0;
	
	ЗаписьJSON = Новый ЗаписьJSON;

    ПараметрыJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Windows);  
    ЗаписьJSON.УстановитьСтроку(ПараметрыJSON);
	
    ЗаписатьJSON(ЗаписьJSON, ПакетМассив);
	СтрокаJS = ЗаписьJSON.Закрыть();
	
	HTTPСоединение = ПолучитьHTTPСоединение();
	ЗапросPOST = ПолучитьHTTPЗапрос("/public/api/v1/prices");
	
	СтрокаJS = СтрЗаменить(СтрокаJS, Символы.ВК + Символы.ПС, "");

	ЗапросPOST.УстановитьТелоИзСтроки(СтрокаJS, "windows-1251", ИспользованиеByteOrderMark.НеИспользовать);
	Попытка
	    ОтветСервера = HTTPСоединение.ОтправитьДляОбработки(ЗапросPOST);
		Если ОтветСервера.КодСостояния = 200 Тогда
			СчетчикОтправленных = ПакетМассив.Количество();
		Иначе
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Ошибка, цены не отправлены в пакете " + НомерПакета + ", код ответа сервера: " + ОтветСервера.КодСостояния + Символы.ПС
			+ "Строки ответа сервера: " + СтрСоединить(ПолучитьМассивОшибокИзJSON(ОтветСервера.ПолучитьТелоКакСтроку()), Символы.ПС));
		КонецЕсли;
	Исключение
	    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю(ОписаниеОшибки()); 
	КонецПопытки;
	
	Возврат СчетчикОтправленных;

КонецФункции

&НаСервере
Функция ПолучитьМассивОшибокИзJSON(СтрокаJSON)
	МассивОшибок = Новый Массив;
	
	Если ЗначениеЗаполнено(СтрокаJSON) Тогда
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
		ОбъектИзJSON = ПрочитатьJSON(ЧтениеJSON);
		Для каждого СтруктураОшибок Из ОбъектИзJSON.errors Цикл
			МассивОшибок.Добавить(СтруктураОшибок);
		КонецЦикла;
		ЧтениеJSON.Закрыть();	
	КонецЕсли;
	
	Возврат МассивОшибок;
КонецФункции

&НаКлиенте
Функция ПроверитьМодифицированностьИЗаполненность()
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Необходимо записать документ.");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Объект.Товары.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Таблица цен пустая.");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат	Истина;
КонецФункции

&НаСервере
Функция ПолучитьHTTPСоединение()
	ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	
	СтруктураСоединения = Новый Структура;
	
	СтруктураСоединения.Вставить("Хост", "suppliers-api.wildberries.ru");
	СтруктураСоединения.Вставить("Порт", 443);
	СтруктураСоединения.Вставить("Таймаут", 60);
	
	Возврат Новый HTTPСоединение(СтруктураСоединения.Хост, СтруктураСоединения.Порт,,,, СтруктураСоединения.Таймаут, ЗащищенноеСоединение);
КонецФункции

&НаСервере
Функция ПолучитьHTTPЗапрос(АдресРесурса)
	ЗаголовкиЗапроса = Новый Соответствие;
	ЗаголовкиЗапроса.Вставить("Authorization", ПолучитьТокенАвторизации());
	ЗаголовкиЗапроса.Вставить("Content-type", "application/json");
	
	Возврат Новый HTTPЗапрос(АдресРесурса, ЗаголовкиЗапроса);
КонецФункции
	
&НаСервере
Функция ПолучитьТокенАвторизации()	
	Возврат "eyJhbGciOiJFUzI1NiIsImtpZCI6IjIwMjQwNTA2djEiLCJ0eXAiOiJKV1QifQ.eyJlbnQiOjEsImV4cCI6MTczMjc1NDc0MSwiaWQiOiJkMmFlNzc1ZS1lZTU1LTQyYzUtYTgzNS03YWQzYWUzNTVkYzYiLCJpaWQiOjE2ODYwODIsIm9pZCI6NTQxNjUsInMiOjE2LCJzaWQiOiJiZGUyOGJiMS1hM2ExLTVjOWQtYTZlNS0zOGQ3ZGUxMTY4ZjQiLCJ0IjpmYWxzZSwidWlkIjoxNjg2MDgyfQ.tpZkhJVLz72BVuV3q2Sv4VjuFlySD0vBzTMkjXBFYpqSInWfe7MpO2wsU2odWdTUnH_vcqOR8UeZS7nuCa8meA";
КонецФункции

&НаСервере
Функция ПолучитьВБИДЗагруженныхЦен()	
	МассивВБИД = Новый Массив;
	
	HTTPСоединение = ПолучитьHTTPСоединение();
	ЗапросGET = ПолучитьHTTPЗапрос("/public/api/v1/info");
	
	Попытка
	    ОтветСервера = HTTPСоединение.Получить(ЗапросGET);
		Если ОтветСервера.КодСостояния = 200 Тогда
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(ОтветСервера.ПолучитьТелоКакСтроку());
			ОбъектИзJSON = ПрочитатьJSON(ЧтениеJSON);
			Для каждого СтруктураЦены Из ОбъектИзJSON Цикл
				МассивВБИД.Добавить(СтруктураЦены.nmId);
			КонецЦикла;
			ЧтениеJSON.Закрыть();	
		Иначе
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Ошибка, не удалось получить цены с сайта WB" + ОтветСервера.КодСостояния);
		КонецЕсли;
	Исключение
	    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю(ОписаниеОшибки()); 
	КонецПопытки;
	
	Возврат МассивВБИД;
КонецФункции

//&НаСервере
//&ИзменениеИКонтроль("ДобавитьГруппуКолонокВТаблицуЦен")
//Процедура B1_ДобавитьГруппуКолонокВТаблицуЦен(ТипЦен)

//	ИмяТипаЦен = СокрЛП(ТипЦен.Код);
//	ДобавляемыеРеквизиты = Новый Массив;
//	//добавим в реквизит формы
//	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен, Новый ОписаниеТипов("Число", , ,
//	Новый КвалификаторыЧисла(15, 2)), "ТаблицаЦен", "" + СокрЛП(ТипЦен)));
//	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "Валюта",
//	Новый ОписаниеТипов("СправочникСсылка.Валюты"), "ТаблицаЦен", "" + ТипЦен.ВалютаЦены));
//	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки",
//	Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(10, 2)), "ТаблицаЦен", "% скидки/наценки"));
//	#Вставка
//	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "ФормулаРасчета",
//	Новый ОписаниеТипов("Строка"), "ТаблицаЦен", "формула расчета"));
//	#КонецВставки

//	ИзменитьРеквизиты(ДобавляемыеРеквизиты, );

//	//добавить на форме
//	#Удаление
//	ГруппаКолонокВ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ", Тип("ГруппаФормы"),
//		Элементы.ТаблицаЦен);
//	ГруппаКолонокВ.ОтображатьВШапке = Ложь;
//	ГруппаКолонокВ.Группировка = ГруппировкаКолонок.Вертикальная;

//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен, Тип("ПолеФормы"), ГруппаКолонокВ);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен;
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;

//	ГруппаКолонокГ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаГ", Тип("ГруппаФормы"), ГруппаКолонокВ);
//	ГруппаКолонокГ.ОтображатьВШапке = Ложь;
//	ГруппаКолонокГ.Группировка = ГруппировкаКолонок.Горизонтальная;

//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "Валюта", Тип("ПолеФормы"),
//		ГруппаКолонокГ);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта";
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииВалюта");

//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки", Тип(
//		"ПолеФормы"), ГруппаКолонокГ);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки";
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииПроцента");
//	#КонецУдаления
//	#Вставка
//	ГруппаКолонокВ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ", Тип("ГруппаФормы"), Элементы.ТаблицаЦен);
//	ГруппаКолонокВ.ОтображатьВШапке = Ложь;
//	ГруппаКолонокВ.Вид = ВидГруппыФормы.ГруппаКолонок;
//	ГруппаКолонокВ.Группировка = ГруппировкаКолонок.Горизонтальная;
//	
//	ГруппаКолонокА = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаА", Тип("ГруппаФормы"), ГруппаКолонокВ);
//	ГруппаКолонокА.ОтображатьВШапке = Ложь;
//	ГруппаКолонокА.Вид = ВидГруппыФормы.ГруппаКолонок;
//	ГруппаКолонокА.Группировка = ГруппировкаКолонок.Вертикальная;
//	
//	ГруппаКолонокБ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаБ", Тип("ГруппаФормы"), ГруппаКолонокВ);
//	ГруппаКолонокБ.ОтображатьВШапке = Ложь;
//	ГруппаКолонокБ.Вид = ВидГруппыФормы.ГруппаКолонок;
//	ГруппаКолонокБ.Группировка = ГруппировкаКолонок.Вертикальная;
//	
//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ФормулаРасчета", Тип("ПолеФормы"), ГруппаКолонокА);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ФормулаРасчета";
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.ТолькоПросмотр = Истина;
//	
//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "Валюта", Тип("ПолеФормы"), ГруппаКолонокА);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта";
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииВалюта");

//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен, Тип("ПолеФормы"), ГруппаКолонокБ);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен;
//	ЭлементФормы.Ширина			= 20;
//	ЭлементФормы.АвтоМаксимальнаяШирина = Ложь;
//	ЭлементФормы.МаксимальнаяШирина = 20;
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииЦена");
//	
//	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки", Тип("ПолеФормы"), ГруппаКолонокБ);
//	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки";
//	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
//	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииПроцента");
//	#КонецВставки

//КонецПроцедуры

//&НаСервере
//&ИзменениеИКонтроль("УдалитьГруппуКолонокВТаблицеЦен")
//Процедура B1_УдалитьГруппуКолонокВТаблицеЦен(ТипЦен)

//	ИмяТипаЦен = СокрЛП(ТипЦен.Код);
//	УдаляемыеРеквизиты = Новый Массив;

//	//удалим из реквизита формы, если он есть
//	ЕстьЭлемент = Элементы.Найти("ТаблицаЦенТипЦен" + ИмяТипаЦен);
//	Если Не ЕстьЭлемент = Неопределено Тогда

//		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен);
//		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта");
//		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки");
//		#Вставка
//		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ФормулаРасчета");
//		#КонецВставки

//		Элементы.Удалить(Элементы["ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ"]);

//	КонецЕсли;

//	ИзменитьРеквизиты( , УдаляемыеРеквизиты);

//КонецПроцедуры

//&НаСервере
//&ИзменениеИКонтроль("ЗаполнитьТаблицуЦенПоТоварам")
//Процедура B1_ЗаполнитьТаблицуЦенПоТоварам()

//	Если Объект.ТипыЦен.Количество() = 0 Тогда

//		Возврат;

//	КонецЕсли;

//	Для Каждого Стр Из Объект.ТипыЦен Цикл
//		ДобавитьГруппуКолонокВТаблицуЦен(Стр.ТипЦен);
//	КонецЦикла;

//	ТаблицаТоваров = Объект.Товары.Выгрузить();
//	ТаблицаТоваров.Сортировать(
//	"ИндексСтрокиТаблицыЦен, СегментНоменклатуры, Номенклатура, ХарактеристикаНоменклатуры, Размер, СерияНоменклатуры, ТипЦен");
//	ТекущийИндексСтрокиТаблицыЦен = Неопределено;

//	НоваяСтрока = Неопределено;

//	Для Каждого СтрокаТаблицыТоваров Из ТаблицаТоваров Цикл

//		Если ТекущийИндексСтрокиТаблицыЦен <> СтрокаТаблицыТоваров.ИндексСтрокиТаблицыЦен
//			Или НоваяСтрока.СегментНоменклатуры <> СтрокаТаблицыТоваров.СегментНоменклатуры
//			Или НоваяСтрока.Номенклатура <> СтрокаТаблицыТоваров.Номенклатура
//			Или НоваяСтрока.ХарактеристикаНоменклатуры <> СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры
//			Или НоваяСтрока.Размер <> СтрокаТаблицыТоваров.Размер Или НоваяСтрока.СерияНоменклатуры
//			<> СтрокаТаблицыТоваров.СерияНоменклатуры Тогда

//			НоваяСтрока = ТаблицаЦен.Добавить();
//			НоваяСтрока.СегментНоменклатуры		   = СтрокаТаблицыТоваров.СегментНоменклатуры;
//			НоваяСтрока.Номенклатура               = СтрокаТаблицыТоваров.Номенклатура;
//			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры;
//			НоваяСтрока.Размер                     = СтрокаТаблицыТоваров.Размер;
//			НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыТоваров.СерияНоменклатуры;
//			#Вставка
//			НоваяСтрока.АртикулВБПолный            = ПолучитьПолныйАртикулВБ(СтрокаТаблицыТоваров.Номенклатура, СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры);
//			#КонецВставки

//			ТекущийИндексСтрокиТаблицыЦен = СтрокаТаблицыТоваров.ИндексСтрокиТаблицыЦен;

//		КонецЕсли;

//		ИмяТипаЦен = СтрокаТаблицыТоваров.ТипЦен.Код;

//		НоваяСтрока["ТипЦен" + ИмяТипаЦен] = СтрокаТаблицыТоваров.Цена;
//		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "Валюта"] = СтрокаТаблицыТоваров.Валюта;
//		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки"] = СтрокаТаблицыТоваров.ПроцентСкидкиНаценки;
//		#Вставка
//		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "ФормулаРасчета"] = СтрокаТаблицыТоваров.ФормулаРасчета;
//		#КонецВставки

//	КонецЦикла;
//КонецПроцедуры

//&НаСервере
//&ИзменениеИКонтроль("ПоместитьТаблицуЦенВТовары")
//Процедура B1_ПоместитьТаблицуЦенВТовары(ТекущийОбъект)
//	//перезаполним типы цен
//	ТекущийОбъект.ТипыЦен.Очистить();
//	Для Каждого Стр Из ВыбранныеТипыЦен Цикл
//		Если Стр.Выбрана Тогда
//			НоваяСтрока = ТекущийОбъект.ТипыЦен.Добавить();
//			НоваяСтрока.ТипЦен = Стр.ТипЦен;
//		КонецЕсли;
//	КонецЦикла;	
//	//перезаполним товары
//	ТекущийОбъект.Товары.Очистить();
//	Для Каждого СтрокаТаблицыЦен Из ТаблицаЦен Цикл
//		Для Каждого СтрокаТаблицыТиповЦен Из ТекущийОбъект.ТипыЦен Цикл
//			НоваяСтрока = ТекущийОбъект.Товары.Добавить();
//			НоваяСтрока.СегментНоменклатуры		   = СтрокаТаблицыЦен.СегментНоменклатуры;
//			НоваяСтрока.Номенклатура               = СтрокаТаблицыЦен.Номенклатура;
//			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыЦен.ХарактеристикаНоменклатуры;
//			НоваяСтрока.Размер                     = СтрокаТаблицыЦен.Размер;
//			НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыЦен.СерияНоменклатуры;

//			НоваяСтрока.ТипЦен = СтрокаТаблицыТиповЦен.ТипЦен;
//			НоваяСтрока.Цена   = СтрокаТаблицыЦен["ТипЦен" + СтрокаТаблицыТиповЦен.ТипЦен.Код];
//			НоваяСтрока.ИндексСтрокиТаблицыЦен = СтрокаТаблицыЦен.ПолучитьИдентификатор();
//			НоваяСтрока.ПроцентСкидкиНаценки = СтрокаТаблицыЦен["ТипЦен" + СтрокаТаблицыТиповЦен.ТипЦен.Код
//			+ "ПроцентСкидкиНаценки"];
//			НоваяСтрока.Валюта = СтрокаТаблицыЦен["ТипЦен" + СтрокаТаблицыТиповЦен.ТипЦен.Код + "Валюта"];
//			#Вставка
//			НоваяСтрока.ФормулаРасчета = СтрокаТаблицыЦен["ТипЦен" + СтрокаТаблицыТиповЦен.ТипЦен.Код + "ФормулаРасчета"];
//			#КонецВставки
//		КонецЦикла;
//	КонецЦикла;

//КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ДобавитьГруппуКолонокВТаблицуЦен")
Процедура B1_ДобавитьГруппуКолонокВТаблицуЦен(ТипЦен)

	ИмяТипаЦен = ТипЦен.УникальныйИдентификатор;
	ДобавляемыеРеквизиты = Новый Массив;
	//добавим в реквизит формы
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен, Новый ОписаниеТипов("Число", , ,
	Новый КвалификаторыЧисла(15, 2)), "ТаблицаЦен", "" + СокрЛП(ТипЦен.ТипЦен)));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "Валюта",
	Новый ОписаниеТипов("СправочникСсылка.Валюты"), "ТаблицаЦен", "" + ТипЦен.ВалютаЦены));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки",
	Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(10, 2)), "ТаблицаЦен", "% скидки/наценки"));
	#Вставка
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипЦен" + ИмяТипаЦен + "ФормулаРасчета",
	Новый ОписаниеТипов("Строка"), "ТаблицаЦен", "формула расчета"));
	#КонецВставки

	ИзменитьРеквизиты(ДобавляемыеРеквизиты, );

	//добавить на форме
	#Удаление
	ГруппаКолонокВ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ", Тип("ГруппаФормы"),
	Элементы.ТаблицаЦен);
	ГруппаКолонокВ.ОтображатьВШапке = Ложь;
	ГруппаКолонокВ.Группировка = ГруппировкаКолонок.Вертикальная;
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен, Тип("ПолеФормы"), ГруппаКолонокВ);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен;
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	
	ГруппаКолонокГ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаГ", Тип("ГруппаФормы"), ГруппаКолонокВ);
	ГруппаКолонокГ.ОтображатьВШапке = Ложь;
	ГруппаКолонокГ.Группировка = ГруппировкаКолонок.Горизонтальная;
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "Валюта", Тип("ПолеФормы"),
	ГруппаКолонокГ);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта";
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииВалюта");
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки", Тип(
	"ПолеФормы"), ГруппаКолонокГ);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки";
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииПроцента");
	#КонецУдаления
	#Вставка
	ГруппаКолонокВ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ", Тип("ГруппаФормы"), Элементы.ТаблицаЦен);
	ГруппаКолонокВ.ОтображатьВШапке = Ложь;
	ГруппаКолонокВ.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокВ.Группировка = ГруппировкаКолонок.Горизонтальная;
	
	ГруппаКолонокА = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаА", Тип("ГруппаФормы"), ГруппаКолонокВ);
	ГруппаКолонокА.ОтображатьВШапке = Ложь;
	ГруппаКолонокА.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокА.Группировка = ГруппировкаКолонок.Вертикальная;
	
	ГруппаКолонокБ = Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаБ", Тип("ГруппаФормы"), ГруппаКолонокВ);
	ГруппаКолонокБ.ОтображатьВШапке = Ложь;
	ГруппаКолонокБ.Вид = ВидГруппыФормы.ГруппаКолонок;
	ГруппаКолонокБ.Группировка = ГруппировкаКолонок.Вертикальная;
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ФормулаРасчета", Тип("ПолеФормы"), ГруппаКолонокА);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ФормулаРасчета";
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.ТолькоПросмотр = Истина;
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "Валюта", Тип("ПолеФормы"), ГруппаКолонокА);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта";
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииВалюта");
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен, Тип("ПолеФормы"), ГруппаКолонокБ);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен;
	ЭлементФормы.Ширина			= 20;
	ЭлементФормы.АвтоМаксимальнаяШирина = Ложь;
	ЭлементФормы.МаксимальнаяШирина = 20;
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииЦена");
	
	ЭлементФормы 				= Элементы.Добавить("ТаблицаЦенТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки", Тип("ПолеФормы"), ГруппаКолонокБ);
	ЭлементФормы.ПутьКДанным 	= "ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки";
	ЭлементФормы.Вид			= ВидПоляФормы.ПолеВвода;
	ЭлементФормы.УстановитьДействие("ПриИзменении", "ТаблицаЦенТипЦенПриИзмененииПроцента");
	
	#КонецВставки

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("УдалитьГруппуКолонокВТаблицеЦен")
Процедура B1_УдалитьГруппуКолонокВТаблицеЦен(ТипЦен)

	ИмяТипаЦен = СокрЛП(ТипЦен.УникальныйИдентификатор);
	УдаляемыеРеквизиты = Новый Массив;

	//удалим из реквизита формы, если он есть
	ЕстьЭлемент = Элементы.Найти("ТаблицаЦенТипЦен" + ИмяТипаЦен);
	Если Не ЕстьЭлемент = Неопределено Тогда

		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен);
		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "Валюта");
		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки");
		#Вставка
		УдаляемыеРеквизиты.Добавить("ТаблицаЦен.ТипЦен" + ИмяТипаЦен + "ФормулаРасчета");
		#КонецВставки

		Элементы.Удалить(Элементы["ТаблицаЦенТипЦен" + ИмяТипаЦен + "ГруппаВ"]);

	КонецЕсли;

	ИзменитьРеквизиты( , УдаляемыеРеквизиты);

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ЗаполнитьТаблицуЦенПоТоварам")
Процедура B1_ЗаполнитьТаблицуЦенПоТоварам()

	Если Объект.ТипыЦен.Количество() = 0 Тогда

		Возврат;

	КонецЕсли;

	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТипыЦен.ТипЦен КАК ТипЦен
	|ПОМЕСТИТЬ ТипыЦен
	|ИЗ
	|	&ТипыЦен КАК ТипыЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыЦенНоменклатуры.Ссылка КАК ТипЦен,
	|	ТипыЦенНоменклатуры.ПроцентСкидкиНаценки КАК ПроцентСкидкиНаценки,
	|	ТипыЦенНоменклатуры.ВалютаЦены КАК ВалютаЦены,
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ТипыЦенНоменклатуры.Ссылка) КАК УникальныйИдентификаторСт
	|ИЗ
	|	ТипыЦен КАК ТипыЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыЦенНоменклатуры КАК ТипыЦенНоменклатуры
	|		ПО ТипыЦен.ТипЦен = ТипыЦенНоменклатуры.Ссылка");
	Запрос.УстановитьПараметр("ТипыЦен", Объект.ТипыЦен.Выгрузить());

	Выборка = Запрос.Выполнить().Выгрузить();
	Выборка.Колонки.Добавить("УникальныйИдентификатор", Новый ОписаниеТипов("Строка"));
	Для Каждого Стр Из Выборка Цикл
		Стр.УникальныйИдентификатор = СтрЗаменить(Стр.УникальныйИдентификаторСт, "-", "");
		ДобавитьГруппуКолонокВТаблицуЦен(Стр);
	КонецЦикла;

	ТаблицаТоваров = Объект.Товары.Выгрузить();
	ТаблицаТоваров.Сортировать(
	"ИндексСтрокиТаблицыЦен, СегментНоменклатуры, Номенклатура, ХарактеристикаНоменклатуры, Размер, СерияНоменклатуры, ТипЦен");
	ТекущийИндексСтрокиТаблицыЦен = Неопределено;

	НоваяСтрока = Неопределено;

	Для Каждого СтрокаТаблицыТоваров Из ТаблицаТоваров Цикл

		Если ТекущийИндексСтрокиТаблицыЦен <> СтрокаТаблицыТоваров.ИндексСтрокиТаблицыЦен
			Или НоваяСтрока.СегментНоменклатуры <> СтрокаТаблицыТоваров.СегментНоменклатуры
			Или НоваяСтрока.Номенклатура <> СтрокаТаблицыТоваров.Номенклатура
			Или НоваяСтрока.ХарактеристикаНоменклатуры <> СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры
			Или НоваяСтрока.Размер <> СтрокаТаблицыТоваров.Размер Или НоваяСтрока.СерияНоменклатуры
			<> СтрокаТаблицыТоваров.СерияНоменклатуры Тогда

			НоваяСтрока = ТаблицаЦен.Добавить();
			НоваяСтрока.СегментНоменклатуры		   = СтрокаТаблицыТоваров.СегментНоменклатуры;
			НоваяСтрока.Номенклатура               = СтрокаТаблицыТоваров.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры;
			НоваяСтрока.Размер                     = СтрокаТаблицыТоваров.Размер;
			НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыТоваров.СерияНоменклатуры;
			#Вставка
			НоваяСтрока.АртикулВБПолный            = ПолучитьПолныйАртикулВБ(СтрокаТаблицыТоваров.Номенклатура, СтрокаТаблицыТоваров.ХарактеристикаНоменклатуры);
			#КонецВставки

			ТекущийИндексСтрокиТаблицыЦен = СтрокаТаблицыТоваров.ИндексСтрокиТаблицыЦен;

		КонецЕсли;

		ИмяТипаЦен = СтрЗаменить(СтрокаТаблицыТоваров.ТипЦен.УникальныйИдентификатор(), "-", "");

		НоваяСтрока["ТипЦен" + ИмяТипаЦен] = СтрокаТаблицыТоваров.Цена;
		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "Валюта"] = СтрокаТаблицыТоваров.Валюта;
		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "ПроцентСкидкиНаценки"] = СтрокаТаблицыТоваров.ПроцентСкидкиНаценки;
		#Вставка
		НоваяСтрока["ТипЦен" + ИмяТипаЦен + "ФормулаРасчета"] = СтрокаТаблицыТоваров.ФормулаРасчета;
		#КонецВставки

	КонецЦикла;
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ПоместитьТаблицуЦенВТовары")
Процедура B1_ПоместитьТаблицуЦенВТовары(ТекущийОбъект)
	//перезаполним типы цен
	ТекущийОбъект.ТипыЦен.Очистить();
	Для Каждого Стр Из ВыбранныеТипыЦен Цикл
		Если Стр.Выбрана Тогда
			НоваяСтрока = ТекущийОбъект.ТипыЦен.Добавить();
			НоваяСтрока.ТипЦен = Стр.ТипЦен;
		КонецЕсли;
	КонецЦикла;	
	//перезаполним товары
	ТекущийОбъект.Товары.Очистить();
	Для Каждого СтрокаТаблицыЦен Из ТаблицаЦен Цикл
		Для Каждого СтрокаТаблицыТиповЦен Из ТекущийОбъект.ТипыЦен Цикл
			НоваяСтрока = ТекущийОбъект.Товары.Добавить();
			НоваяСтрока.СегментНоменклатуры		   = СтрокаТаблицыЦен.СегментНоменклатуры;
			НоваяСтрока.Номенклатура               = СтрокаТаблицыЦен.Номенклатура;
			НоваяСтрока.ХарактеристикаНоменклатуры = СтрокаТаблицыЦен.ХарактеристикаНоменклатуры;
			НоваяСтрока.Размер                     = СтрокаТаблицыЦен.Размер;
			НоваяСтрока.СерияНоменклатуры          = СтрокаТаблицыЦен.СерияНоменклатуры;

			НоваяСтрока.ТипЦен = СтрокаТаблицыТиповЦен.ТипЦен;                                                   
			УникальныйИдентификаторТипаЦен = СтрЗаменить(СтрокаТаблицыТиповЦен.ТипЦен.УникальныйИдентификатор(), "-", "");
			НоваяСтрока.Цена   = СтрокаТаблицыЦен["ТипЦен" + УникальныйИдентификаторТипаЦен];
			НоваяСтрока.ИндексСтрокиТаблицыЦен = СтрокаТаблицыЦен.ПолучитьИдентификатор();
			НоваяСтрока.ПроцентСкидкиНаценки = СтрокаТаблицыЦен["ТипЦен" + УникальныйИдентификаторТипаЦен
			+ "ПроцентСкидкиНаценки"];
			НоваяСтрока.Валюта = СтрокаТаблицыЦен["ТипЦен" + УникальныйИдентификаторТипаЦен + "Валюта"];
			#Вставка                              
			ИмяТипаЦен = СтрЗаменить(СтрокаТаблицыТиповЦен.ТипЦен.УникальныйИдентификатор(), "-", "");
			НоваяСтрока.ФормулаРасчета = СтрокаТаблицыЦен["ТипЦен" + ИмяТипаЦен + "ФормулаРасчета"];
			#КонецВставки
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЦенТипЦенПриИзмененииЦена(Элемент)
	ИмяТипаЦен = СтрЗаменить(Элемент.Имя, "ТаблицаЦен", "");
	Элементы.ТаблицаЦен.ТекущиеДанные[ИмяТипаЦен + "ФормулаРасчета"] = "";
КонецПроцедуры

&НаКлиенте
Процедура B1_ДеревоЦенСерияНоменклатурыПриИзмененииПосле(Элемент)
	Для каждого ЭлементСтроки Из ЭтотОбъект.ВыбранныеТипыЦен Цикл
		Если ЭлементСтроки.Выбрана Тогда
			Элементы.ТаблицаЦен.ТекущиеДанные["ТипЦен" + ЭлементСтроки.КодЦены + "ФормулаРасчета"] = "";
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура B1_ЗаполнитьПоСкладуПослеНаСервере(ПараметрыЗаполнения)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииТоваровНаСкладахОстаткиИОбороты.Номенклатура КАК Номенклатура
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.ОстаткиИОбороты(&ПериодНачало, &ПериодКонец, , , Склад = &Склад) КАК ПартииТоваровНаСкладахОстаткиИОбороты
		|ГДЕ
		|	ПартииТоваровНаСкладахОстаткиИОбороты.КоличествоПриход > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровНаСкладахОстаткиИОбороты.Номенклатура.Артикул";
	
	Запрос.УстановитьПараметр("ПериодНачало", НачалоДня(ПараметрыЗаполнения.ВыбранныйПериод.ДатаНачала));
	Запрос.УстановитьПараметр("ПериодКонец", КонецДня(ПараметрыЗаполнения.ВыбранныйПериод.ДатаОкончания));
	Запрос.УстановитьПараметр("Склад", ПараметрыЗаполнения.Склад);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		СтрокаТабличнойЧасти = ТаблицаЦен.Добавить();
		СтрокаТабличнойЧасти.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
	КонецЦикла;
	
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоСкладуПосле(Команда)
	ПоказатьВопросДаНет("Перед заполнением табличная часть будет очищена. Заполнить?", "B1_ЗаполнитьПоСкладуПослеЗавершение");
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоСкладуПослеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЭтотОбъект.ТаблицаЦен.Очистить();
		ЭтотОбъект.Модифицированность = Истина;
		ОткрытьФормуЗаполненияПоСкладу();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура B1_ДобавитьПоСкладуПосле(Команда)
	ОткрытьФормуЗаполненияПоСкладу();
КонецПроцедуры
 
&НаКлиенте
Процедура ОткрытьФормуЗаполненияПоСкладу()
	ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.B1_ФормаЗаполненияПоСкладу", , ЭтотОбъект, , ,
		, Новый ОписаниеОповещения("ЗаполнениеПоСкладуВыборЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры
	
&НаКлиенте
Процедура ЗаполнениеПоСкладуВыборЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора <> Неопределено И ЗначениеЗаполнено(РезультатВыбора.Склад) 
		И ЗначениеЗаполнено(РезультатВыбора.ВыбранныйПериод.ДатаОкончания)
		И РезультатВыбора.ВыбранныйПериод.ДатаОкончания >= РезультатВыбора.ВыбранныйПериод.ДатаНачала Тогда

		B1_ЗаполнитьПоСкладуПослеНаСервере(РезультатВыбора);
		ОбновитьОтображениеДанных();
		ТаблицаЦенКоличествоСтрок = ТаблицаЦен.Количество();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьПолныйАртикулВБ(Номенклатура, Характеристика)
	Возврат ?(ЗначениеЗаполнено(Характеристика.B1_ДопАртикулДляВБ), Номенклатура.Артикул + Характеристика.B1_ДопАртикулДляВБ, "");	
КонецФункции

&НаКлиенте
Процедура B1_ДеревоЦенХарактеристикаНоменклатурыПриИзмененииПосле(Элемент)
	Элементы.ТаблицаЦен.ТекущиеДанные.АртикулВБПолный = ПолучитьПолныйАртикулВБ(Элементы.ТаблицаЦен.ТекущиеДанные.Номенклатура, Элементы.ТаблицаЦен.ТекущиеДанные.ХарактеристикаНоменклатуры);
КонецПроцедуры
