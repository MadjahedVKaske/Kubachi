
&НаКлиенте
Процедура B1_ЗаполнитьПоДаннымВБВместо(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		Если Объект.Товары.Количество() > 0 Тогда
			ПоказатьВопросОчисткиТабЧасти("ЗаполнитьПоДаннымВБОчиститьЗавершение");
		Иначе	
			ОткрытьФормуЗагрузкиДанныхВБ();
		КонецЕсли;
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не выбран договор контрагента.");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымВБОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		Объект.Возвраты.Очистить();
		Объект.ТоварыВозвращенные.Очистить();
		Объект.ВозвратыИсходныеДокументы.Очистить();
		Объект.B1_ДатаНачалаПериодаДанныхВБ = Неопределено;
		Объект.B1_ДатаОкончанияПериодаДанныхВБ = Неопределено;
		ЭтотОбъект.Модифицированность = Истина;
		ОткрытьФормуЗагрузкиДанныхВБ();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуЗагрузкиДанныхВБ()

	ОткрытьФорму("Обработка.B1_ЗаполнитьТЧИзДанныхВБ.Форма.Форма", , ЭтотОбъект, , ,
		, Новый ОписаниеОповещения("ЗаполнениеПоДаннымВБВыборЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПоказатьВопросОчисткиТабЧасти(ИмяФункцииОбработчика, ДопПараметры = Неопределено)
	
	ПоказатьВопрос(Новый ОписаниеОповещения(ИмяФункцииОбработчика, ЭтотОбъект, ДопПараметры),
		"Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеПоДаннымВБВыборЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт

	Если РезультатВыбора <> Неопределено Тогда
		Если Не РезультатВыбора.ДанныеЗагрузки.Ошибка Тогда
			Объект.B1_ДатаНачалаПериодаДанныхВБ = ?(ЗначениеЗаполнено(РезультатВыбора.ПериодЗапрашиваемыхДанных.ДатаНачала), РезультатВыбора.ПериодЗапрашиваемыхДанных.ДатаНачала, Неопределено);
			Объект.B1_ДатаОкончанияПериодаДанныхВБ = ?(ЗначениеЗаполнено(РезультатВыбора.ПериодЗапрашиваемыхДанных.ДатаОкончания), РезультатВыбора.ПериодЗапрашиваемыхДанных.ДатаОкончания, Неопределено);
			ЗаполнитьТоварыДокумента(РезультатВыбора.ДанныеЗагрузки);
		Иначе
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Сервис Wildberries вернул ошибку: " + РезультатВыбора.ДанныеЗагрузки.ОписаниеОшибки);	
		КонецЕсли; 
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыДокумента(ДанныеЗагрузки)

	Если Не ДанныеЗагрузки.Ошибка И ЗначениеЗаполнено(ДанныеЗагрузки.АдресТаблицыВХранилище) Тогда
		
		Объект.УчитыватьНДС = Истина;
		Объект.СуммаВключаетНДС = Истина;
		Объект.B1_СтавкаНДСПоУмолчанию = ОбработкаТабличныхЧастей.ЗаполнитьСтавкуНДСПоУмолчанию(Объект.Организация, Объект.Дата);
		
		ПродажиПоУИНИШтрихкоду = Новый ТаблицаЗначений;
		ПродажиПоУИНИШтрихкоду.Колонки.Добавить("УИН");
		ПродажиПоУИНИШтрихкоду.Колонки.Добавить("ШтрихКод");
		ПродажиПоУИНИШтрихкоду.Колонки.Добавить("СерияНоменклатуры");
		ПродажиПоУИНИШтрихкоду.Колонки.Добавить("Количество");
		ПродажиПоУИНИШтрихкоду.Колонки.Добавить("Сумма");
		
		ДоговорКонтрагента = Новый Массив;
		ДоговорКонтрагента.Добавить(Объект.ДоговорКонтрагента);
		//Отладка для старого договора WB/РВБ
		//ДоговорКонтрагента.Добавить(Справочники.ДоговорыКонтрагентов.НайтиПоКоду("00000000114"));
				
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДанныхВБ.ТипДокумента КАК ТипДокумента,
		|	ТаблицаДанныхВБ.ДатаОперации КАК ДатаОперации,
		|	ТаблицаДанныхВБ.ДатаЗаказа КАК ДатаЗаказа,
		|	ТаблицаДанныхВБ.ДатаПродажи КАК ДатаПродажи,
		|	ТаблицаДанныхВБ.ШтрихКод КАК ШтрихКод,
		|	ТаблицаДанныхВБ.Количество КАК Количество,
		|	ТаблицаДанныхВБ.Цена КАК Цена,
		|	ТаблицаДанныхВБ.Сумма КАК Сумма,
		|	ТаблицаДанныхВБ.СуммаВознаграждения КАК СуммаВознаграждения,
		|	ТаблицаДанныхВБ.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
		|	ТаблицаДанныхВБ.УИН КАК УИН
		|ПОМЕСТИТЬ ДанныеПродажВБ
		|ИЗ
		|	&ТаблицаДанныхВБ КАК ТаблицаДанныхВБ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПродажВБ.ТипДокумента КАК ТипДокумента,
		|	ДанныеПродажВБ.ДатаОперации КАК ДатаОперации,
		|	ДанныеПродажВБ.ДатаЗаказа КАК ДатаЗаказа,
		|	ДанныеПродажВБ.ДатаПродажи КАК ДатаПродажи,
		|	ДанныеПродажВБ.ШтрихКод КАК ШтрихКод,
		|	ДанныеПродажВБ.Количество КАК Количество,
		|	ДанныеПродажВБ.Цена КАК Цена,
		|	ДанныеПродажВБ.Сумма КАК Сумма,
		|	ДанныеПродажВБ.СуммаВознаграждения КАК СуммаВознаграждения,
		|	ДанныеПродажВБ.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
		|	ДанныеПродажВБ.УИН КАК УИН,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Владелец КАК Номенклатура,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Вес КАК Вес,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Размер КАК Размер,
		|	ВЫБОР
		|		КОГДА ПартииТоваровПереданныеОстатки.КоличествоОстаток ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ ПартииТоваровПереданныеОстатки.КоличествоОстаток
		|	КОНЕЦ КАК КоличествоПереданное,
		|	ЗНАЧЕНИЕ(Документ.ОтчетКомиссионераОПродажах.ПустаяСсылка) КАК ДокументРеализации
		|ПОМЕСТИТЬ ДанныеПродажВБУИН
		|ИЗ
		|	ДанныеПродажВБ КАК ДанныеПродажВБ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданные.Остатки(&ДатаДокумента, ДоговорКонтрагента В (&ДоговорКонтрагента)) КАК ПартииТоваровПереданныеОстатки
		|		ПО ДанныеПродажВБ.УИН = ПартииТоваровПереданныеОстатки.СерияНоменклатуры.УИН
		|ГДЕ
		|	ДанныеПродажВБ.УИН ЕСТЬ НЕ NULL 
		|	И ДанныеПродажВБ.УИН <> """"
		|	И ДанныеПродажВБ.ТипДокумента = ""Продажа""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеПродажВБ.ТипДокумента,
		|	ДанныеПродажВБ.ДатаОперации,
		|	ДанныеПродажВБ.ДатаЗаказа,
		|	ДанныеПродажВБ.ДатаПродажи,
		|	ДанныеПродажВБ.ШтрихКод,
		|	ДанныеПродажВБ.Количество,
		|	ДанныеПродажВБ.Цена,
		|	ДанныеПродажВБ.Сумма,
		|	ДанныеПродажВБ.СуммаВознаграждения,
		|	ДанныеПродажВБ.СуммаНДСВознаграждения,
		|	ДанныеПродажВБ.УИН,
		|	Продажи1Обороты.СерияНоменклатуры,
		|	Продажи1Обороты.СерияНоменклатуры.Владелец,
		|	Продажи1Обороты.СерияНоменклатуры.ХарактеристикаНоменклатуры,
		|	Продажи1Обороты.СерияНоменклатуры.Вес,
		|	Продажи1Обороты.СерияНоменклатуры.Размер,
		|	ВЫБОР
		|		КОГДА Продажи1Обороты.КоличествоОборот ЕСТЬ NULL
		|			ТОГДА 0
		|		ИНАЧЕ Продажи1Обороты.КоличествоОборот
		|	КОНЕЦ,
		|	Продажи1Обороты.ДокументПродажи
		|ИЗ
		|	ДанныеПродажВБ КАК ДанныеПродажВБ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи1.Обороты(ДОБАВИТЬКДАТЕ(&ДатаДокумента, ГОД, -1), &ДатаДокумента, , ДоговорКонтрагента В (&ДоговорКонтрагента)) КАК Продажи1Обороты
		|		ПО ДанныеПродажВБ.УИН = Продажи1Обороты.СерияНоменклатуры.УИН
		|ГДЕ
		|	ДанныеПродажВБ.УИН ЕСТЬ НЕ NULL 
		|	И ДанныеПродажВБ.УИН <> """"
		|	И ДанныеПродажВБ.ТипДокумента = ""Возврат""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеПродажВБ.ТипДокумента,
		|	ДанныеПродажВБ.ДатаОперации,
		|	ДанныеПродажВБ.ДатаЗаказа,
		|	ДанныеПродажВБ.ДатаПродажи,
		|	ДанныеПродажВБ.ШтрихКод,
		|	ДанныеПродажВБ.Количество,
		|	ДанныеПродажВБ.Цена,
		|	ДанныеПродажВБ.Сумма,
		|	ДанныеПродажВБ.СуммаВознаграждения,
		|	ДанныеПродажВБ.СуммаНДСВознаграждения,
		|	ДанныеПродажВБ.УИН,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|	0,
		|	ЗНАЧЕНИЕ(Справочник.Размер.ПустаяСсылка),
		|	0,
		|	ЗНАЧЕНИЕ(Документ.ОтчетКомиссионераОПродажах.ПустаяСсылка)
		|ИЗ
		|	ДанныеПродажВБ КАК ДанныеПродажВБ
		|ГДЕ
		|	(ДанныеПродажВБ.УИН ЕСТЬ NULL
		|			ИЛИ ДанныеПродажВБ.УИН = """")
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПродажВБ.ШтрихКод КАК ШтрихКод,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Код КАК СерияНоменклатурыКод,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Владелец КАК Номенклатура,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Вес КАК Вес,
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Размер КАК Размер
		|ИЗ
		|	ДанныеПродажВБ КАК ДанныеПродажВБ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровПереданные.Остатки(
		|				&ДатаДокумента,
		|				ДоговорКонтрагента В (&ДоговорКонтрагента)
		|					И НЕ СерияНоменклатуры В
		|							(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|								ДанныеПродажВБУИН.СерияНоменклатуры
		|							ИЗ
		|								ДанныеПродажВБУИН КАК ДанныеПродажВБУИН
		|							ГДЕ
		|								ДанныеПродажВБУИН.ТипДокумента = ""Продажа""
		|								И ДанныеПродажВБУИН.СерияНоменклатуры ЕСТЬ НЕ NULL )) КАК ПартииТоваровПереданныеОстатки
		|		ПО ДанныеПродажВБ.ШтрихКод = ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Наименование
		|ГДЕ
		|	ПартииТоваровПереданныеОстатки.КоличествоОстаток >= ДанныеПродажВБ.Количество
		|	И ДанныеПродажВБ.ТипДокумента = ""Продажа""
		|	И (ДанныеПродажВБ.УИН ЕСТЬ NULL
		|			ИЛИ ДанныеПродажВБ.УИН = """")
		|
		|УПОРЯДОЧИТЬ ПО
		|	ШтрихКод УБЫВ,
		|	СерияНоменклатурыКод УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеПродажВБ.ШтрихКод КАК ШтрихКод,
		|	Продажи1Обороты.СерияНоменклатуры КАК СерияНоменклатуры,
		|	Продажи1Обороты.СерияНоменклатуры.Код КАК СерияНоменклатурыКод,
		|	Продажи1Обороты.СерияНоменклатуры.Владелец КАК Номенклатура,
		|	Продажи1Обороты.СерияНоменклатуры.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	Продажи1Обороты.СерияНоменклатуры.Вес КАК Вес,
		|	Продажи1Обороты.СерияНоменклатуры.Размер КАК Размер,
		|	Продажи1Обороты.ДокументПродажи КАК ДокументРеализации
		|ИЗ
		|	ДанныеПродажВБ КАК ДанныеПродажВБ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Продажи1.Обороты(
		|				ДОБАВИТЬКДАТЕ(&ДатаДокумента, ГОД, -1),
		|				&ДатаДокумента,
		|				,
		|				ДоговорКонтрагента В (&ДоговорКонтрагента)
		|					И НЕ СерияНоменклатуры В
		|							(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|								ДанныеПродажВБУИН.СерияНоменклатуры
		|							ИЗ
		|								ДанныеПродажВБУИН КАК ДанныеПродажВБУИН
		|							ГДЕ
		|								ДанныеПродажВБУИН.ТипДокумента = ""Возврат""
		|								И ДанныеПродажВБУИН.СерияНоменклатуры ЕСТЬ НЕ NULL )) КАК Продажи1Обороты
		|		ПО ДанныеПродажВБ.ШтрихКод = Продажи1Обороты.СерияНоменклатуры.Наименование
		|ГДЕ
		|	Продажи1Обороты.КоличествоОборот >= ДанныеПродажВБ.Количество
		|	И ДанныеПродажВБ.ТипДокумента = ""Возврат""
		|	И (ДанныеПродажВБ.УИН ЕСТЬ NULL
		|			ИЛИ ДанныеПродажВБ.УИН = """")
		|
		|УПОРЯДОЧИТЬ ПО
		|	ШтрихКод УБЫВ,
		|	СерияНоменклатурыКод УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПродажВБУИН.ТипДокумента КАК ТипДокумента,
		|	ДанныеПродажВБУИН.ДатаОперации КАК ДатаОперации,
		|	ДанныеПродажВБУИН.ДатаЗаказа КАК ДатаЗаказа,
		|	ДанныеПродажВБУИН.ДатаПродажи КАК ДатаПродажи,
		|	ДанныеПродажВБУИН.ШтрихКод КАК ШтрихКод,
		|	ДанныеПродажВБУИН.Количество КАК Количество,
		|	ДанныеПродажВБУИН.Цена КАК Цена,
		|	ДанныеПродажВБУИН.Сумма КАК Сумма,
		|	ДанныеПродажВБУИН.СуммаВознаграждения КАК СуммаВознаграждения,
		|	ДанныеПродажВБУИН.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
		|	ДанныеПродажВБУИН.УИН КАК УИН,
		|	ДанныеПродажВБУИН.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ДанныеПродажВБУИН.Номенклатура КАК Номенклатура,
		|	ДанныеПродажВБУИН.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ДанныеПродажВБУИН.Вес КАК Вес,
		|	ДанныеПродажВБУИН.Размер КАК Размер,
		|	ДанныеПродажВБУИН.КоличествоПереданное КАК КоличествоПереданное,
		|	ДанныеПродажВБУИН.ДокументРеализации КАК ДокументРеализации
		|ИЗ
		|	ДанныеПродажВБУИН КАК ДанныеПродажВБУИН
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаОперации";
		
		Запрос.УстановитьПараметр("ДатаДокумента", КонецДня(Объект.Дата));
		Запрос.УстановитьПараметр("ТаблицаДанныхВБ", ПолучитьИзВременногоХранилища(ДанныеЗагрузки.АдресТаблицыВХранилище));
		Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);

		МассивРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
		
		ТЗПродажиБезУИН = МассивРезультатов[2].Выгрузить();
		ТЗВозвратыБезУИН = МассивРезультатов[3].Выгрузить();
		
		//ТЗПродажи = МассивРезультатов[4].Выгрузить();
		//Возврат;

		ВыборкаПродажиВозвратыУИН = МассивРезультатов[4].Выбрать();
		
		Пока ВыборкаПродажиВозвратыУИН.Следующий() Цикл
		
			Если ВыборкаПродажиВозвратыУИН.ТипДокумента = "Продажа" Тогда
				
				ИспользуемаяТабЧасть = Объект.Товары;
				
				Если ЗначениеЗаполнено(ВыборкаПродажиВозвратыУИН.УИН) Тогда
					Если ЗначениеЗаполнено(ВыборкаПродажиВозвратыУИН.СерияНоменклатуры) И ВыборкаПродажиВозвратыУИН.КоличествоПереданное >= ВыборкаПродажиВозвратыУИН.Количество Тогда
						СоздатьИЗаполнитьСтроку(ИспользуемаяТабЧасть, ВыборкаПродажиВозвратыУИН);
						ДобавитьСтрокуПродажПоУИНШК(ПродажиПоУИНИШтрихкоду, ВыборкаПродажиВозвратыУИН, ВыборкаПродажиВозвратыУИН.СерияНоменклатуры);
					Иначе
						ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдены остатки по УИН " + ВыборкаПродажиВозвратыУИН.УИН);
					КонецЕсли;
				Иначе
					НайденнаяСтрока = ТЗПродажиБезУИН.Найти(ВыборкаПродажиВозвратыУИН.ШтрихКод, "ШтрихКод");

					Если НайденнаяСтрока <> Неопределено Тогда
						НоваяСтрока = СоздатьИЗаполнитьСтроку(ИспользуемаяТабЧасть, ВыборкаПродажиВозвратыУИН);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
						ЗаполнитьЦенуИНДССтроки(НоваяСтрока);
						ДобавитьСтрокуПродажПоУИНШК(ПродажиПоУИНИШтрихкоду, ВыборкаПродажиВозвратыУИН, НоваяСтрока.СерияНоменклатуры);
						ТЗПродажиБезУИН.Удалить(НайденнаяСтрока);
					Иначе	
					    ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдены остатки по серии " + ВыборкаПродажиВозвратыУИН.ШтрихКод);
					КонецЕсли;
				КонецЕсли; 
				
			ИначеЕсли ВыборкаПродажиВозвратыУИН.ТипДокумента = "Возврат" Тогда	
				
				ИспользуемаяТабЧасть = Объект.ТоварыВозвращенные;
				
				Если ЗначениеЗаполнено(ВыборкаПродажиВозвратыУИН.УИН) Тогда
					Если ЗначениеЗаполнено(ВыборкаПродажиВозвратыУИН.СерияНоменклатуры) И ВыборкаПродажиВозвратыУИН.КоличествоПереданное >= ВыборкаПродажиВозвратыУИН.Количество Тогда
						НоваяСтрока = СоздатьИЗаполнитьСтроку(ИспользуемаяТабЧасть, ВыборкаПродажиВозвратыУИН);
						НоваяСтрока.КлючСтроки = ПолучитьИДСтрокиВозврата(ВыборкаПродажиВозвратыУИН.ДокументРеализации);
					ИначеЕсли Не СоздатьСтрокуВозвратаПоТекущемуДокументу(ВыборкаПродажиВозвратыУИН, ИспользуемаяТабЧасть, ПродажиПоУИНИШтрихкоду) Тогда
						ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдены продажи для возврата по УИН " + ВыборкаПродажиВозвратыУИН.УИН);
					КонецЕсли; 
				Иначе
					НайденнаяСтрока = ТЗВозвратыБезУИН.Найти(ВыборкаПродажиВозвратыУИН.ШтрихКод, "ШтрихКод");

					Если НайденнаяСтрока <> Неопределено Тогда
						НоваяСтрока = СоздатьИЗаполнитьСтроку(ИспользуемаяТабЧасть, ВыборкаПродажиВозвратыУИН);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
						НоваяСтрока.КлючСтроки = ПолучитьИДСтрокиВозврата(НайденнаяСтрока.ДокументРеализации);
						ТЗВозвратыБезУИН.Удалить(НайденнаяСтрока);
					ИначеЕсли Не СоздатьСтрокуВозвратаПоТекущемуДокументу(ВыборкаПродажиВозвратыУИН, ИспользуемаяТабЧасть, ПродажиПоУИНИШтрихкоду) Тогда
						ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдены продажи для возврата по серии " + ВыборкаПродажиВозвратыУИН.ШтрихКод);
					КонецЕсли; 
				КонецЕсли;
				
			Иначе
				ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Неизвестный тип операции по серии " + ВыборкаПродажиВозвратыУИН.ШтрихКод);
				Продолжить;	
			КонецЕсли;
		
		КонецЦикла;
				
		ЗаполнитьКомментарий();
	    ЭтотОбъект.Модифицированность = Истина;
		УдалитьИзВременногоХранилища(ДанныеЗагрузки.АдресТаблицыВХранилище);
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЦенуИНДССтроки(НоваяСтрока)
	
	Если НоваяСтрока.СерияНоменклатуры.Владелец.БазоваяЕдиницаИзмерения.Код = "796" Тогда
		Знаменатель = НоваяСтрока.Количество;
	Иначе	
	    Знаменатель = НоваяСтрока.СерияНоменклатуры.Вес;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Знаменатель) Тогда
		НоваяСтрока.Цена = Окр(НоваяСтрока.Сумма / Знаменатель, 2);	
	КонецЕсли;
	
	НоваяСтрока.СтавкаНДС = Объект.B1_СтавкаНДСПоУмолчанию;
	СтавкаНДСЧисло = Ценообразование.ПолучитьСтавкуНДС(Объект.B1_СтавкаНДСПоУмолчанию);
	НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * СтавкаНДСЧисло/(100 + СтавкаНДСЧисло); 
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКомментарий()
	
	СтрокаКоментария = ?(ЗначениеЗаполнено(Объект.B1_ДатаНачалаПериодаДанныхВБ), "Период данных с: " + ПолучитьОтформатированнуюДату(Объект.B1_ДатаНачалаПериодаДанныхВБ), "");
	Если ЗначениеЗаполнено(СтрокаКоментария) И ЗначениеЗаполнено(Объект.B1_ДатаОкончанияПериодаДанныхВБ) Тогда
		СтрокаКоментария = СтрокаКоментария  + " по: " + ПолучитьОтформатированнуюДату(Объект.B1_ДатаОкончанияПериодаДанныхВБ);
	ИначеЕсли ЗначениеЗаполнено(Объект.B1_ДатаОкончанияПериодаДанныхВБ) Тогда
		СтрокаКоментария = "Период данных по: " + ПолучитьОтформатированнуюДату(Объект.B1_ДатаОкончанияПериодаДанныхВБ);
	КонецЕсли;
	
	Объект.Комментарий = ?(ЗначениеЗаполнено(СтрокаКоментария), СтрокаКоментария + ", общий вес: " + Объект.Товары.Итог("Вес"), "Общий вес: " + Объект.Товары.Итог("Вес"));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьИДСтрокиВозврата(ДокументРеализации)
	
	ОтборСтрокВозврата = Новый Структура("Сделка", ДокументРеализации);
	СтрокиВозврата = Объект.Возвраты.НайтиСтроки(ОтборСтрокВозврата);
	
	Если СтрокиВозврата.Количество() Тогда
		ИДСтрокиВозврата = СтрокиВозврата[0].КлючСтроки;
	Иначе	
	    НоваяСтрока = Объект.Возвраты.Добавить();
		НоваяСтрока.Покупатель = Объект.Контрагент;
		НоваяСтрока.Сделка = ДокументРеализации;
		НоваяСтрока.КлючСтроки = Новый УникальныйИдентификатор(); 
		ИДСтрокиВозврата = НоваяСтрока.КлючСтроки;
	КонецЕсли;
	
	Возврат ИДСтрокиВозврата;	
КонецФункции

&НаСервере
Функция СоздатьСтрокуВозвратаПоТекущемуДокументу(ВыборкаПродажиВозвратыУИН, ИспользуемаяТабЧасть, ПродажиПоУИНИШтрихкоду)
	
	СтрокаПродажиВДокументе = ПолучитьСтрокуПродажиПоУИНШК(ВыборкаПродажиВозвратыУИН, ПродажиПоУИНИШтрихкоду);

	Если СтрокаПродажиВДокументе <> Неопределено Тогда
		НоваяСтрока = СоздатьИЗаполнитьСтроку(ИспользуемаяТабЧасть, СтрокаПродажиВДокументе);
		НоваяСтрока.Номенклатура = НоваяСтрока.СерияНоменклатуры.Владелец;
		НоваяСтрока.ХарактеристикаНоменклатуры = НоваяСтрока.СерияНоменклатуры.ХарактеристикаНоменклатуры;
		НоваяСтрока.Вес = НоваяСтрока.СерияНоменклатуры.Вес;
		НоваяСтрока.Размер = НоваяСтрока.СерияНоменклатуры.Размер;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПродажиВозвратыУИН,,"СерияНоменклатуры,Номенклатура,ХарактеристикаНоменклатуры,Вес,Размер");
		ЗаполнитьЦенуИНДССтроки(НоваяСтрока);
		НоваяСтрока.КлючСтроки = ПолучитьИДСтрокиВозврата(Документы.ОтчетКомиссионераОПродажах.ПустаяСсылка());
		НоваяСтрока.ПоТекущемуДокументу = Истина;
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

&НаСервере
Процедура ДобавитьСтрокуПродажПоУИНШК(ПродажиПоУИНИШтрихкоду, СтрокаПродаж, СерияНоменклатуры)
	
	НоваяСтрока = ПродажиПоУИНИШтрихкоду.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПродаж);
	НоваяСтрока.СерияНоменклатуры = СерияНоменклатуры; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтрокуПродажиПоУИНШК(СтрокаВозврата, ПродажиПоУИНИШтрихкоду)
		
	ОтборСтрок = Новый Структура("УИН, ШтрихКод, Количество, Сумма");
	ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаВозврата);
	
	Если ЗначениеЗаполнено(СтрокаВозврата.СерияНоменклатуры) Тогда
		ОтборСтрок.Вставить("СерияНоменклатуры", СтрокаВозврата.СерияНоменклатуры);	
	КонецЕсли;

	СтрокиПродаж = ПродажиПоУИНИШтрихкоду.НайтиСтроки(ОтборСтрок);
	КоличествоСтрок = СтрокиПродаж.Количество();
	
	Если КоличествоСтрок Тогда
		Возврат СтрокиПродаж[КоличествоСтрок - 1];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
		
КонецФункции

&НаСервере
Функция СоздатьИЗаполнитьСтроку(ТабЧасть, ДанныеВыборки)
	
	НоваяСтрока = ТабЧасть.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеВыборки);
	ЗаполнитьЦенуИНДССтроки(НоваяСтрока);
	
	Возврат НоваяСтрока;	
КонецФункции

&НаСервере
Функция ПолучитьОтформатированнуюДату(ОбрабатываемаяДата)
	Возврат Формат(ОбрабатываемаяДата, "ДЛФ=D");	
КонецФункции

&НаСервере
Функция НайтиСериюНоменклатурыПоСкладу(СерияКод, Склад, КоличествоНаСкладе, НайденныеСерии)
	
	ВозвращаемаяСерия = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры КАК СерияНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(, Склад = &Склад) КАК ПартииТоваровНаСкладахОстатки
		|ГДЕ
		|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток >= &КоличествоОстаток
		|	И ПартииТоваровНаСкладахОстатки.СерияНоменклатуры.Наименование = &Наименование
		|	И НЕ ПартииТоваровПереданныеОстатки.СерияНоменклатуры В (&НайденныеСерии)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры.Код";
	
	Запрос.УстановитьПараметр("Наименование", СерияКод);
	Запрос.УстановитьПараметр("Склад", Склад); 
	Запрос.УстановитьПараметр("КоличествоОстаток", КоличествоНаСкладе);
	Запрос.УстановитьПараметр("НайденныеСерии", НайденныеСерии);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВозвращаемаяСерия = ВыборкаДетальныеЗаписи.СерияНоменклатуры;		
	КонецЕсли;
	
	Возврат ВозвращаемаяСерия;

КонецФункции


&НаСервере
Функция НайтиСериюНоменклатурыНаКомиссии(СерияУИН, СерияКод, ДоговорКонтрагента, КоличествоНаСкладе, НайденныеСерии, ЭтоВозврат = Ложь)
	
	ВозвращаемаяСерия = Неопределено;
	
	Запрос = Новый Запрос;
	Если ЗначениеЗаполнено(СерияУИН) Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры КАК СерияНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданные.Остатки(&ДатаДокумента, СерияНоменклатуры.УИН = &СерияУИН) КАК ПартииТоваровПереданныеОстатки
		|ГДЕ
		|	ПартииТоваровПереданныеОстатки.КоличествоОстаток > 0";
	    Запрос.УстановитьПараметр("СерияУИН", СерияУИН); 
	ИначеЕсли ЭтоВозврат Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартииТоваровПереданныеОбороты.СерияНоменклатуры КАК СерияНоменклатуры
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданные.Обороты(
		|			,
		|			&ДатаДокумента,
		|			,
		|			СерияНоменклатуры.Наименование = &Наименование
		|				И ДоговорКонтрагента = &ДоговорКонтрагента
		|				И НЕ СерияНоменклатуры В (&НайденныеСерии)) КАК ПартииТоваровПереданныеОбороты
		|ГДЕ
		|	ПартииТоваровПереданныеОбороты.КоличествоРасход > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровПереданныеОбороты.СерияНоменклатуры.Код УБЫВ";
	Иначе 
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПартииТоваровПереданныеОстатки.КоличествоОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ПартииТоваровПереданные.Остатки(
		|			&ДатаДокумента,
		|			СерияНоменклатуры.Наименование = &Наименование
		|				И ДоговорКонтрагента = &ДоговорКонтрагента
		|				И НЕ СерияНоменклатуры В (&НайденныеСерии)) КАК ПартииТоваровПереданныеОстатки
		|ГДЕ
		|	ПартииТоваровПереданныеОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПартииТоваровПереданныеОстатки.СерияНоменклатуры.Код УБЫВ";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаДокумента", КонецДня(Объект.Дата));
	Запрос.УстановитьПараметр("Наименование", СерияКод);
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента); 
	Запрос.УстановитьПараметр("НайденныеСерии", НайденныеСерии);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВозвращаемаяСерия = ВыборкаДетальныеЗаписи.СерияНоменклатуры;		
	КонецЕсли;
	
	Возврат ВозвращаемаяСерия;

КонецФункции
	
