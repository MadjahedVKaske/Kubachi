
&НаСервере
Процедура B1_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ЗаполнитьШК", "Заполнить ШК", "B1_ЗаполнитьШК");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ЗаполнитьШК", Элементы.Заполнить, "Заполнить ШК", "B1_ЗаполнитьШК");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ЗаполнитьУИН", "Заполнить УИН", "B1_ЗаполнитьУИН");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ЗаполнитьУИН", Элементы.Заполнить, "Заполнить УИН", "B1_ЗаполнитьУИН");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_РазделитьДокумент", "Разделить документ", "B1_РазделитьДокумент");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_РазделитьДокумент", Элементы.Заполнить, "Разделить документ", "B1_РазделитьДокумент");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьКоманду(ЭтаФорма, "B1_ПерезаполнитьКлючСтроки", "Перезаполнить ключ строки", "B1_ПерезаполнитьКлючСтроки");
	B1_ПрограммноеСозданиеЭлементов.СоздатьКнопку(ЭтаФорма, "B1_ПерезаполнитьКлючСтроки", Элементы.Заполнить, "Перезаполнить ключ строки", "B1_ПерезаполнитьКлючСтроки");
	
	B1_ПрограммноеСозданиеЭлементов.СоздатьПоле(ЭтаФорма, "B1_Мастер", Элементы.ГруппаДополнительно, 1, "Объект.B1_Мастер");
	
	
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьШК(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
	КонецЕсли;
	
	МассивТоваров = ПолучитьТЧТовары();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МассивТоваров", МассивТоваров);
	ПараметрыОткрытия.Вставить("ИсходныйДокумент", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("B1_ЗаполнитьШК_Завершение", ЭтаФорма);
	
	ОткрытьФорму("Обработка.ЗаполнениеШтрихкодовВТабличнойЧасти.Форма.ФормаОбработки", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//ОткрытьФорму("Обработка.ЗаполнениеШтрихкодовВТабличнойЧасти.Форма.ФормаОбработки", ПараметрыОткрытия, ЭтаФорма, , , ,  , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьУИН(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
	КонецЕсли;
	
	МассивТоваров = ПолучитьТЧТовары();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МассивТоваров", МассивТоваров);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("B1_ЗаполнитьШК_Завершение", ЭтаФорма);
	
	ОткрытьФорму("Обработка.B1_ЗаполнениеУИНВСерияхНоменклатуры.Форма.Форма", ПараметрыОткрытия, ЭтаФорма, , , , ОписаниеОповещения , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьШК_Завершение(ОсновныеПараметры, ДопПараметры) Экспорт
	
	Для каждого СтрокаТовара Из Объект.Товары Цикл
		ОповеститьОбИзменении(СтрокаТовара.СерияНоменклатуры);
	КонецЦикла;
	
	Элементы.Товары.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура B1_РазделитьДокумент(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	B1_РазделитьДокумент_НаСервере();
	
КонецПроцедуры

&НаСервере
Процедура B1_РазделитьДокумент_НаСервере()
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	КоличествоТовары = Объект.Товары.Количество();
	мКоличество = Окр(КоличествоТовары/15) + 1;
	ДатаДокумента = Объект.Дата + 1;
	
	ГлобИтер = 1;
	мЗакончить = Ложь;
	
	Для Итер = 1 По мКоличество Цикл
		ДокументОбъект = Документы.ПоступлениеПродукцииИзПроизводства.СоздатьДокумент();
		ЗаполнитьЗначенияСвойств(ДокументОбъект, Объект);
		ДокументОбъект.Номер = "";
		ДокументОбъект.Дата = ДатаДокумента;
		ДатаДокумента = ДатаДокумента + 1;
		Для каждого ТекСтрока Из Объект.МатериалыДляПередачиВГИИСДМДК Цикл
			НоваяСтрока = ДокументОбъект.МатериалыДляПередачиВГИИСДМДК.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НоваяСтрока.СтатусЗапроса = "";
		КонецЦикла;
		
		
		Для Итер2 = 1 По 15 Цикл
			Если (Итер - 1) * 15 + Итер2 > КоличествоТовары Тогда
				мЗакончить = Истина;
				Прервать;
			КонецЕсли;
			
			ТекСтрока = Объект.Товары[(Итер - 1) * 15 + Итер2 - 1];
			НоваяСтрокаТовары = ДокументОбъект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ТекСтрока);
			НоваяСтрокаТовары.СтатусЗапроса = "";
			НоваяСтрокаТовары.КлючСвязи = НоваяСтрокаТовары.НомерСтроки;
			
			ТекСтрока = Объект.Материалы[(Итер - 1) * 15 + Итер2 - 1];
			НоваяСтрока = ДокументОбъект.Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			НоваяСтрока.СтатусЗапроса = "";
			НоваяСтрока.КлючСвязи = НоваяСтрокаТовары.НомерСтроки;
			
		КонецЦикла;
		ДокументОбъект.Записать();
		
		Если мЗакончить Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Обработка завершена");
	
КонецПроцедуры

&НаКлиенте
Процедура B1_ПерезаполнитьКлючСтроки(Элемент)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Запишите документ");
		Возврат;
	КонецЕсли;
	
	B1_ПерезаполнитьКлючСтроки_НаСервере();
	
КонецПроцедуры

&НаСервере
Процедура B1_ПерезаполнитьКлючСтроки_НаСервере()
	
	Для Каждого ТекСтрока Из Объект.Материалы Цикл
		ТекСтрока.КлючСвязи = ТекСтрока.НомерСтроки;
	КонецЦикла;
	
	Для Каждого ТекСтрока Из Объект.Товары Цикл
		ТекСтрока.КлючСвязи = ТекСтрока.НомерСтроки;
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьТЧТовары()
	
	МассивТоваров = Новый Массив;
	
	Для каждого ТекСтрока Из Объект.Товары Цикл
		
		СтрДанных = Новый Структура;
		СтрДанных.Вставить("Номенклатура",  ТекСтрока.Номенклатура);
		СтрДанных.Вставить("ХарактеристикаНоменклатуры",  ТекСтрока.ХарактеристикаНоменклатуры);
		СтрДанных.Вставить("Размер",  ТекСтрока.Размер);
		СтрДанных.Вставить("СерияНоменклатуры",  ТекСтрока.СерияНоменклатуры);
		
		МассивТоваров.Добавить(СтрДанных);
		
	КонецЦикла;
	
	Возврат МассивТоваров;
	
КонецФункции

&НаСервере
Процедура B1_ЗаполнитьПоСкладуПослеНаСервере()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартииТоваровНаСкладахОстатки.Номенклатура КАК Номенклатура,
		|	ПартииТоваровНаСкладахОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
		|	ПартииТоваровНаСкладахОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
		|	ПартииТоваровНаСкладахОстатки.Размер КАК Размер,
		|	ПартииТоваровНаСкладахОстатки.Качество КАК Качество,
		|	ПартииТоваровНаСкладахОстатки.КоличествоОстаток КАК Количество,
		|	ПартииТоваровНаСкладахОстатки.ВесОстаток КАК Вес,
		|	ПартииТоваровНаСкладахОстатки.СтоимостьОстаток КАК СтоимостьОстаток,
		|	ПартииТоваровНаСкладахОстатки.СтоимостьУпрОстаток КАК СтоимостьУпрОстаток,
		|	ПартииТоваровНаСкладахОстатки.СтоимостьБезНДСОстаток КАК СтоимостьБезНДСОстаток,
		|	ПартииТоваровНаСкладахОстатки.ВесМеталлаВПробеОстаток КАК ВесМеталлаВПробеОстаток,
		|	ПартииТоваровНаСкладахОстатки.ВесМеталлаВЧистотеОстаток КАК ВесМеталлаВЧистотеОстаток
		|ИЗ
		|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(&Дата) КАК ПартииТоваровНаСкладахОстатки
		|ГДЕ
		|	ПартииТоваровНаСкладахОстатки.Склад = &Склад
		|	И ПартииТоваровНаСкладахОстатки.КоличествоОстаток > 0";
	
	Запрос.УстановитьПараметр("Склад", Объект.Склад);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		НоваяСтрока.ВесЧистоты = ЮвелирнаяТорговля.РассчитатьВесЧистоты(НоваяСтрока, Объект.УчитыватьВесВставок, Объект.РучнойУчетВесаВставок);
		Если Не Объект.РучнойУчетВесаВставок Тогда
			НоваяСтрока.ВесВставок = ЮвелирнаяТорговля.РассчитатьВесВставок(НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеОтображения();
КонецПроцедуры

&НаКлиенте
Процедура B1_ЗаполнитьПоСкладуПосле(Команда)
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоСкладуЗавершение", ЭтотОбъект),
		"Табличная часть будет очищена. Продолжить?", РежимДиалогаВопрос.ДаНет);	
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не выбран склад");	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоСкладуЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		B1_ЗаполнитьПоСкладуПослеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура B1_СохранитьВExcelФайлПослеНаСервере(ИмяФайла, ТипФайла)
	B1_СохранениеФайлов.B1_СохранитьБаркодыВExcelФайл(ИмяФайла, ТипФайла, Объект.Ссылка);		
КонецПроцедуры

&НаКлиенте
Процедура B1_СохранитьВExcelФайлПосле(Команда)
	Если ПроверитьМодифицированностьИЗаполненность() Тогда
		ДиалогСохраненияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогСохраненияФайла.ПолноеИмяФайла = "Баркоды Wildberries " + Объект.Номер + ".xlsx";                
		ДиалогСохраненияФайла.Фильтр = "Файл Excel(*.xlsx)|*.xlsx"; 
		ДиалогСохраненияФайла.МножественныйВыбор = Ложь;
		
		Если ДиалогСохраненияФайла.Выбрать() Тогда
			B1_СохранитьВExcelФайлПослеНаСервере(ДиалогСохраненияФайла.ПолноеИмяФайла, ТипФайлаТабличногоДокумента.XLSX);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПроверитьМодифицированностьИЗаполненность()
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Необходимо записать документ.");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Объект.Товары.Количество() Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Таблица цен пустая.");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат	Истина;
КонецФункции

&НаСервереБезКонтекста
&ИзменениеИКонтроль("ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервереБезКонтекста")
Процедура B1_ТоварыХарактеристикаНоменклатурыПриИзмененииНаСервереБезКонтекста(СтруктураСтрокаТабличнойЧасти, СтруктураПараметров, ЗначениеХарактеристики)

	СтрокаТабличнойЧасти = СтруктураСтрокаТабличнойЧасти;

	Если ЗначениеХарактеристики <> Неопределено Тогда
		СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = ЗначениеХарактеристики;
	КонецЕсли;

	// СК [15.01.2021]
	// Проверим корректность характеристики
	Если ВставкиЗаказчика(СтруктураПараметров.МатериалВПроизводстве) Тогда

		// Должна быть заполнена Номенклатура в строках спецификации

		Запрос = Новый Запрос;
		#Удаление
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХарактеристикиНоменклатурыСпецификация.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры.Спецификация КАК ХарактеристикиНоменклатурыСпецификация
		|ГДЕ
		|	ХарактеристикиНоменклатурыСпецификация.Ссылка = &Ссылка
		|	И ХарактеристикиНоменклатурыСпецификация.Номенклатура = &ПустаяНоменклатура";
		#КонецУдаления
		#Вставка
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ХарактеристикиНоменклатурыСпецификация.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры.Спецификация КАК ХарактеристикиНоменклатурыСпецификация
		|ГДЕ
		|	ХарактеристикиНоменклатурыСпецификация.Ссылка = &Ссылка";
		#КонецВставки

		Запрос.УстановитьПараметр("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
		Запрос.УстановитьПараметр("Ссылка", 			СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры);

		РезультатЗапроса = Запрос.Выполнить();
		
		#Удаление
		Если НЕ РезультатЗапроса.Пустой() Тогда
		#КонецУдаления
		#Вставка
		Если РезультатЗапроса.Пустой() Тогда
		#КонецВставки
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Невозможно выбрать характеристику по причине:");

			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();       		
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В строке " + ВыборкаДетальныеЗаписи.НомерСтроки + " спецификации не заполнена номенклатура");	
			КонецЦикла;

			СтрокаТабличнойЧасти.ХарактеристикаНоменклатуры = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();

		КонецЕсли;

	КонецЕсли;	   	
	// \

	// Заполнение реквизитов табличной части.
	ОбработкаТабличныхЧастейСерверУ.ЗаполнитьЦенуПокупкиПоТипуЦенТабЧастиСтруктура(СтрокаТабличнойЧасти,
	СтруктураПараметров);

	// Расчет реквизитов табличной части.
	СтрокаТабличнойЧасти.ВесПотерь = ЮвелирнаяТорговля.РассчитатьВесПотерь(СтрокаТабличнойЧасти,
	СтруктураПараметров.УчитыватьВесВставок, СтруктураПараметров.РучнойУчетВесаВставок, СтруктураПараметров.УчетМеталлаВПроизводствеПоНоменклатуре,
	СтруктураПараметров.ДоговорКонтрагента);

	// Расчет реквизитов табличной части.
	ОбработкаТабличныхЧастейСерверУ.РассчитатьСуммуТабЧастиСтруктура(СтрокаТабличнойЧасти, СтруктураПараметров);
	ОбработкаТабличнойЧастиТоварыКлиентСервер.РассчитатьСуммуНДСТабЧастиСтруктура(СтрокаТабличнойЧасти,
	СтруктураПараметров);

КонецПроцедуры
