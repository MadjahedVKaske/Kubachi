
&НаКлиенте
Процедура ОтправитьДанныеВГИИСДМДК(Команда)
	
	Элементы.ГруппаОшибки.Видимость = Ложь;
	
	Если Элементы.ПолеГалка1.Картинка <> БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда
		ОтправитьДанныеПоОперации("Описание1", Истина);
	ИначеЕсли Элементы.ПолеГалка2.Картинка <> БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда	
		ОтправитьДанныеПоОперации("Описание2", Истина);
	ИначеЕсли Элементы.ПолеГалка3.Картинка <> БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда	
		ОтправитьДанныеПоОперации("Описание3", Истина);	
	ИначеЕсли Элементы.ПолеГалка4.Картинка <> БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда	
		ОтправитьДанныеПоОперации("Описание4", Истина);
	Иначе
		Возврат;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.ВидОперации = Перечисления.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Помощник реализован только для операции преобразования в лом");
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	
	Если Не ДанныеЗаполненыКорректно() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИнтеграцияГИИСДМДК.ПолучитьСертификатыОрганизации(Объект.Организация, ЭтотОбъект);

	НастройкиПоОрганизации = РегистрыСведений.НастройкиОбменаГИИСДМДК.НастройкиПоОрганизации(Объект.Организация);
	
	ТаймаутМеждуПопытками = ?(НастройкиПоОрганизации.ТаймаутМеждуПопытками > 5, НастройкиПоОрганизации.ТаймаутМеждуПопытками, 5);
	КоличествоПопыток = ?(НастройкиПоОрганизации.КоличествоПопыток > 3, НастройкиПоОрганизации.КоличествоПопыток, 3);
	ПодписьНаСервере = НастройкиПоОрганизации.ПодписьНаСервере;
	ОтправкаНаСервере = НастройкиПоОрганизации.ОтправкаНаСервере;
	
	ДопустимоеКоличествоПартий = 50;
	
	Если Объект.СписокЗапросов.Количество() = 0 Тогда
		ЗаполнитьСписокЗапросов();
	КонецЕсли;
	
	ЗаполнитьСтатусОпераций();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусОпераций()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СписокЗапросов.ИНП КАК ИНП,
	                      |	СписокЗапросов.Статус КАК Статус,
	                      |	СписокЗапросов.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	СписокЗапросов.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	СписокЗапросов.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
	                      |	СписокЗапросов.НомерСтрокиТаблЧасти КАК НомерСтрокиТаблЧасти,
	                      |	СписокЗапросов.ИмяМетода КАК ИмяМетода
	                      |ПОМЕСТИТЬ СписокЗапросовПреобразование
	                      |ИЗ
	                      |	&СписокЗапросов КАК СписокЗапросов
	                      |ГДЕ
	                      |	(СписокЗапросов.ИмяМетода = """"
	                      |			ИЛИ СписокЗапросов.ИмяМетода = ""SendBatchConvert""
	                      |			ИЛИ СписокЗапросов.ИмяМетода = ""CheckBatchConvert"")
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПреобразование.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоУспех,
	                      |	СУММА(1) КАК КоличествоВсего,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПреобразование.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Ошибка)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОшибка,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПреобразование.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.ОжиданиеОтвета)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОжиданиеОтвета
	                      |ИЗ
	                      |	СписокЗапросовПреобразование КАК СписокЗапросовПреобразование
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СписокЗапросов.ИНП КАК ИНП,
	                      |	СписокЗапросов.Статус КАК Статус,
	                      |	СписокЗапросов.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	СписокЗапросов.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	СписокЗапросов.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
	                      |	СписокЗапросов.НомерСтрокиТаблЧасти КАК НомерСтрокиТаблЧасти,
	                      |	СписокЗапросов.ИмяМетода КАК ИмяМетода
	                      |ПОМЕСТИТЬ СписокЗапросовПолучениеХимЧистоты
	                      |ИЗ
	                      |	&СписокЗапросов КАК СписокЗапросов
	                      |ГДЕ
	                      |	(СписокЗапросов.ИмяМетода = ""SendGetBatch""
	                      |			ИЛИ СписокЗапросов.ИмяМетода = ""CheckGetBatch"")
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПолучениеХимЧистоты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоУспех,
	                      |	СУММА(1) КАК КоличествоВсего,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПолучениеХимЧистоты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Ошибка)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОшибка,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПолучениеХимЧистоты.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.ОжиданиеОтвета)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОжиданиеОтвета
	                      |ИЗ
	                      |	СписокЗапросовПолучениеХимЧистоты КАК СписокЗапросовПолучениеХимЧистоты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СписокЗапросов.ИНП КАК ИНП,
	                      |	СписокЗапросов.Статус КАК Статус,
	                      |	СписокЗапросов.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	СписокЗапросов.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	СписокЗапросов.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
	                      |	СписокЗапросов.НомерСтрокиТаблЧасти КАК НомерСтрокиТаблЧасти,
	                      |	СписокЗапросов.ИмяМетода КАК ИмяМетода
	                      |ПОМЕСТИТЬ СписокЗапросовОбъединение
	                      |ИЗ
	                      |	&СписокЗапросов КАК СписокЗапросов
	                      |ГДЕ
	                      |	СписокЗапросов.ИмяТабличнойЧасти = """"
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовОбъединение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоУспех,
	                      |	СУММА(1) КАК КоличествоВсего,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовОбъединение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Ошибка)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОшибка,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовОбъединение.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.ОжиданиеОтвета)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОжиданиеОтвета
	                      |ИЗ
	                      |	СписокЗапросовОбъединение КАК СписокЗапросовОбъединение
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СписокЗапросов.ИНП КАК ИНП,
	                      |	СписокЗапросов.Статус КАК Статус,
	                      |	СписокЗапросов.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	СписокЗапросов.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	СписокЗапросов.ИмяТабличнойЧасти КАК ИмяТабличнойЧасти,
	                      |	СписокЗапросов.НомерСтрокиТаблЧасти КАК НомерСтрокиТаблЧасти,
	                      |	СписокЗапросов.ИмяМетода КАК ИмяМетода
	                      |ПОМЕСТИТЬ СписокЗапросовПоКамням
	                      |ИЗ
	                      |	&СписокЗапросов КАК СписокЗапросов
	                      |ГДЕ
	                      |	СписокЗапросов.ИмяТабличнойЧасти = ""Камни""
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПоКамням.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоУспех,
	                      |	СУММА(1) КАК КоличествоВсего,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПоКамням.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Ошибка)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОшибка,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА СписокЗапросовПоКамням.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.ОжиданиеОтвета)
	                      |				ТОГДА 1
	                      |			ИНАЧЕ 0
	                      |		КОНЕЦ) КАК КоличествоОжиданиеОтвета
	                      |ИЗ
	                      |	СписокЗапросовПоКамням КАК СписокЗапросовПоКамням" );
	Запрос.УстановитьПараметр("СписокЗапросов", Объект.СписокЗапросов.Выгрузить());
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаОбъединение = Результат[5].Выбрать();
	Пока ВыборкаОбъединение.Следующий() Цикл
		ЗаполнитьСостояние(ВыборкаОбъединение, 3);			
	КонецЦикла;
	
	Если Элементы.ПолеГалка3.Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено
		Или Элементы.ПолеГалка3.Картинка = БиблиотекаКартинок.СинхронизацияДанныхВыполнение Тогда
		Элементы.ПолеГалка1.Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено;
		Элементы.ТекущееСостояние1.Заголовок = "Данные успешно отправлены";
	Иначе
		ВыборкаПреобразование = Результат[1].Выбрать();
		Пока ВыборкаПреобразование.Следующий() Цикл
			ЗаполнитьСостояние(ВыборкаПреобразование, 1);			
		КонецЦикла;
	КонецЕсли;
	Если Элементы.ПолеГалка3.Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено 
		Или Элементы.ПолеГалка3.Картинка = БиблиотекаКартинок.СинхронизацияДанныхВыполнение Тогда
		Элементы.ПолеГалка2.Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено;
		Элементы.ТекущееСостояние2.Заголовок = "Данные успешно отправлены";
	Иначе
		ВыборкаПолучениеХимЧистоты = Результат[3].Выбрать();
		Пока ВыборкаПолучениеХимЧистоты.Следующий() Цикл			
			ЗаполнитьСостояние(ВыборкаПолучениеХимЧистоты, 2);
		КонецЦикла;
	КонецЕсли;

	ВыборкаПолучениеКамней = Результат[7].Выбрать();
	Пока ВыборкаПолучениеКамней.Следующий() Цикл
		Если ВыборкаПолучениеКамней.КоличествоВсего = NULL Тогда
			Элементы.ПолеГалка4.Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено;
			Элементы.ТекущееСостояние4.Заголовок = "Не требуется";
		Иначе	
			ЗаполнитьСостояние(ВыборкаПолучениеКамней, 4);
		КонецЕсли;			
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСостояние(ВыборкаДанных, НомерОперации)
	Если ВыборкаДанных.КоличествоВсего <> NULL И ВыборкаДанных.КоличествоВсего = ВыборкаДанных.КоличествоУспех Тогда
		Элементы["ПолеГалка"+НомерОперации].Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено;
		Элементы["ТекущееСостояние"+НомерОперации].Заголовок = "Данные успешно отправлены";
	ИначеЕсли ВыборкаДанных.КоличествоВсего = NULL Или ВыборкаДанных.КоличествоОшибка = 0
		И ВыборкаДанных.КоличествоУспех = 0 И ВыборкаДанных.КоличествоОжиданиеОтвета = 0 Тогда
		Элементы["ПолеГалка"+НомерОперации].Картинка = Новый Картинка;
		Элементы["ТекущееСостояние"+НомерОперации].Заголовок = "Данные не отправлялись";
	ИначеЕсли ВыборкаДанных.КоличествоВсего <> NULL 
		И ВыборкаДанных.КоличествоОжиданиеОтвета = ВыборкаДанных.КоличествоВсего Тогда
		Элементы["ПолеГалка"+НомерОперации].Картинка = БиблиотекаКартинок.СинхронизацияДанныхВыполнение;
		Элементы["ТекущееСостояние"+НомерОперации].Заголовок = "Ожидание ответа от ГИИС ДМДК";
	Иначе
		Элементы["ПолеГалка"+НомерОперации].Картинка = БиблиотекаКартинок.Остановить;
		Элементы["ТекущееСостояние"+НомерОперации].Заголовок = "Есть ошибки при отправке данных";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокЗапросов()
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	КомплектацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	                      |	КомплектацияТоваровТовары.СерияНоменклатуры КАК СерияНоменклатуры,
	                      |	КомплектацияТоваровТовары.ИНП КАК ИНП,
	                      |	КомплектацияТоваровТовары.ВесВХимЧистоте КАК ВесВХимЧистоте,
	                      |	КомплектацияТоваровТовары.ИНППреобразованияВЛом КАК ИНППреобразованияВЛом,
	                      |	КомплектацияТоваровТовары.ВесВХимЧистотеГИИСДМДК КАК ВесВХимЧистотеГИИСДМДК,
	                      |	КомплектацияТоваровТовары.ИмяМетода КАК ИмяМетода,
	                      |	КомплектацияТоваровТовары.СтатусЗапроса КАК СтатусЗапроса,
	                      |	КомплектацияТоваровТовары.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	КомплектацияТоваровТовары.ИдентификаторЗапроса КАК ИдентификаторЗапроса
	                      |ИЗ
	                      |	Документ.КомплектацияТоваров.Товары КАК КомплектацияТоваровТовары
	                      |ГДЕ
	                      |	КомплектацияТоваровТовары.Ссылка = &Ссылка
	                      |	И (КомплектацияТоваровТовары.ИмяМетода = """"
	                      |			ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""SendBatchConvert""
	                      |			ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""CheckBatchConvert""
	                      |			ИЛИ КомплектацияТоваровТовары.ИНППреобразованияВЛом <> """")
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	""Товары"" КАК ИмяТабличнойЧасти,
	                      |	КомплектацияТоваровТовары.НомерСтроки КАК НомерСтроки,
	                      |	КомплектацияТоваровТовары.ИНП КАК ИНП,
	                      |	КомплектацияТоваровТовары.ИмяМетода КАК ИмяМетода,
	                      |	ВЫБОР
	                      |		КОГДА КомплектацияТоваровТовары.ИмяМетода = ""SendBatchUnion""
	                      |				ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""CheckBatchUnion""
	                      |			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                      |		ИНАЧЕ КомплектацияТоваровТовары.СтатусЗапроса
	                      |	КОНЕЦ КАК СтатусЗапроса,
	                      |	КомплектацияТоваровТовары.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	КомплектацияТоваровТовары.ИдентификаторЗапроса КАК ИдентификаторЗапроса
	                      |ИЗ
	                      |	Документ.КомплектацияТоваров.Товары КАК КомплектацияТоваровТовары
	                      |ГДЕ
	                      |	КомплектацияТоваровТовары.Ссылка = &Ссылка
	                      |	И (КомплектацияТоваровТовары.ИмяМетода = ""CheckGetBatch""
	                      |			ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""SendGetBatch""
	                      |			ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""SendBatchUnion""
	                      |			ИЛИ КомплектацияТоваровТовары.ИмяМетода = ""CheckBatchUnion"")
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	""Металл"",
	                      |	КомплектацияТоваровМеталл.НомерСтроки,
	                      |	КомплектацияТоваровМеталл.ИНП,
	                      |	""SendGetBatch"",
	                      |	КомплектацияТоваровМеталл.СтатусЗапроса,
	                      |	КомплектацияТоваровМеталл.ОписаниеОшибки,
	                      |	КомплектацияТоваровМеталл.ИдентификаторЗапроса
	                      |ИЗ
	                      |	Документ.КомплектацияТоваров.Металл КАК КомплектацияТоваровМеталл
	                      |ГДЕ
	                      |	КомплектацияТоваровМеталл.Ссылка = &Ссылка
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КомплектацияТоваров.ИНП КАК ИНП,
	                      |	КомплектацияТоваров.ИмяМетода КАК ИмяМетода,
	                      |	КомплектацияТоваров.СтатусЗапроса КАК СтатусЗапроса,
	                      |	КомплектацияТоваров.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	КомплектацияТоваров.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	0 КАК НомерСтроки,
	                      |	"""" КАК ИмяТабличнойЧасти
	                      |ИЗ
	                      |	Документ.КомплектацияТоваров КАК КомплектацияТоваров
	                      |ГДЕ
	                      |	КомплектацияТоваров.Ссылка = &Ссылка
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	КомплектацияТоваровКамни.ИНП КАК ИНП,
	                      |	КомплектацияТоваровКамни.СтатусЗапроса КАК СтатусЗапроса,
	                      |	КомплектацияТоваровКамни.ОписаниеОшибки КАК ОписаниеОшибки,
	                      |	КомплектацияТоваровКамни.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                      |	""Камни"" КАК ИмяТабличнойЧасти,
	                      |	КомплектацияТоваровКамни.НомерСтроки КАК НомерСтроки
	                      |ИЗ
	                      |	Документ.КомплектацияТоваров.Камни КАК КомплектацияТоваровКамни
	                      |ГДЕ
	                      |	КомплектацияТоваровКамни.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Объект.СписокЗапросов.Очистить();
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаПреобразованиеВлом = Результат[0].Выбрать();
	Пока ВыборкаПреобразованиеВлом.Следующий() Цикл
		НовыйЗапрос = Объект.СписокЗапросов.Добавить();
		НовыйЗапрос.ИмяТабличнойЧасти = "Товары";
		НовыйЗапрос.НомерСтрокиТаблЧасти = ВыборкаПреобразованиеВЛом.НомерСтроки;
		НовыйЗапрос.ИНП = ВыборкаПреобразованиеВЛом.ИНППреобразованияВЛом;
		Если ВыборкаПреобразованиеВлом.ИНППреобразованияВЛом <> "" Тогда
			НовыйЗапрос.Статус = ИнтеграцияГИИСДМДК.СтатусУспех();
			НовыйЗапрос.ОписаниеОшибки = "";
			НовыйЗапрос.ИдентификаторЗапроса = "";	
			НовыйЗапрос.ИмяМетода = "SendBatchConvert";
		Иначе	
			НовыйЗапрос.Статус = ВыборкаПреобразованиеВЛом.СтатусЗапроса;
			НовыйЗапрос.ОписаниеОшибки = ВыборкаПреобразованиеВЛом.ОписаниеОшибки;
			НовыйЗапрос.ИдентификаторЗапроса = ВыборкаПреобразованиеВЛом.ИдентификаторЗапроса;
			Если ВыборкаПреобразованиеВлом.ИмяМетода = "" Тогда
				НовыйЗапрос.ИмяМетода = "SendBatchConvert";
			Иначе	
				НовыйЗапрос.ИмяМетода = ВыборкаПреобразованиеВлом.ИмяМетода;
			КонецЕсли;	
		КонецЕсли;
	КонецЦикла;	
	
	ВыборкаПолучениеВесаХимЧистоты = Результат[1].Выбрать();
	Пока ВыборкаПолучениеВесаХимЧистоты.Следующий() Цикл
		НовыйЗапрос = Объект.СписокЗапросов.Добавить();
		НовыйЗапрос.ИмяТабличнойЧасти = ВыборкаПолучениеВесаХимЧистоты.ИмяТабличнойЧасти;
		НовыйЗапрос.НомерСтрокиТаблЧасти = ВыборкаПолучениеВесаХимЧистоты.НомерСтроки;
		НовыйЗапрос.ИНП = ВыборкаПолучениеВесаХимЧистоты.ИНП;
		Если ВыборкаПолучениеВесаХимЧистоты.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех() Тогда
			НовыйЗапрос.Статус = ИнтеграцияГИИСДМДК.СтатусУспех();
			НовыйЗапрос.ОписаниеОшибки = "";
			НовыйЗапрос.ИдентификаторЗапроса = "";	
		Иначе	
			НовыйЗапрос.Статус = ВыборкаПолучениеВесаХимЧистоты.СтатусЗапроса;
			НовыйЗапрос.ОписаниеОшибки = ВыборкаПолучениеВесаХимЧистоты.ОписаниеОшибки;
			НовыйЗапрос.ИдентификаторЗапроса = ВыборкаПолучениеВесаХимЧистоты.ИдентификаторЗапроса;
		КонецЕсли;
		НовыйЗапрос.ИмяМетода = "SendGetBatch";
	КонецЦикла;	
	
	ВыборкаОбъединениеВОднуПартию = Результат[2].Выбрать();
	Пока ВыборкаОбъединениеВОднуПартию.Следующий() Цикл
		НовыйЗапрос = Объект.СписокЗапросов.Добавить();
		НовыйЗапрос.ИмяТабличнойЧасти = ВыборкаОбъединениеВОднуПартию.ИмяТабличнойЧасти;
		НовыйЗапрос.НомерСтрокиТаблЧасти = ВыборкаОбъединениеВОднуПартию.НомерСтроки;
		НовыйЗапрос.ИНП = ВыборкаОбъединениеВОднуПартию.ИНП;
		Если ВыборкаОбъединениеВОднуПартию.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех() Тогда
			НовыйЗапрос.Статус = ИнтеграцияГИИСДМДК.СтатусУспех();
			НовыйЗапрос.ОписаниеОшибки = "";
			НовыйЗапрос.ИдентификаторЗапроса = "";	
		Иначе	
			НовыйЗапрос.Статус = ВыборкаОбъединениеВОднуПартию.СтатусЗапроса;
			НовыйЗапрос.ОписаниеОшибки = ВыборкаОбъединениеВОднуПартию.ОписаниеОшибки;
			НовыйЗапрос.ИдентификаторЗапроса = ВыборкаОбъединениеВОднуПартию.ИдентификаторЗапроса;
		КонецЕсли;
		НовыйЗапрос.ИмяМетода = "SendBatchUnion";
	КонецЦикла;	
	
	ВыборкаПолучениеДанныхПоКамням = Результат[3].Выбрать();
	Пока ВыборкаПолучениеДанныхПоКамням.Следующий() Цикл
		НовыйЗапрос = Объект.СписокЗапросов.Добавить();
		НовыйЗапрос.ИмяТабличнойЧасти = ВыборкаПолучениеДанныхПоКамням.ИмяТабличнойЧасти;
		НовыйЗапрос.НомерСтрокиТаблЧасти = ВыборкаПолучениеДанныхПоКамням.НомерСтроки;
		НовыйЗапрос.ИНП = ВыборкаПолучениеДанныхПоКамням.ИНП;
		Если ВыборкаПолучениеДанныхПоКамням.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех() Тогда
			НовыйЗапрос.Статус = ИнтеграцияГИИСДМДК.СтатусУспех();
			НовыйЗапрос.ОписаниеОшибки = "";
			НовыйЗапрос.ИдентификаторЗапроса = "";	
		Иначе	
			НовыйЗапрос.Статус = ВыборкаПолучениеДанныхПоКамням.СтатусЗапроса;
			НовыйЗапрос.ОписаниеОшибки = ВыборкаПолучениеДанныхПоКамням.ОписаниеОшибки;
			НовыйЗапрос.ИдентификаторЗапроса = ВыборкаПолучениеДанныхПоКамням.ИдентификаторЗапроса;
		КонецЕсли;
		НовыйЗапрос.ИмяМетода = "SendGetBatch";
	КонецЦикла;
		
КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьСписокЗапросов(Команда)
	ЗаполнитьСписокЗапросов();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибки(НомерОперации)
	СтруктураПоиска = Новый Структура;
	НомерТекущейОперации = Число(Прав(НомерОперации,1));
	Если НомерТекущейОперации = 1 Тогда
		СтруктураПоиска.Вставить("ИмяМетода", "SendBatchConvert");
	КонецЕсли;
	СтруктураПоиска.Вставить("Статус", ИнтеграцияГИИСДМДККлиент.СтатусОшибка());
	ЕстьСтроки = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоиска);
	СписокЗапросов.Очистить();
	Элементы.ГруппаОшибки.Видимость = истина;
	Для Каждого Стр Из ЕстьСтроки Цикл
		НоваяСтрока = СписокЗапросов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
	КонецЦикла;	 
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗапросы(НомерОперации)
	СтруктураПоиска = Новый Структура;
	НомерТекущейОперации = Число(Прав(НомерОперации,1));
	Если НомерТекущейОперации = 1 Тогда
		СтруктураПоиска.Вставить("ИмяМетода", "SendBatchConvert");
	ИначеЕсли НомерТекущейОперации = 2 Тогда
		СтруктураПоиска.Вставить("ИмяМетода", "SendGetBatch");
	ИначеЕсли НомерТекущейОперации = 3 Тогда
		СтруктураПоиска.Вставить("ИмяМетода", "SendBatchUnion");
	ИначеЕсли НомерТекущейОперации = 4 Тогда
		СтруктураПоиска.Вставить("ИмяМетода", "SendGetBatch");	
		СтруктураПоиска.Вставить("ИмяТабличнойЧасти", "Камни");	
	КонецЕсли;
	ЕстьСтроки = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоиска);
	СписокИдентификаторовЗапросов = Новый Массив;
	Для Каждого Стр Из ЕстьСтроки Цикл
		Если Стр.ИдентификаторЗапроса <> "" Тогда
			СписокИдентификаторовЗапросов.Добавить(Стр.ИдентификаторЗапроса);
		КонецЕсли;	
	КонецЦикла;
	Если СписокИдентификаторовЗапросов.Количество()>0 Тогда
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("ИдентификаторГИИС", СписокИдентификаторовЗапросов);
		ОткрытьФорму("РегистрСведений.ЖурналЗапросовГИИСДМДК.Форма.ФормаСписка", ПараметрыОткрытияФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не найдены запросы для отображения");
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ДанныеЗаполненыКорректно()
	НетОшибок = Истина;	
	Если Не ЗначениеЗаполнено(Объект.Комплект) Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен комплект");
		НетОшибок = Ложь;	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.СерияНоменклатурыКомплекта) Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнена серия комплекта");
		НетОшибок = Ложь;	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Комплект.ОКПД2) Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен ОКПД2 для комплекта");
		НетОшибок = Ложь;	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Комплект.ВидПартииМеталла) Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен вид партии металла для комплекта");
		НетОшибок = Ложь;	
	КонецЕсли;
	Если Объект.ВесКомплекта = 0 Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен вес комплекта");
		НетОшибок = Ложь;	
	КонецЕсли;
	Если Объект.Товары.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнена табличная часть ""Товары""");
		НетОшибок = Ложь;	
	КонецЕсли;
	
	Для Каждого Стр Из Объект.Товары Цикл
		Если Не ЗначениеЗаполнено(Стр.Номенклатура) Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнена номенклатура");
			НетОшибок = Ложь;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Стр.СерияНоменклатуры) Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнена серия");
			НетОшибок = Ложь;
		ИначеЕсли ПустаяСтрока(Стр.СерияНоменклатуры.УИН)
			и (Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияЛом")
				или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни")
				или Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом")) Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен УИН в серии");
			НетОшибок = Ложь;	
		КонецЕсли;
		Если Стр.Вес = 0 Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен вес");
			НетОшибок = Ложь;	
		КонецЕсли;
	КонецЦикла;	
	
	Возврат НетОшибок;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьИнформациюОКомплектации(ВыбСертификат, Пакеты, НомерПакета = 1, НомерОперации, ПереходитьКСледующейОперации)
	ДанныеРегистрации = Пакеты.Получить(НомерПакета);
	
	Если ДанныеРегистрации = Неопределено Тогда
		
		ИдетОтправкаДанных = Ложь;
		
		ПараметрыЗаписи = Новый Структура;
    	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		Записать(ПараметрыЗаписи);
		
		Если ПереходитьКСледующейОперации Тогда
			ОтправитьДанныеПоОперации("Описание" + (НомерОперации + 1), ПереходитьКСледующейОперации);
		КонецЕсли;
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеРегистрации.Свойство("ИдентификаторЗапроса") = Истина Тогда
		ДанныеОЗапросе = Новый Структура;
		ДанныеОЗапросе.Вставить("messageId", ДанныеРегистрации.ИдентификаторЗапроса);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("НомераСтрок", ДанныеРегистрации);
		ДополнительныеПараметры.Вставить("Пакеты", Пакеты);
		ДополнительныеПараметры.Вставить("НомерПакета", НомерПакета);
		ДополнительныеПараметры.Вставить("НомерОперации", НомерОперации);
		ДополнительныеПараметры.Вставить("ПереходитьКСледующейОперации", ПереходитьКСледующейОперации);
		ДополнительныеПараметры.Вставить("Сертификат", ВыбСертификат);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправкаКомплектацийВГИИСЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыВыполнения = ИнтеграцияГИИСДМДККлиент.ИнициализироватьПараметрыВыполнения();
		ПараметрыВыполнения.ИмяМетода = ДанныеРегистрации.ИмяМетода;
		ПараметрыВыполнения.ТребуетсяПодпись = Истина;
		ПараметрыВыполнения.Сертификат = ВыбСертификат;
		ПараметрыВыполнения.МетодПодписи = "БСП";
		ПараметрыВыполнения.ПолучатьРезультат = Ложь;
		ПараметрыВыполнения.ТаймаутМеждуПопытками = ТаймаутМеждуПопытками;
		ПараметрыВыполнения.КоличествоПопыток = КоличествоПопыток;
		ПараметрыВыполнения.ПодписьНаСервере = ПодписьНаСервере;
		ПараметрыВыполнения.ОтправкаНаСервере = ОтправкаНаСервере;
		ПараметрыВыполнения.Организация = Объект.Организация;
		
		НастройкиПоОрганизации.ТаймаутМеждуПопытками = ТаймаутМеждуПопытками;
		НастройкиПоОрганизации.КоличествоПопыток = КоличествоПопыток;
		НастройкиПоОрганизации.ПодписьНаСервере = ПодписьНаСервере;
		НастройкиПоОрганизации.ОтправкаНаСервере = ОтправкаНаСервере;
		ПараметрыВыполнения.НастройкиОбмена = НастройкиПоОрганизации;
		
		ИнтеграцияГИИСДМДККлиент.ВыполнитьМетодСервиса(ДанныеОЗапросе, ПараметрыВыполнения, ЭтотОбъект, ОписаниеОповещения);
		
	Иначе
		Если ДанныеРегистрации.Свойство("Спецификации") И Не ЗначениеЗаполнено(ДанныеРегистрации.Спецификации) Тогда
			ИдетОтправкаДанных = Ложь;
			Возврат;
		КонецЕсли;
		Если ДанныеРегистрации.Свойство("УИН") И Не ЗначениеЗаполнено(ДанныеРегистрации.УИН) Тогда
			ИдетОтправкаДанных = Ложь;
			Возврат;
		КонецЕсли;
			
		ДанныеДляОтправки = Новый Структура();
		Если ДанныеРегистрации.Свойство("Спецификации") Тогда
			Спецификации = Новый Массив;
			Спецификации.Добавить(ДанныеРегистрации.Спецификации);
			ДанныеДляОтправки.Вставить("Спецификации", Спецификации);
		Иначе
			ДанныеДляОтправки.Вставить("УИН", ДанныеРегистрации.УИН);
			Если ДанныеРегистрации.Свойство("ТипИерархии") Тогда
				ДанныеДляОтправки.Вставить("ТипИерархии", ДанныеРегистрации.ТипИерархии);
			КонецЕсли;
			Если ДанныеРегистрации.Свойство("page") Тогда
				ДанныеДляОтправки.Вставить("page", ДанныеРегистрации.page);
			КонецЕсли;
			Если ДанныеРегистрации.Свойство("size") Тогда
				ДанныеДляОтправки.Вставить("size", ДанныеРегистрации.size);
			КонецЕсли;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("НомераСтрок", ДанныеРегистрации.НомераСтрокНеОтправлено);
		ДополнительныеПараметры.Вставить("Пакеты", Пакеты);
		ДополнительныеПараметры.Вставить("НомерПакета", НомерПакета);
		ДополнительныеПараметры.Вставить("НомерОперации", НомерОперации);
		ДополнительныеПараметры.Вставить("ПереходитьКСледующейОперации", ПереходитьКСледующейОперации);
		ДополнительныеПараметры.Вставить("Сертификат", ВыбСертификат);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправкаКомплектацийВГИИСЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыВыполнения = ИнтеграцияГИИСДМДККлиент.ИнициализироватьПараметрыВыполнения();
		ПараметрыВыполнения.ИмяМетода = ДанныеРегистрации.ИмяМетода;
		ПараметрыВыполнения.ТребуетсяПодпись = Истина;
		ПараметрыВыполнения.Сертификат = ВыбСертификат;
		ПараметрыВыполнения.МетодПодписи = "БСП";
		ПараметрыВыполнения.ПолучатьРезультат = Истина;
		ПараметрыВыполнения.ТаймаутМеждуПопытками = ТаймаутМеждуПопытками;
		ПараметрыВыполнения.КоличествоПопыток = КоличествоПопыток;
		ПараметрыВыполнения.ПодписьНаСервере = ПодписьНаСервере;
		ПараметрыВыполнения.ОтправкаНаСервере = ОтправкаНаСервере;
		ПараметрыВыполнения.Организация = Объект.Организация;
		
		НастройкиПоОрганизации.ТаймаутМеждуПопытками = ТаймаутМеждуПопытками;
		НастройкиПоОрганизации.КоличествоПопыток = КоличествоПопыток;
		НастройкиПоОрганизации.ПодписьНаСервере = ПодписьНаСервере;
		НастройкиПоОрганизации.ОтправкаНаСервере = ОтправкаНаСервере;
		ПараметрыВыполнения.НастройкиОбмена = НастройкиПоОрганизации;
		
		ИнтеграцияГИИСДМДККлиент.ВыполнитьМетодСервиса(ДанныеДляОтправки, ПараметрыВыполнения, ЭтотОбъект, ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИНППреобразованияВЛом(ТекСтрТовары, ТекУИН, НомерСтроки, ДополнительныеПараметры)
	
	ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех");
	ТекСтрТовары.ИНППреобразованияВЛом = "";
	ТекСтрТовары.ОписаниеОшибки = "";
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИНП", ТекУИН);
	ЕстьСтроки = Объект.Металл.НайтиСтроки(СтруктураОтбора);
	Если ЕстьСтроки.Количество() = 0 Тогда
		ТекСтрМеталл = Объект.Металл.Добавить();
		ТекСтрМеталл.ИНП = ТекУИН;
		ТекСтрМеталл.ИНПИзделия = ТекСтрТовары.ИНП;
	КонецЕсли;
	//НомерСледующегоПакета = ДополнительныеПараметры.НомерПакета + 1;
	//ТекПакет = ДополнительныеПараметры.Пакеты.Получить(НомерСледующегоПакета);
	//Пока ТекПакет <> Неопределено Цикл
	//	Если ТекПакет.ИмяМетода = "SendBatchUnion" Тогда
	//		Для Каждого ТекСтрРодительскойПартии Из ТекПакет.Спецификации.МассивТоваров[0].МассивРодительскихПартий Цикл
	//			Если ТекСтрРодительскойПартии.НомерСтроки = Число(НомерСтроки) Тогда
	//				Если ЗначениеЗаполнено(ТекСтрРодительскойПартии.УИН) Тогда
	//					НоваяРодительскаяПартия = ТекПакет.Спецификации.МассивТоваров[0].МассивРодительскихПартий.Добавить();
	//					НоваяРодительскаяПартия.НомерСтроки = ТекСтрРодительскойПартии.НомерСтроки;
	//					//НоваяРодительскаяПартия.Количество = Стр.Количество;
	//					//прервать!!!
	//					НоваяРодительскаяПартия.УИН = ТекУИН;
	//				Иначе	
	//					ТекСтрРодительскойПартии.УИН = ТекУИН;
	//				КонецЕсли;
	//				Прервать;
	//			КонецЕсли;
	//		КонецЦикла;	
	//	КонецЕсли;
	//	НомерСледующегоПакета = НомерСледующегоПакета + 1;
	//	ТекПакет = ДополнительныеПараметры.Пакеты.Получить(НомерСледующегоПакета);
	//КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьНовыйСтатусНаСервере(Данные, НовыйСтатус, ДополнительныеПараметры, ИдентификаторЗапроса)
	
	НомерСтроки = ДополнительныеПараметры.НомераСтрок;
	ИмяМетода = "";
	Если ТипЗнч(НомерСтроки) = Тип("Структура") Тогда
		Если НомерСтроки.Свойство("ИмяМетода") Тогда
			ИмяМетода = НомерСтроки.ИмяМетода;
		КонецЕсли;
	КонецЕсли;	
	ЕстьОшибки = Ложь;
	
	Если Данные.Количество() = 0 Тогда
		Возврат ЕстьОшибки;	
	КонецЕсли;
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		Если Данные.Свойство("Ошибка") и Данные.Ошибка = ""	Тогда
			Возврат ЕстьОшибки;
		КонецЕсли;	
	КонецЕсли;
	
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	
	ОбработанныйПакет = ДополнительныеПараметры.Пакеты.Получить(ДополнительныеПараметры.НомерПакета);
	Для Каждого ТекУИН Из Данные Цикл
		Если НовыйСтатус = "Ошибка" 
			И ((ТекУИН.Свойство("Ошибка") и ТекУИН.Ошибка = "")
			Или (ТекУИН.Свойство("Значение") и ТекУИН.Значение = "")) Тогда
			Продолжить;
		КонецЕсли;	
		Если ОбработанныйПакет.ИмяМетода = "SendBatchConvert" 
			Или ОбработанныйПакет.ИмяМетода = "CheckBatchConvert" Тогда
			Если НовыйСтатус = "Успех" Тогда
				ТекСтрТовары = Объект.Товары[Число(ТекУИН.НомерСтроки) - 1];
			Иначе
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("ИНП", ТекУИН.УИН);
				ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
				Если ЕстьСтроки.Количество() > 0 Тогда
					ТекСтрТовары = ЕстьСтроки[0];
				КонецЕсли;	
			КонецЕсли;
						
			Если НовыйСтатус = "Успех" Тогда
				Если ТипЗнч(ТекУИН.УИН) = Тип("Массив") Тогда
					Если ТекУИН.УИН.Количество() > 1 Тогда
						Для Каждого ТекУИНИзМассива Из ТекУИН.УИН Цикл
							ЗаполнитьИНППреобразованияВЛом(ТекСтрТовары, ТекУИНИзМассива, ТекУИН.НомерСтроки, ДополнительныеПараметры);	
						КонецЦикла;	
						ИНППреобразованияВЛом = "";
					Иначе
						ИНППреобразованияВЛом = ТекУИН.УИН[0];
					КонецЕсли;	
				Иначе
					ИНППреобразованияВЛом = ТекУИН.УИН;
				КонецЕсли;	
				ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех");
				ТекСтрТовары.ИНППреобразованияВЛом = ИНППреобразованияВЛом;
				ТекСтрТовары.ОписаниеОшибки = "";
				НомерСледующегоПакета = ДополнительныеПараметры.НомерПакета + 1;
				Если ТекУИН.Свойство("Камни") Тогда
					Для Каждого КаменьУИН Из ТекУИН.Камни.УИН Цикл
						СтруктураПоиска = Новый Структура;
						СтруктураПоиска.Вставить("ИНП", КаменьУИН);
						ЕстьКамни = Объект.Камни.НайтиСтроки(СтруктураПоиска);
						Если ЕстьКамни.Количество() = 0 Тогда
							НовСтрКамень = Объект.Камни.Добавить();	
							НовСтрКамень.ИНП = КаменьУИН;
						КонецЕсли;
					КонецЦикла;
					Если Объект.Камни.Количество() > 0 Тогда
						Элементы.ГруппаОперация4.Видимость = Истина;
					КонецЕсли;
					ВсегоПакетов = ДополнительныеПараметры.Пакеты.Количество();
					Если ДополнительныеПараметры.Пакеты.Получить(ВсегоПакетов) <> Неопределено Тогда
						ВсегоПакетов = ВсегоПакетов + 1;						
					КонецЕсли;
				КонецЕсли;	
			ИначеЕсли НовыйСтатус = "Ошибка" Тогда
				ТекСтрТовары.ИдентификаторЗапроса = ИдентификаторЗапроса;
				ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Ошибка");
				ТекСтрТовары.ОписаниеОшибки = ТекУИН.Ошибка;
				ЕстьОшибки = Истина;	
			КонецЕсли;
			ТекСтрТовары.ИмяМетода = ОбработанныйПакет.ИмяМетода;
			
			СтруктураПоискаЗапросов = Новый Структура;
			СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтрТовары.НомерСтроки);
			СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "Товары");
			СтруктураПоискаЗапросов.Вставить("ИмяМетода", "SendBatchConvert");
			ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
			Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
				ТекущийЗапрос.Статус = ТекСтрТовары.СтатусЗапроса;
				ТекущийЗапрос.ОписаниеОшибки = ТекСтрТовары.ОписаниеОшибки;
				ТекущийЗапрос.ИдентификаторЗапроса = ТекСтрТовары.ИдентификаторЗапроса;
			КонецЦикла;	

			НомерСтроки = 0;
		ИначеЕсли (ОбработанныйПакет.ИмяМетода = "SendGetBatch"
			Или ОбработанныйПакет.ИмяМетода = "CheckGetBatch") И ОбработанныйПакет.Свойство("ИмяТабЧасти") Тогда
			Если ТекУИН.Свойство("ДанныеОКамне") И ОбработанныйПакет.ИмяТабЧасти = "Камни" Тогда
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("ИНП", ТекУИН.УИН);
				ЕстьСтроки = Объект.Камни.НайтиСтроки(СтруктураОтбора);
				Если ЕстьСтроки.Количество() > 0 Тогда
					ТекСтрТовары = ЕстьСтроки[0];
				КонецЕсли;
				Если НовыйСтатус = "Успех" Тогда
					ДанныеРазбораКода = ИнтеграцияГИИСДМДКЮТДСервер.РазобратьКлассификационныйКодПартииКамней(ТекУИН.ДанныеОКамне.КлассификационныйКод);
					Если ДанныеРазбораКода <> Неопределено
						И ТипЗнч(ДанныеРазбораКода) = Тип("Структура") Тогда
						
						Если ДанныеРазбораКода.Свойство("ВидКамня") Тогда
							
							ТекСтрТовары.Камень = ДанныеРазбораКода.ВидКамня;
							
						КонецЕсли;
						
						Если ДанныеРазбораКода.Свойство("ГруппаКодированияГИИСДМДК") Тогда
							
							ТекСтрТовары.ГруппаКодированияГИИСДМДК = ДанныеРазбораКода.ГруппаКодированияГИИСДМДК;
							
						КонецЕсли;
						
						Если ДанныеРазбораКода.Свойство("ХарактеристикиГИИСДМДК") Тогда
							
							Для Каждого ТекХарактеристика Из ДанныеРазбораКода.ХарактеристикиГИИСДМДК Цикл
								Если ТекХарактеристика.ИмяЭлементаКодирования = "ТипКамня" Тогда
									ТекСтрТовары.ТипКамняГИИСДМДК = ТекХарактеристика.ЗначениеКлассификатораГИИСДМДК;
								ИначеЕсли ТекХарактеристика.ИмяЭлементаКодирования = "ФормаОгранки" Тогда
									ТекСтрТовары.ФормаОгранкиГИИСДМДК = ТекХарактеристика.ЗначениеКлассификатораГИИСДМДК;
									ТекСтрТовары.ФормаОгранки = ТекХарактеристика.ЗначениеДанных;
								ИначеЕсли ТекХарактеристика.ИмяЭлементаКодирования = "ГруппаЧистоты" Тогда
									ТекСтрТовары.ГруппаЧистотыГИИСДМДК = ТекХарактеристика.ЗначениеКлассификатораГИИСДМДК;
									ТекСтрТовары.ГруппаЧистоты = ТекХарактеристика.ЗначениеДанных;
								ИначеЕсли ТекХарактеристика.ИмяЭлементаКодирования = "ГруппаЦвета" Тогда
									ТекСтрТовары.ГруппаЦветаГИИСДМДК = ТекХарактеристика.ЗначениеКлассификатораГИИСДМДК;
									ТекСтрТовары.ГруппаЦвета = ТекХарактеристика.ЗначениеДанных;	
								КонецЕсли;	
							КонецЦикла;	
							
						КонецЕсли;
					КонецЕсли;
					ТекСтрТовары.Количество = ТекУИН.Количество;
					ТекСтрТовары.Вес = ТекУИН.Вес*5/ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса();
					ТекСтрТовары.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех();
				Иначе
					ТекСтрТовары.ИдентификаторЗапроса = ИдентификаторЗапроса;
					ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Ошибка");
					ТекСтрТовары.ОписаниеОшибки = ТекУИН.Ошибка;
					ЕстьОшибки = Истина;
				КонецЕсли;
				СтруктураПоискаЗапросов = Новый Структура;
				СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтрТовары.НомерСтроки);
				СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "Камни");
				СтруктураПоискаЗапросов.Вставить("ИмяМетода", "SendGetBatch");
				ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
				Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
					ТекущийЗапрос.Статус = ТекСтрТовары.СтатусЗапроса;
					ТекущийЗапрос.ОписаниеОшибки = ТекСтрТовары.ОписаниеОшибки;
					ТекущийЗапрос.ИдентификаторЗапроса = ТекСтрТовары.ИдентификаторЗапроса;
				КонецЦикла;
			ИначеЕсли ОбработанныйПакет.ИмяТабЧасти	= "Металл" Или ОбработанныйПакет.ИмяТабЧасти = "Товары" Тогда
				СтруктураОтбора = Новый Структура;
				Если ОбработанныйПакет.ИмяТабЧасти	= "Металл" Тогда
					СтруктураОтбора.Вставить("ИНП", ТекУИН.УИН);
				Иначе
					СтруктураОтбора.Вставить("ИНППреобразованияВЛом", ТекУИН.УИН);
				КонецЕсли;
				ЕстьСтроки = Объект[ОбработанныйПакет.ИмяТабЧасти].НайтиСтроки(СтруктураОтбора);
				Если ЕстьСтроки.Количество() > 0 Тогда
					ТекСтрТовары = ЕстьСтроки[0];
				КонецЕсли;
				Если НовыйСтатус = "Успех" Тогда
					Если ОбработанныйПакет.ИмяТабЧасти = "Товары" Тогда
						Если ТекУИН.ДанныеОМеталле.ОписаниеМеталла.Количество() > 1 Тогда
							Для Каждого ТекущаяСтрокаДанныхОМеталле Из ТекУИН.ДанныеОМеталле.ОписаниеМеталла Цикл
								Если ТекущаяСтрокаДанныхОМеталле.Металл = ТекУИН.ДанныеОМеталле.Металл Тогда
									ТекСтрТовары.ВесВХимЧистотеГИИСДМДК = ТекущаяСтрокаДанныхОМеталле.Вес/ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса();
								КонецЕсли;	
							КонецЦикла;	
						Иначе	
							ТекСтрТовары.ВесВХимЧистотеГИИСДМДК = ТекУИН.ДанныеОМеталле.ОписаниеМеталла[0].Вес/ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса();
						КонецЕсли;
					Иначе	
						Проба = ОписаниеТипаЧисло.ПривестиЗначение(ТекУИН.ДанныеОМеталле.Проба);
						ТекСтрТовары.ТипМеталла = ИнтеграцияГИИСДМДКЮТДСервер.ТипМеталлаКонфигурации(ТекУИН.ДанныеОМеталле.Металл);
						ТекСтрТовары.Проба = Проба / ИнтеграцияГИИСДМДК.КоэффициентПробы();
						ТекСтрТовары.Вес = ТекУИН.Вес/ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса();
						ТекСтрТовары.ВесВЧистоте = ТекУИН.ДанныеОМеталле.ОписаниеМеталла[0].Вес/ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса();
					КонецЕсли;
					ТекСтрТовары.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех();
				Иначе
					ТекСтрТовары.ИдентификаторЗапроса = ИдентификаторЗапроса;
					ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Ошибка");
					ТекСтрТовары.ОписаниеОшибки = ТекУИН.Ошибка;
					ЕстьОшибки = Истина;
				КонецЕсли;
				СтруктураПоискаЗапросов = Новый Структура;
				СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтрТовары.НомерСтроки);
				СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", ОбработанныйПакет.ИмяТабЧасти);
				СтруктураПоискаЗапросов.Вставить("ИмяМетода", "SendGetBatch");
				ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
				Если ЕстьЗапросыДляОбновления.Количество() > 0 Тогда
					Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
						ТекущийЗапрос.Статус = ТекСтрТовары.СтатусЗапроса;
						ТекущийЗапрос.ОписаниеОшибки = ТекСтрТовары.ОписаниеОшибки;
						ТекущийЗапрос.ИдентификаторЗапроса = ТекСтрТовары.ИдентификаторЗапроса;
					КонецЦикла;
				Иначе	
					ТекущийЗапрос = Объект.СписокЗапросов.Добавить();
					ТекущийЗапрос.НомерСтрокиТаблЧасти = ТекСтрТовары.НомерСтроки;
					ТекущийЗапрос.ИмяТабличнойЧасти = ОбработанныйПакет.ИмяТабЧасти;
					ТекущийЗапрос.ИмяМетода = "SendGetBatch";
					ТекущийЗапрос.Статус = ТекСтрТовары.СтатусЗапроса;
					ТекущийЗапрос.ОписаниеОшибки = ТекСтрТовары.ОписаниеОшибки;
					ТекущийЗапрос.ИдентификаторЗапроса = ТекСтрТовары.ИдентификаторЗапроса;
				КонецЕсли;
			КонецЕсли;	
	
		ИначеЕсли ОбработанныйПакет.ИмяМетода = "SendGetBatch" Или ОбработанныйПакет.ИмяМетода = "CheckGetBatch" Тогда
			Если ТекУИН.Свойство("ДанныеОМеталле") и ТекУИН.ДанныеОМеталле.Свойство("ОписаниеМеталла") Тогда
				ТекВесКомплектаВХимЧистоте = ИнтеграцияГИИСДМДКЮТДКлиентСервер.ПолучитьВесИзВходящихДанных(ТекУИН);
				Если ТекВесКомплектаВХимЧистоте > 0 Тогда					
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("ИНП", ТекУИН.УИН);
					ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
					Если ЕстьСтроки.Количество() > 0 Тогда
						ТекСтрТовары = ЕстьСтроки[0];
						ТекСтрТовары.ВесВХимЧистотеГИИСДМДК = ТекВесКомплектаВХимЧистоте;
						ТекСтрТовары.ИмяМетода = ОбработанныйПакет.ИмяМетода;
					Иначе
						Объект.ВесКомплектаВХимЧистоте = ТекВесКомплектаВХимЧистоте;
						Объект.ИмяМетода = ОбработанныйПакет.ИмяМетода;
					КонецЕсли;
					НомерСледующегоПакета = ДополнительныеПараметры.НомерПакета + 1;
					ТекПакет = ДополнительныеПараметры.Пакеты.Получить(НомерСледующегоПакета);
					Пока ТекПакет <> Неопределено Цикл
						Если ТекПакет.ИмяМетода = "SendBatchUnion" Тогда
							Для Каждого ТекСтрРодительскойПартии Из ТекПакет.Спецификации.МассивТоваров[0].МассивРодительскихПартий Цикл
								Если ТекСтрРодительскойПартии.УИН = Число(ТекУИН.УИН) Тогда
									ТекСтрРодительскойПартии.СведенияОСплаве[0].ВесМеталлаВЧистоте = ТекВесКомплектаВХимЧистоте;
									Прервать;
								КонецЕсли;
							КонецЦикла;	
						КонецЕсли;
						НомерСледующегоПакета = НомерСледующегоПакета + 1;
						ТекПакет = ДополнительныеПараметры.Пакеты.Получить(НомерСледующегоПакета);
					КонецЦикла;
					СтруктураПоискаЗапросов = Новый Структура;
					Если ЕстьСтроки.Количество() > 0 Тогда
						СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтрТовары.НомерСтроки);
						СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "Товары");
					Иначе
						СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", 0);
						СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "");
					КонецЕсли;	
					СтруктураПоискаЗапросов.Вставить("ИмяМетода", "SendGetBatch");
					ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
					Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
						ТекущийЗапрос.Статус = ИнтеграцияГИИСДМДК.СтатусУспех();
						ТекущийЗапрос.ОписаниеОшибки = "";
						ТекущийЗапрос.ИдентификаторЗапроса = "";
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
				
		ИначеЕсли (ТипЗнч(НомерСтроки) = Тип("Структура") И НомерСтроки.ИдентификаторЗапроса = Объект.ИдентификаторЗапроса)
			Или НомерСтроки = 0 Или Объект.ИдентификаторЗапроса = НомерСтроки Тогда
			Если НовыйСтатус = "Успех" Тогда
				Объект.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех");
				Объект.ОписаниеОшибки = "";
				Объект.ИНП = ТекУИН.УИН;
				СерияОбъект = Объект.СерияНоменклатурыКомплекта.ПолучитьОбъект();
				СерияОбъект.УИН = Объект.ИНП;
				СерияОбъект.Записать();
			ИначеЕсли НовыйСтатус = "Ошибка" Тогда
				Объект.ИдентификаторЗапроса = ИдентификаторЗапроса;
				Объект.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Ошибка");
				Объект.ОписаниеОшибки = ТекУИН.Ошибка;
				ЕстьОшибки = Истина;
			КонецЕсли;
			Объект.ИмяМетода = ОбработанныйПакет.ИмяМетода;
			
			СтруктураПоискаЗапросов = Новый Структура;
			СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", 0);
			СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "");
			МетодЗапроса = ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.ИмяМетодаПолученияЗапроса(ОбработанныйПакет.ИмяМетода);
			Если МетодЗапроса = "" Тогда
				СтруктураПоискаЗапросов.Вставить("ИмяМетода", ОбработанныйПакет.ИмяМетода);
			Иначе
				СтруктураПоискаЗапросов.Вставить("ИмяМетода", МетодЗапроса);
			КонецЕсли;	
			ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
			Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
				ТекущийЗапрос.Статус = Объект.СтатусЗапроса;
				ТекущийЗапрос.ОписаниеОшибки = Объект.ОписаниеОшибки;
				ТекущийЗапрос.ИдентификаторЗапроса = Объект.ИдентификаторЗапроса;	
			КонецЦикла;
		Иначе	
			СтруктураОтбора = Новый Структура;
			Если ТипЗнч(НомерСтроки) = Тип("Структура") Тогда
				СтруктураОтбора.Вставить("ИдентификаторЗапроса", НомерСтроки.ИдентификаторЗапроса);
			Иначе	
				СтруктураОтбора.Вставить("НомерСтроки", НомерСтроки);
			КонецЕсли;
			ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
			Если ЕстьСтроки.Количество() > 0 Тогда
				ТекСтрТовары = ЕстьСтроки[0];
				Если НовыйСтатус = "Успех" Тогда
					ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех");	
					ТекСтрТовары.ИНП = ТекУИН.УИН;
					СерияОбъект = ТекСтрТовары.СерияНоменклатуры.ПолучитьОбъект();
					СерияОбъект.УИН = ТекСтрТовары.ИНП;
					СерияОбъект.Записать();
					ТекСтрТовары.ОписаниеОшибки = "";
				ИначеЕсли НовыйСтатус = "Ошибка" Тогда
					ТекСтрТовары.ИдентификаторЗапроса = ИдентификаторЗапроса;
					ТекСтрТовары.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Ошибка");
					ТекСтрТовары.ОписаниеОшибки = ТекУИН.Ошибка;
					ЕстьОшибки = Истина;
				КонецЕсли;
				ТекСтрТовары.ИмяМетода = ОбработанныйПакет.ИмяМетода;
				СтруктураПоискаЗапросов = Новый Структура;
				СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтрТовары.НомерСтроки);
				СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "Товары");
				МетодЗапроса = ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.ИмяМетодаПолученияЗапроса(ОбработанныйПакет.ИмяМетода);
				Если МетодЗапроса = "" Тогда
					СтруктураПоискаЗапросов.Вставить("ИмяМетода", ОбработанныйПакет.ИмяМетода);
				Иначе
					СтруктураПоискаЗапросов.Вставить("ИмяМетода", МетодЗапроса);
				КонецЕсли;
				ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
				Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
					ТекущийЗапрос.Статус = ТекСтрТовары.СтатусЗапроса;
					ТекущийЗапрос.ОписаниеОшибки = ТекСтрТовары.ОписаниеОшибки;
					ТекущийЗапрос.ИдентификаторЗапроса = ТекСтрТовары.ИдентификаторЗапроса;	
				КонецЦикла;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	
	ПараметрыЗаписи = Новый Структура;
    ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	Записать(ПараметрыЗаписи);
		
	Если ЕстьОшибки = Ложь Тогда
		Если НомерСтроки <> 0 Тогда
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("СтатусЗапроса", ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех"));
			Если Объект.ВидОперации = Перечисления.ВидыОперацииКомплектацииЦенностей.КомплектацияИзделие
				Или Объект.ВидОперации = Перечисления.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни
				Или Объект.ВидОперации = Перечисления.ВидыОперацииКомплектацииЦенностей.КомплектацияЛом
				Или Объект.ВидОперации = Перечисления.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом Тогда
				СтруктураОтбора.Вставить("ИмяМетода", "SendBatchUnion");
			КонецЕСли;	
			ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);	
			Если ЕстьСтроки.Количество() = Объект.Товары.Количество() Тогда	
				Объект.СтатусЗапроса = ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех");
				ПараметрыЗаписи = Новый Структура;
				ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
				Записать(ПараметрыЗаписи);
			КонецЕсли;	
		КонецЕсли;	
	КонецЕсли;
	
	ЗаполнитьСтатусОпераций();
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьНовыйСтатус(Данные, НовыйСтатус, ДополнительныеПараметры, messageId)
	ЕстьОшибки = ЗаписатьНовыйСтатусНаСервере(Данные, НовыйСтатус, ДополнительныеПараметры, messageId);
	Если ЕстьОшибки Тогда
		//УстановитьВидимостьПредупреждения(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИнформациюПоОжидающимОтветаВСтроке(НомерСтроки, messageId, ИмяМетода)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НомерСтроки", НомерСтроки);
	ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	Если ЕстьСтроки.Количество() > 0 Тогда
		ТекСтр = ЕстьСтроки[0];
		ТекСтр.ИдентификаторЗапроса = messageId;
		ТекСтр.СтатусЗапроса = ИнтеграцияГИИСДМДККлиент.СтатусОжиданиеОтвета();
		ТекСтр.ОписаниеОшибки = "";
		ТекСтр.ИмяМетода = ИмяМетода;
		СтруктураПоискаЗапросов = Новый Структура;
		СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", ТекСтр.НомерСтроки);
		СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "Товары");
		МетодЗапроса = ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.ИмяМетодаПолученияЗапроса(ИмяМетода);
		Если МетодЗапроса = "" Тогда
			СтруктураПоискаЗапросов.Вставить("ИмяМетода", ИмяМетода);
		Иначе
			СтруктураПоискаЗапросов.Вставить("ИмяМетода", МетодЗапроса);
		КонецЕсли;
		ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
		Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
			ТекущийЗапрос.Статус = ТекСтр.СтатусЗапроса;
			ТекущийЗапрос.ОписаниеОшибки = ТекСтр.ОписаниеОшибки;
			ТекущийЗапрос.ИдентификаторЗапроса = ТекСтр.ИдентификаторЗапроса;	
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИнформациюПоОжидающимОтвета(НомерСтроки, messageId, ДополнительныеПараметры)
	
	ИмяМетода = ДополнительныеПараметры.Пакеты[ДополнительныеПараметры.НомерПакета].ИмяМетода;
	
	ИмяМетодаРезультат = ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.ИмяМетодаПолученияРезультата(ИмяМетода);
	ИмяМетода = ?(ИмяМетодаРезультат = "", ИмяМетода, ИмяМетодаРезультат);
	Если НомерСтроки = 0 
		Или (ТипЗнч(НомерСтроки) = Тип("Массив") И НомерСтроки.Количество() = 1 И НомерСтроки[0] = 0) Тогда
		Объект.ИдентификаторЗапроса = messageId;
		Объект.СтатусЗапроса = ИнтеграцияГИИСДМДККлиент.СтатусОжиданиеОтвета();
		Объект.ОписаниеОшибки = "";
		Объект.ИмяМетода = ИмяМетода;
		СтруктураПоискаЗапросов = Новый Структура;
		СтруктураПоискаЗапросов.Вставить("НомерСтрокиТаблЧасти", 0);
		СтруктураПоискаЗапросов.Вставить("ИмяТабличнойЧасти", "");
		МетодЗапроса = ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.ИмяМетодаПолученияЗапроса(ИмяМетода);
		Если МетодЗапроса = "" Тогда
			СтруктураПоискаЗапросов.Вставить("ИмяМетода", ИмяМетода);
		Иначе
			СтруктураПоискаЗапросов.Вставить("ИмяМетода", МетодЗапроса);
		КонецЕсли;
		ЕстьЗапросыДляОбновления = Объект.СписокЗапросов.НайтиСтроки(СтруктураПоискаЗапросов);
		Для Каждого ТекущийЗапрос Из ЕстьЗапросыДляОбновления Цикл
			ТекущийЗапрос.Статус = Объект.СтатусЗапроса;
			ТекущийЗапрос.ОписаниеОшибки = Объект.ОписаниеОшибки;
			ТекущийЗапрос.ИдентификаторЗапроса = Объект.ИдентификаторЗапроса;	
		КонецЦикла;
	Иначе	
		ОбработанныйПакет = ДополнительныеПараметры.Пакеты.Получить(ДополнительныеПараметры.НомерПакета);
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ИдентификаторЗапроса", messageId);
		Если (ОбработанныйПакет.ИмяМетода = "SendGetBatch"
			Или ОбработанныйПакет.ИмяМетода = "CheckGetBatch") И ОбработанныйПакет.Свойство("ИмяТабЧасти") Тогда
			ЕстьСтроки = Объект[ОбработанныйПакет.ИмяТабЧасти].НайтиСтроки(СтруктураОтбора);
		Иначе	
			ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
		КонецЕсли;
		Если ЕстьСтроки.Количество() = 0 Тогда
			
			Если ТипЗнч(НомерСтроки) = Тип("Массив") Тогда
				Для Каждого ТекСтр Из НомерСтроки Цикл
					ЗаполнитьИнформациюПоОжидающимОтветаВСтроке(ТекСтр, messageId, ИмяМетода);	
				КонецЦикла;	
			Иначе
				ЗаполнитьИнформациюПоОжидающимОтветаВСтроке(НомерСтроки, messageId, ИмяМетода);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
    ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	Записать(ПараметрыЗаписи);
		
	ЗаполнитьСтатусОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаКомплектацийВГИИСЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
 	Если Результат.Успех = Истина Тогда
		
		ЗаписатьНовыйСтатус(Результат.Данные.Данные, "Успех", ДополнительныеПараметры, Результат.messageId);
		ЗаписатьНовыйСтатус(Результат.Данные.Ошибки, "Ошибка", ДополнительныеПараметры, Результат.messageId);
		
	ИначеЕсли Результат.Успех = Ложь Тогда
		
		//УстановитьВидимостьПредупреждения(Истина);
		
		Если ЗначениеЗаполнено(Результат.messageId) Тогда
			ЗаполнитьИнформациюПоОжидающимОтвета(ДополнительныеПараметры.НомераСтрок, Результат.messageId, ДополнительныеПараметры);
		КонецЕсли;
			
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура;
    ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
	Записать(ПараметрыЗаписи);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("СтатусЗапроса", ПредопределенноеЗначение("Перечисление.СтатусОтветаГИИСДМДК.Успех"));
	ЕстьСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);				
				
	Если ДополнительныеПараметры.Свойство("НомерПакета") И Результат.Успех = Истина И Результат.Данные.Данные.Количество() > 0 Тогда
			
		ОтправитьИнформациюОКомплектации(ДополнительныеПараметры.Сертификат, ДополнительныеПараметры.Пакеты,
				ДополнительныеПараметры.НомерПакета + 1, ДополнительныеПараметры.НомерОперации, ДополнительныеПараметры.ПереходитьКСледующейОперации);
				
		
	Иначе
		
		ИдетОтправкаДанных = Ложь;
		
		ПараметрыЗаписи = Новый Структура;
    	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		Записать(ПараметрыЗаписи);
	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДанныеДокументаСОперациейКомплектации(ДанныеШапкиДокументов, ДанныеПоТоварам, ДанныеПоДопПробе,
		ПоискТоваровПоВнутреннемуШК, ПоискТоваровПоПаспорту)
	
	МассивТоваров = Новый Массив;
	
	ТаблицаДополнительныхПроб = Новый ТаблицаЗначений;
	ТаблицаДополнительныхПроб.Колонки.Добавить("Металл");
	ТаблицаДополнительныхПроб.Колонки.Добавить("ВесМеталлаВЧистоте");
	
	Спецификация = Новый Структура;		
	Спецификация.Вставить("Номер", ДанныеШапкиДокументов.Номер);
	Спецификация.Вставить("Дата", ДанныеШапкиДокументов.Дата);
	
	ПараметрыНаименования = Новый Структура;
	ПараметрыНаименования.Вставить("ВидНоменклатуры", ДанныеШапкиДокументов.ВидНоменклатуры);
	ПараметрыНаименования.Вставить("Камень", ДанныеШапкиДокументов.Камень);
	ПараметрыНаименования.Вставить("Наименование", ДанныеШапкиДокументов.Наименование);
	ПараметрыНаименования.Вставить("НаименованиеПолное", ДанныеШапкиДокументов.НаименованиеПолное);
	ПараметрыНаименования.Вставить("НаименованиеГИИСДМДК", ДанныеШапкиДокументов.НаименованиеГИИСДМДК);
	ПараметрыНаименования.Вставить("ВнутреннийШтрихкод", ДанныеШапкиДокументов.НаименованиеСерии);
	ПараметрыНаименования.Вставить("ШтрихкодПоставщика", ДанныеШапкиДокументов.НомерПаспорта);
	ПараметрыНаименования.Вставить("ПоискТоваровПоВнутреннемуШК", ПоискТоваровПоВнутреннемуШК);
	ПараметрыНаименования.Вставить("ПоискТоваровПоПаспорту", ПоискТоваровПоПаспорту);
	
	Наименования = ИнтеграцияГИИСДМДКЮТДСервер.НаименованияМатериала(ПараметрыНаименования);
	
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("НомерСтроки", 0);
	СтруктураСтроки.Вставить("Наименование", Наименования.Наименование);	
	СтруктураСтроки.Вставить("ТипПартии", ДанныеШапкиДокументов.ТипПартии);
	СтруктураСтроки.Вставить("ВидПартии", ДанныеШапкиДокументов.ВидПартии);
	СтруктураСтроки.Вставить("ЭтапОбработки", ДанныеШапкиДокументов.ЭтапОбработки);
	СтруктураСтроки.Вставить("СтадияОбработки", ДанныеШапкиДокументов.СтадияОбработки);
	СтруктураСтроки.Вставить("ОКПД2", ДанныеШапкиДокументов.ОКПД2);
	
	СтруктураОрганизации = Новый Структура;
	Если ДанныеШапкиДокументов.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		СтруктураОрганизации.Вставить("ТипКонтрагента", "legal");
		Если ДанныеШапкиДокументов.ОрганизацияКПП = "" Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен КПП организации ");
		КонецЕсли;	
		Если ДанныеШапкиДокументов.ОрганизацияОГРН = "" Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен ОГРН организации");
		КонецЕсли;
	ИначеЕсли ДанныеШапкиДокументов.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		СтруктураОрганизации.Вставить("ТипКонтрагента", "physical");
		Если ДанныеШапкиДокументов.ОрганизацияОГРН = "" Тогда
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнен ОГРН организации");
		КонецЕсли;
	КонецЕсли;
	СтруктураОрганизации.Вставить("ИНН", ДанныеШапкиДокументов.ОрганизацияИНН);
	Если ДанныеШапкиДокументов.ОрганизацияЮрФизЛицо = Перечисления.ЮрФизЛицо.ЮрЛицо Тогда
		СтруктураОрганизации.Вставить("КПП", ДанныеШапкиДокументов.ОрганизацияКПП);
	Иначе
		СтруктураОрганизации.Вставить("КПП", "");
	КонецЕсли;	
	СтруктураОрганизации.Вставить("ОГРН", ДанныеШапкиДокументов.ОрганизацияОГРН);
	СтруктураСтроки.Вставить("Собственник", СтруктураОрганизации);
	
	СтруктураСтроки.Вставить("Вес", ДанныеШапкиДокументов.Вес * ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса());
	СтруктураСтроки.Вставить("ЕдиницаИзмерения", ДанныеШапкиДокументов.ЕдиницаИзмерения);	
	
	СтруктураСтроки.Вставить("Количество", 0);
	СтруктураСтроки.Вставить("ТипПартии", "METAL");
	СтруктураСтроки.Вставить("ВидПартии", "SCRAP_METAL");
	СтруктураСтроки.Вставить("ЭтапОбработки", "DOMESTIC_TURNOVER");
	СтруктураСтроки.Вставить("СтадияОбработки", "STORED");
	СтруктураСтроки.Вставить("ОКПД2", ДанныеШапкиДокументов.ОКПД2);
			
	СтруктураСтроки.Вставить("Вес", ДанныеШапкиДокументов.Вес * ИнтеграцияГИИСДМДКСлужебныйКлиентСервер.КоэффициентВеса());
	СтруктураСтроки.Вставить("ЕдиницаИзмерения", "GRM");
	
	СтруктураПартии = Новый Структура;
	СтруктураПартии.Вставить("ВидОсновногоМеталла", ИнтеграцияГИИСДМДКЮТДСервер.ПолучитьМеталлПартии(ДанныеШапкиДокументов.Металл));
	Если СтруктураПартии.ВидОсновногоМеталла = "No" Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не определен вид металла для серии " + ДанныеШапкиДокументов.НаименованиеСерии);
	КонецЕсли;
	СтруктураПартии.Вставить("Металл", СтруктураПартии.ВидОсновногоМеталла);
	СтруктураПартии.Вставить("Проба", ДанныеШапкиДокументов.Проба*100);
	Если СтруктураПартии.Проба = 0 Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не заполнена проба для серии " + ДанныеШапкиДокументов.НаименованиеСерии);
	КонецЕсли;
	
	СтруктураПартии.Вставить("ПробаПодтвержденная", СтруктураПартии.Проба);
	МассивСплавов = Новый Массив;
	СтруктураМеталла = Новый Структура;
	СтруктураМеталла.Вставить("Металл", СтруктураПартии.ВидОсновногоМеталла);
	СтруктураМеталла.Вставить("ВесМеталлаВЧистоте", ДанныеШапкиДокументов.ВесМеталлаВЧистоте * ИнтеграцияГИИСДМДК.КоэффициентВеса());
	МассивСплавов.Добавить(СтруктураМеталла);
	
	ТаблицаДополнительныхПроб.Очистить();
	Для Каждого СтрДопМатериал Из ДанныеПоДопПробе Цикл
		Если Не ЗначениеЗаполнено(СтрДопМатериал.МатериалМеталл) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрДопМатериал.Вес = 0 
			или СтрДопМатериал.МатериалМеталл.НеДрагоценныйМеталл  Тогда
			Продолжить;
		КонецЕсли;	
		Если СтрДопМатериал.МатериалМеталл.ПробаЧистоты = 0 Тогда
			ТекВесМеталлаВЧистоте = 0;
		Иначе	
			ТекВесМеталлаВЧистоте = Окр(СтрДопМатериал.Вес 
			* СтрДопМатериал.МатериалПроба/СтрДопМатериал.МатериалМеталл.ПробаЧистоты, 3);
		КонецЕсли;
		Если ТекВесМеталлаВЧистоте = 0 Тогда
			Продолжить;
		КонецЕсли;	
		МеталлДопПробы = ИнтеграцияГИИСДМДКЮТДСервер.ПолучитьМеталлПартии(СтрДопМатериал.МатериалМеталл);
		Если СтруктураМеталла.Металл = МеталлДопПробы И СтрДопМатериал.МатериалПроба*100 = СтруктураПартии.Проба Тогда
			Продолжить;	
		ИначеЕсли СтруктураМеталла.Металл = МеталлДопПробы Тогда
			СтруктураМеталла.ВесМеталлаВЧистоте = СтруктураМеталла.ВесМеталлаВЧистоте + ТекВесМеталлаВЧистоте*100000;
		Иначе	
			СтруктураДопМеталла = ТаблицаДополнительныхПроб.Добавить();
			СтруктураДопМеталла.Металл = МеталлДопПробы;				
			СтруктураДопМеталла.ВесМеталлаВЧистоте = ТекВесМеталлаВЧистоте*100000;
		КонецЕсли;
	КонецЦикла;	
	Если ТаблицаДополнительныхПроб.Количество() > 0 Тогда
		ТаблицаДополнительныхПроб.Свернуть("Металл", "ВесМеталлаВЧистоте");
		Для Каждого СтрокаДопПробы Из ТаблицаДополнительныхПроб Цикл
			СтруктураДопМеталла = Новый Структура;
			СтруктураДопМеталла.Вставить("Металл", СтрокаДопПробы.Металл);				
			СтруктураДопМеталла.Вставить("ВесМеталлаВЧистоте",  СтрокаДопПробы.ВесМеталлаВЧистоте);
			МассивСплавов.Добавить(СтруктураДопМеталла);	
		КонецЦикла;	
	КонецЕсли;
	
	СтруктураПартии.Вставить("СведенияОСплаве", МассивСплавов);
	СтруктураСтроки.Вставить("СведенияОМеталле",  СтруктураПартии);
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СсылкаНаДокумент", ДанныеШапкиДокументов.Ссылка);
	ЕстьСтрокиДокумента = ДанныеПоТоварам.НайтиСтроки(СтруктураПоиска);
	МассивРодительскихПартий = Новый Массив;
	Для Каждого Стр Из ЕстьСтрокиДокумента Цикл
		СтруктураПартии = Новый Структура;	
		СтруктураПартии.Вставить("НомерСтроки", Стр.НомерСтроки);
		СтруктураПартии.Вставить("УИН", Стр.ИНППреобразованияВЛом);
		СтруктураПартии.Вставить("Количество", Стр.Количество);
		ТекВес = Стр.Вес;
		ТекВесВЧистоте = Стр.ВесМеталлаВЧистоте;
		
		СтруктураПартии.Вставить("Вес", ТекВес * ИнтеграцияГИИСДМДК.КоэффициентВеса());
		МассивСплавов = Новый Массив;
		СтруктураМеталла = Новый Структура;
		СтруктураМеталла.Вставить("Металл", ИнтеграцияГИИСДМДКЮТДСервер.ПолучитьМеталлПартии(Стр.Металл));
		//ВесМеталлаВЧистоте = ?(ТекВесВЧистоте = 0, Окр(ТекВес * Стр.Проба / 999.9, 3), ТекВесВЧистоте);
		СтруктураМеталла.Вставить("ВесМеталлаВЧистоте", ТекВесВЧистоте * ИнтеграцияГИИСДМДК.КоэффициентВеса());
		МассивСплавов.Добавить(СтруктураМеталла);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("НомерСтроки", Стр.НомерСтроки);
		ЕстьСтрокиСХарактеристикой = ДанныеПоДопПробе.НайтиСтроки(СтруктураПоиска);
		ТаблицаДополнительныхПроб.Очистить();
		Для Каждого СтрДопМатериал Из ЕстьСтрокиСХарактеристикой Цикл
			Если Не ЗначениеЗаполнено(СтрДопМатериал.МатериалМеталл) Тогда
				Продолжить;
			КонецЕсли;
			Если СтрДопМатериал.Вес = 0 
				или СтрДопМатериал.МатериалМеталл.НеДрагоценныйМеталл  Тогда
				Продолжить;
			КонецЕсли;	
			Если СтрДопМатериал.МатериалМеталл.ПробаЧистоты = 0 Тогда
				ТекВесМеталлаВЧистоте = 0;
			Иначе	
				ТекВесМеталлаВЧистоте = Окр(СтрДопМатериал.Вес 
				* СтрДопМатериал.МатериалПроба/СтрДопМатериал.МатериалМеталл.ПробаЧистоты, 3);
			КонецЕсли;
			Если ТекВесМеталлаВЧистоте = 0 Тогда
				Продолжить;
			КонецЕсли;	
			МеталлДопПробы = ИнтеграцияГИИСДМДКЮТДСервер.ПолучитьМеталлПартии(СтрДопМатериал.МатериалМеталл);
			Если СтруктураМеталла.Металл = МеталлДопПробы Тогда
				СтруктураМеталла.ВесМеталлаВЧистоте = СтруктураМеталла.ВесМеталлаВЧистоте;// + ТекВесМеталлаВЧистоте*100000;
			Иначе	
				СтруктураДопМеталла = ТаблицаДополнительныхПроб.Добавить();
				СтруктураДопМеталла.Металл = МеталлДопПробы;				
				СтруктураДопМеталла.ВесМеталлаВЧистоте = ТекВесМеталлаВЧистоте*100000;
			КонецЕсли;
		КонецЦикла;	
		Если ТаблицаДополнительныхПроб.Количество() > 0 Тогда
			ТаблицаДополнительныхПроб.Свернуть("Металл", "ВесМеталлаВЧистоте");
			Для Каждого СтрокаДопПробы Из ТаблицаДополнительныхПроб Цикл
				СтруктураДопМеталла = Новый Структура;
				СтруктураДопМеталла.Вставить("Металл", СтрокаДопПробы.Металл);				
				СтруктураДопМеталла.Вставить("ВесМеталлаВЧистоте",  СтрокаДопПробы.ВесМеталлаВЧистоте);
				МассивСплавов.Добавить(СтруктураДопМеталла);	
			КонецЦикла;	
		КонецЕсли;
		
		СтруктураПартии.Вставить("СведенияОСплаве", МассивСплавов);

		МассивРодительскихПартий.Добавить(СтруктураПартии);				   
	КонецЦикла;
	СтруктураСтроки.Вставить("МассивРодительскихПартий", МассивРодительскихПартий);
	
	МассивТоваров.Добавить(СтруктураСтроки);
	
	Спецификация.Вставить("МассивТоваров", МассивТоваров);

	Возврат Спецификация;
	
КонецФункции

&НаСервере
Функция ПодготовитьДанныеКомплектацииТоваров(НомерОперации)
	
	ПоискТоваровПоВнутреннемуШК = Константы.ПоискТоваровПоВнутреннемуШК.Получить();
	ПоискТоваровПоПаспорту = Константы.ПоискТоваровПоПаспорту.Получить();

	Запрос = Новый Запрос( "ВЫБРАТЬ
	                       |	КомплектацияТоваров.Комплект КАК Номенклатура,
	                       |	КомплектацияТоваров.Комплект.ВидНоменклатуры КАК ВидНоменклатуры,
	                       |	КомплектацияТоваров.Комплект.Камень КАК Камень,
	                       |	КомплектацияТоваров.Комплект.Наименование КАК Наименование,
	                       |	КомплектацияТоваров.Комплект.НаименованиеПолное КАК НаименованиеПолное,
	                       |	КомплектацияТоваров.Комплект.НаименованиеГИИСДМДК КАК НаименованиеГИИСДМДК,
	                       |	КомплектацияТоваров.СерияНоменклатурыКомплекта.Наименование КАК НаименованиеСерии,
	                       |	КомплектацияТоваров.СерияНоменклатурыКомплекта.НомерПаспорта КАК НомерПаспорта,
	                       |	КомплектацияТоваров.Номер КАК Номер,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""GEMSTONE""
	                       |		ИНАЧЕ ""METAL""
	                       |	КОНЕЦ КАК ТипПартии,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""TREATED_GEMSTONES""
	                       |		ИНАЧЕ ""SCRAP_METAL""
	                       |	КОНЕЦ КАК ВидПартии,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""SORTING_GEMS""
	                       |		ИНАЧЕ ""DOMESTIC_TURNOVER""
	                       |	КОНЕЦ КАК ЭтапОбработки,
	                       |	""STORED"" КАК СтадияОбработки,
	                       |	КомплектацияТоваров.Комплект.ОКПД2.Код КАК ОКПД2,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияЛом)
	                       |			ТОГДА ПартииТоваровНаСкладах.Количество
	                       |		ИНАЧЕ КомплектацияТоваров.КоличествоКомплекта
	                       |	КОНЕЦ КАК Количество,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияЛом)
	                       |			ТОГДА ПартииТоваровНаСкладах.Вес
	                       |		ИНАЧЕ КомплектацияТоваров.ВесКомплекта
	                       |	КОНЕЦ КАК Вес,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.Комплект.Камень.УчетВКаратах
	                       |			ТОГДА ""CTM""
	                       |		ИНАЧЕ ""GRM""
	                       |	КОНЕЦ КАК ЕдиницаИзмерения,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ИСТИНА
	                       |		ИНАЧЕ ЛОЖЬ
	                       |	КОНЕЦ КАК ЭтоКамни,
	                       |	КомплектацияТоваров.Ссылка КАК Ссылка,
	                       |	КомплектацияТоваров.Комплект.Проба.Металл КАК Металл,
	                       |	КомплектацияТоваров.Комплект.Проба.Проба КАК Проба,
	                       |	КомплектацияТоваров.Дата КАК Дата,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.Комплект.Камень = ЗНАЧЕНИЕ(Справочник.ДрагоценныеКамни.ПустаяСсылка)
	                       |			ТОГДА ЛОЖЬ
	                       |		ИНАЧЕ КомплектацияТоваров.Комплект.Камень.УчетВКаратах
	                       |	КОНЕЦ КАК УчетВКаратах,
	                       |	КомплектацияТоваров.ВидОперации КАК ВидОперации,
	                       |	КомплектацияТоваров.СерияНоменклатурыКомплекта.УИН КАК УИН,
	                       |	КомплектацияТоваров.СтатусЗапроса КАК СтатусЗапроса,
	                       |	КомплектацияТоваров.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	ВЫБОР
	                       |		КОГДА КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияЛом)
	                       |			ТОГДА ПартииТоваровНаСкладах.ВесМеталлаВЧистоте
	                       |		КОГДА КомплектацияТоваров.СерияНоменклатурыКомплекта.ВесМеталлаВЧистотеГИИСДМДК > 0
	                       |			ТОГДА КомплектацияТоваров.СерияНоменклатурыКомплекта.ВесМеталлаВЧистотеГИИСДМДК
	                       |		ИНАЧЕ КомплектацияТоваров.СерияНоменклатурыКомплекта.ВесМеталлаВЧистоте
	                       |	КОНЕЦ КАК ВесМеталлаВЧистоте,
	                       |	КомплектацияТоваров.Организация.ЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
	                       |	КомплектацияТоваров.Организация.ИНН КАК ОрганизацияИНН,
	                       |	КомплектацияТоваров.Организация.КПП КАК ОрганизацияКПП,
	                       |	КомплектацияТоваров.Организация.ОГРН КАК ОрганизацияОГРН,
	                       |	КомплектацияТоваров.ИмяМетода КАК ИмяМетода
	                       |ПОМЕСТИТЬ ШапкаДокумента
	                       |ИЗ
	                       |	Документ.КомплектацияТоваров КАК КомплектацияТоваров
	                       |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
	                       |		ПО КомплектацияТоваров.Ссылка = ПартииТоваровНаСкладах.Регистратор
	                       |			И КомплектацияТоваров.СерияНоменклатурыКомплекта = ПартииТоваровНаСкладах.СерияНоменклатуры
	                       |			И (КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |				ИЛИ КомплектацияТоваров.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияЛом))
	                       |ГДЕ
	                       |	КомплектацияТоваров.Ссылка = &Ссылка
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	                       |	ВЫРАЗИТЬ(Товары.СерияНоменклатуры КАК Справочник.СерииНоменклатуры) КАК СерияНоменклатуры,
	                       |	Товары.Количество КАК Количество,
	                       |	Товары.Вес КАК Вес,
	                       |	ВЫРАЗИТЬ(&Ссылка КАК Документ.КомплектацияТоваров) КАК СсылкаНаДокумент,
	                       |	Товары.НомерСтроки КАК НомерСтроки,
	                       |	Товары.СтатусЗапроса КАК СтатусЗапроса,
	                       |	Товары.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	ВЫБОР
	                       |		КОГДА Товары.ВесВХимЧистотеГИИСДМДК > 0
	                       |			ТОГДА Товары.ВесВХимЧистотеГИИСДМДК
	                       |		ИНАЧЕ Товары.ВесВХимЧистоте
	                       |	КОНЕЦ КАК ВесВХимЧистоте,
	                       |	Товары.ИНП КАК ИНП,
	                       |	Товары.ИНППреобразованияВЛом КАК ИНППреобразованияВЛом,
	                       |	Товары.ИмяМетода КАК ИмяМетода,
	                       |	ВЫРАЗИТЬ(Товары.ХарактеристикаНоменклатуры КАК Справочник.ХарактеристикиНоменклатуры) КАК ХарактеристикаНоменклатуры,
	                       |	Товары.ВесВХимЧистотеГИИСДМДК КАК ВесВХимЧистотеГИИСДМДК
	                       |ПОМЕСТИТЬ Товары
	                       |ИЗ
	                       |	&Товары КАК Товары
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	КомплектацияТоваровМеталл.ИНП КАК ИНП,
	                       |	КомплектацияТоваровМеталл.ТипМеталла КАК ТипМеталла,
	                       |	КомплектацияТоваровМеталл.Вес КАК Вес,
	                       |	КомплектацияТоваровМеталл.ВесВЧистоте КАК ВесВЧистоте,
	                       |	КомплектацияТоваровМеталл.Ссылка КАК Ссылка,
	                       |	КомплектацияТоваровМеталл.НомерСтроки КАК НомерСтроки,
	                       |	КомплектацияТоваровМеталл.Проба КАК Проба,
	                       |	КомплектацияТоваровМеталл.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	КомплектацияТоваровМеталл.ОписаниеОшибки КАК ОписаниеОшибки,
	                       |	КомплектацияТоваровМеталл.СтатусЗапроса КАК Статус,
	                       |	КомплектацияТоваровМеталл.ИНПИзделия КАК ИНПИзделия
	                       |ПОМЕСТИТЬ Металлы
	                       |ИЗ
	                       |	Документ.КомплектацияТоваров.Металл КАК КомплектацияТоваровМеталл
	                       |ГДЕ
	                       |	КомплектацияТоваровМеталл.Ссылка = &Ссылка
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	Товары.Количество КАК Количество,
	                       |	Товары.Вес КАК Вес,
	                       |	Товары.СсылкаНаДокумент КАК СсылкаНаДокумент,
	                       |	Товары.НомерСтроки КАК НомерСтроки,
	                       |	ВЫБОР
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом)
	                       |			ТОГДА Товары.ИНППреобразованияВЛом
	                       |		ИНАЧЕ Товары.СерияНоменклатуры.УИН
	                       |	КОНЕЦ КАК УИН,
	                       |	ВЫБОР
	                       |		КОГДА Товары.Номенклатура.Камень = ЗНАЧЕНИЕ(Справочник.ДрагоценныеКамни.ПустаяСсылка)
	                       |			ТОГДА ЛОЖЬ
	                       |		ИНАЧЕ Товары.Номенклатура.Камень.УчетВКаратах
	                       |	КОНЕЦ КАК УчетВКаратах,
	                       |	Товары.Номенклатура.Проба.Проба КАК Проба,
	                       |	Товары.Номенклатура.Проба.Металл КАК Металл,
	                       |	Товары.Номенклатура КАК Номенклатура,
	                       |	Товары.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	                       |	Товары.Номенклатура.Камень КАК Камень,
	                       |	Товары.Номенклатура.Наименование КАК Наименование,
	                       |	Товары.Номенклатура.НаименованиеПолное КАК НаименованиеПолное,
	                       |	Товары.Номенклатура.НаименованиеГИИСДМДК КАК НаименованиеГИИСДМДК,
	                       |	Товары.СерияНоменклатуры.Наименование КАК НаименованиеСерии,
	                       |	Товары.СерияНоменклатуры.НомерПаспорта КАК НомерПаспорта,
	                       |	Товары.СсылкаНаДокумент.Номер КАК Номер,
	                       |	ВЫБОР
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""GEMSTONE""
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом)
	                       |				И Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.ЮвелирноеИзделие)
	                       |			ТОГДА ""PRODUCT""
	                       |		ИНАЧЕ ""METAL""
	                       |	КОНЕЦ КАК ТипПартии,
	                       |	ВЫБОР
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""TREATED_GEMSTONES""
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом)
	                       |				И Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.ЮвелирноеИзделие)
	                       |			ТОГДА ""JEWERLY""
	                       |		ИНАЧЕ ""SCRAP_METAL""
	                       |	КОНЕЦ КАК ВидПартии,
	                       |	ВЫБОР
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ""SORTING_GEMS""
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом)
	                       |				И Товары.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.ЮвелирноеИзделие)
	                       |			ТОГДА ""BUYING_UP""
	                       |		ИНАЧЕ ""DOMESTIC_TURNOVER""
	                       |	КОНЕЦ КАК ЭтапОбработки,
	                       |	""STORED"" КАК СтадияОбработки,
	                       |	Товары.Номенклатура.ОКПД2.Код КАК ОКПД2,
	                       |	ВЫБОР
	                       |		КОГДА Товары.Номенклатура.Камень.УчетВКаратах
	                       |			ТОГДА ""CTM""
	                       |		ИНАЧЕ ""GRM""
	                       |	КОНЕЦ КАК ЕдиницаИзмерения,
	                       |	ВЫБОР
	                       |		КОГДА Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.КомплектацияКамни)
	                       |				ИЛИ Товары.СсылкаНаДокумент.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.РазукомплектацияКамни)
	                       |			ТОГДА ИСТИНА
	                       |		ИНАЧЕ ЛОЖЬ
	                       |	КОНЕЦ КАК ЭтоКамни,
	                       |	Товары.СсылкаНаДокумент.Дата КАК Дата,
	                       |	Товары.СсылкаНаДокумент.ВидОперации КАК ВидОперации,
	                       |	Товары.СтатусЗапроса КАК СтатусЗапроса,
	                       |	Товары.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	ВЫБОР
	                       |		КОГДА Товары.ВесВХимЧистотеГИИСДМДК > 0
	                       |			ТОГДА Товары.ВесВХимЧистотеГИИСДМДК
	                       |		КОГДА ПартииТоваровНаСкладах.ВесМеталлаВЧистоте > 0
	                       |			ТОГДА ПартииТоваровНаСкладах.ВесМеталлаВЧистоте
	                       |		ИНАЧЕ Товары.ВесВХимЧистоте
	                       |	КОНЕЦ КАК ВесМеталлаВЧистоте,
	                       |	ВЫБОР
	                       |		КОГДА Товары.ИНП = """"
	                       |			ТОГДА Товары.СерияНоменклатуры.УИН
	                       |		ИНАЧЕ Товары.ИНП
	                       |	КОНЕЦ КАК ИНП,
	                       |	Товары.ИмяМетода КАК ИмяМетода,
	                       |	Товары.СсылкаНаДокумент.Организация.ЮрФизЛицо КАК СсылкаНаДокументОрганизацияЮрФизЛицо,
	                       |	Товары.СсылкаНаДокумент.Организация.ИНН КАК СсылкаНаДокументОрганизацияИНН,
	                       |	Товары.СсылкаНаДокумент.Организация.КПП КАК СсылкаНаДокументОрганизацияКПП,
	                       |	Товары.СсылкаНаДокумент.Организация.ОГРН КАК СсылкаНаДокументОрганизацияОГРН,
	                       |	Товары.ИНППреобразованияВЛом КАК ИНППреобразованияВЛом,
	                       |	Товары.СерияНоменклатуры КАК СерияНоменклатуры
	                       |ПОМЕСТИТЬ СтрокиДокумента
	                       |ИЗ
	                       |	Товары КАК Товары
	                       |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах КАК ПартииТоваровНаСкладах
	                       |		ПО Товары.СсылкаНаДокумент = ПартииТоваровНаСкладах.Регистратор
	                       |			И Товары.Номенклатура = ПартииТоваровНаСкладах.Номенклатура
	                       |			И Товары.СерияНоменклатуры = ПартииТоваровНаСкладах.СерияНоменклатуры
	                       |			И Товары.ХарактеристикаНоменклатуры = ПартииТоваровНаСкладах.ХарактеристикаНоменклатуры
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	СтрокиДокумента.НомерСтроки КАК НомерСтроки,
	                       |	СтрокиДокумента.ИНП КАК УИН,
	                       |	СтрокиДокумента.СтатусЗапроса КАК СтатусЗапроса,
	                       |	СтрокиДокумента.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	СтрокиДокумента.ИмяМетода КАК ИмяМетода
	                       |ИЗ
	                       |	СтрокиДокумента КАК СтрокиДокумента
	                       |		ЛЕВОЕ СОЕДИНЕНИЕ Металлы КАК Металлы
	                       |		ПО СтрокиДокумента.ИНП = Металлы.ИНПИзделия
	                       |ГДЕ
	                       |	СтрокиДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацииКомплектацииЦенностей.ПреобразованиеВЛом)
	                       |	И СтрокиДокумента.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.ЮвелирноеИзделие)
	                       |	И СтрокиДокумента.СсылкаНаДокумент.СтатусЗапроса <> ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.Успех)
	                       |	И ВЫБОР
	                       |			КОГДА Металлы.ИНП ЕСТЬ NULL
	                       |				ТОГДА СтрокиДокумента.ИНППреобразованияВЛом = """"
	                       |			ИНАЧЕ ЛОЖЬ
	                       |		КОНЕЦ
	                       |	И &НомерОперации = 1
	                       |
	                       |УПОРЯДОЧИТЬ ПО
	                       |	НомерСтроки
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	""Товары"" КАК ИмяТабЧасти,
	                       |	СтрокиДокумента.НомерСтроки КАК НомерСтроки,
	                       |	СтрокиДокумента.ИНППреобразованияВЛом КАК ИНП,
	                       |	ВЫБОР
	                       |		КОГДА СтрокиДокумента.ИмяМетода = ""SendGetBatch""
	                       |			ТОГДА СтрокиДокумента.СтатусЗапроса
	                       |		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусОтветаГИИСДМДК.ПустаяСсылка)
	                       |	КОНЕЦ КАК СтатусЗапроса,
	                       |	СтрокиДокумента.ИдентификаторЗапроса КАК ИдентификаторЗапроса
	                       |ИЗ
	                       |	СтрокиДокумента КАК СтрокиДокумента
	                       |ГДЕ
	                       |	СтрокиДокумента.ИНППреобразованияВЛом <> """"
	                       |	И &НомерОперации = 2
	                       |
	                       |ОБЪЕДИНИТЬ ВСЕ
	                       |
	                       |ВЫБРАТЬ
	                       |	""Металл"",
	                       |	Металлы.НомерСтроки,
	                       |	Металлы.ИНП,
	                       |	Металлы.Статус,
	                       |	Металлы.ИдентификаторЗапроса
	                       |ИЗ
	                       |	Металлы КАК Металлы
	                       |ГДЕ
	                       |	Металлы.ИНП <> """"
	                       |	И &НомерОперации = 2
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	ШапкаДокумента.Номенклатура КАК Номенклатура,
	                       |	ШапкаДокумента.ВидНоменклатуры КАК ВидНоменклатуры,
	                       |	ШапкаДокумента.Камень КАК Камень,
	                       |	ШапкаДокумента.Наименование КАК Наименование,
	                       |	ШапкаДокумента.НаименованиеПолное КАК НаименованиеПолное,
	                       |	ШапкаДокумента.НаименованиеГИИСДМДК КАК НаименованиеГИИСДМДК,
	                       |	ШапкаДокумента.НаименованиеСерии КАК НаименованиеСерии,
	                       |	ШапкаДокумента.НомерПаспорта КАК НомерПаспорта,
	                       |	ШапкаДокумента.Номер КАК Номер,
	                       |	ШапкаДокумента.ТипПартии КАК ТипПартии,
	                       |	ШапкаДокумента.ВидПартии КАК ВидПартии,
	                       |	ШапкаДокумента.ЭтапОбработки КАК ЭтапОбработки,
	                       |	ШапкаДокумента.СтадияОбработки КАК СтадияОбработки,
	                       |	ШапкаДокумента.ОКПД2 КАК ОКПД2,
	                       |	ШапкаДокумента.Количество КАК Количество,
	                       |	ШапкаДокумента.Вес КАК Вес,
	                       |	ШапкаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	                       |	ШапкаДокумента.ЭтоКамни КАК ЭтоКамни,
	                       |	ШапкаДокумента.Ссылка КАК Ссылка,
	                       |	ШапкаДокумента.Металл КАК Металл,
	                       |	ШапкаДокумента.Проба КАК Проба,
	                       |	ШапкаДокумента.Дата КАК Дата,
	                       |	ШапкаДокумента.УчетВКаратах КАК УчетВКаратах,
	                       |	ШапкаДокумента.ВидОперации КАК ВидОперации,
	                       |	ШапкаДокумента.УИН КАК УИН,
	                       |	ШапкаДокумента.СтатусЗапроса КАК СтатусЗапроса,
	                       |	ШапкаДокумента.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	ШапкаДокумента.ВесМеталлаВЧистоте КАК ВесМеталлаВЧистоте,
	                       |	ШапкаДокумента.ОрганизацияЮрФизЛицо КАК ОрганизацияЮрФизЛицо,
	                       |	ШапкаДокумента.ОрганизацияИНН КАК ОрганизацияИНН,
	                       |	ШапкаДокумента.ОрганизацияКПП КАК ОрганизацияКПП,
	                       |	ШапкаДокумента.ОрганизацияОГРН КАК ОрганизацияОГРН,
	                       |	ШапкаДокумента.ИмяМетода КАК ИмяМетода
	                       |ИЗ
	                       |	ШапкаДокумента КАК ШапкаДокумента
	                       |ГДЕ
	                       |	&НомерОперации = 3
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	СтрокиДокумента.НомерСтроки КАК НомерСтроки,
	                       |	СтрокиДокумента.ИНППреобразованияВЛом КАК ИНППреобразованияВЛом,
	                       |	СтрокиДокумента.Количество КАК Количество,
	                       |	СтрокиДокумента.Вес КАК Вес,
	                       |	СтрокиДокумента.ВесМеталлаВЧистоте КАК ВесМеталлаВЧистоте,
	                       |	СтрокиДокумента.Металл КАК Металл,
	                       |	СтрокиДокумента.СтатусЗапроса КАК СтатусЗапроса,
	                       |	СтрокиДокумента.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	СтрокиДокумента.СсылкаНаДокумент КАК СсылкаНаДокумент
	                       |ИЗ
	                       |	СтрокиДокумента КАК СтрокиДокумента
	                       |ГДЕ
	                       |	СтрокиДокумента.ИНППреобразованияВЛом <> """"
	                       |	И &НомерОперации = 3
	                       |
	                       |ОБЪЕДИНИТЬ ВСЕ
	                       |
	                       |ВЫБРАТЬ
	                       |	Металлы.НомерСтроки,
	                       |	Металлы.ИНП,
	                       |	0,
	                       |	Металлы.Вес,
	                       |	Металлы.ВесВЧистоте,
	                       |	Металлы.ТипМеталла,
	                       |	Металлы.Статус,
	                       |	Металлы.ИдентификаторЗапроса,
	                       |	Металлы.Ссылка
	                       |ИЗ
	                       |	Металлы КАК Металлы
	                       |ГДЕ
	                       |	&НомерОперации = 3
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	ХарактеристикиНоменклатурыДополнительныеМатериалы.Материал КАК Материал,
	                       |	ХарактеристикиНоменклатурыДополнительныеМатериалы.Вес КАК Вес,
	                       |	Товары.НомерСтроки КАК НомерСтроки,
	                       |	ХарактеристикиНоменклатурыДополнительныеМатериалы.Материал.Металл КАК МатериалМеталл,
	                       |	ХарактеристикиНоменклатурыДополнительныеМатериалы.Материал.Проба КАК МатериалПроба
	                       |ИЗ
	                       |	Товары КАК Товары
	                       |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры.ДополнительныеМатериалы КАК ХарактеристикиНоменклатурыДополнительныеМатериалы
	                       |		ПО Товары.ХарактеристикаНоменклатуры = ХарактеристикиНоменклатурыДополнительныеМатериалы.Ссылка
	                       |;
	                       |
	                       |////////////////////////////////////////////////////////////////////////////////
	                       |ВЫБРАТЬ
	                       |	КомплектацияТоваровКамни.НомерСтроки КАК НомерСтроки,
	                       |	КомплектацияТоваровКамни.ИНП КАК ИНП,
	                       |	КомплектацияТоваровКамни.ИдентификаторЗапроса КАК ИдентификаторЗапроса,
	                       |	КомплектацияТоваровКамни.ОписаниеОшибки КАК ОписаниеОшибки,
	                       |	КомплектацияТоваровКамни.СтатусЗапроса КАК СтатусЗапроса
	                       |ИЗ
	                       |	Документ.КомплектацияТоваров.Камни КАК КомплектацияТоваровКамни
	                       |ГДЕ
	                       |	КомплектацияТоваровКамни.Ссылка = &Ссылка");
	Запрос.УстановитьПараметр("Товары", Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("НомерОперации", НомерОперации);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ДанныеДляПреобразования = РезультатЗапроса[4].Выбрать();
	ДанныеДляПолученияВесаЧистоты = РезультатЗапроса[5].Выбрать();
	ДанныеШапкиДокументов = РезультатЗапроса[6].Выбрать();
	ДанныеПоТоварам = РезультатЗапроса[7].Выгрузить();
	ДанныеПоДопМатериалам = РезультатЗапроса[8].Выгрузить();
	ДанныеПоКамням = РезультатЗапроса[9].Выбрать();
	
	Пакеты = Новый Соответствие;
	ДанныеДляОтправки = Новый Структура;
	НомерПакета = 0;
	
	МассивТоваров = Новый Массив;
	
	//ДопустимоеКоличествоПартий = 50;
	КоличествоВЗапросе = 0;
	НомераСтрокНеОтправлено = Новый Массив;
	МассивТоваровДляОтправкиВПакете = Новый Массив;
	
	Пока ДанныеДляПреобразования.Следующий() Цикл
		Если ДанныеДляПреобразования.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех() 
			И Не ДанныеДляПреобразования.ИмяМетода = ИнтеграцияГИИСДМДК.ИмяМетодаЗапросПолученияСведенийОПартиях() Тогда
			Продолжить;
		ИначеЕсли ДанныеДляПреобразования.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусОжиданиеОтвета() 
			и (ДанныеДляПреобразования.ИмяМетода = "" 
				Или ДанныеДляПреобразования.ИмяМетода = ИнтеграцияГИИСДМДК.ИмяМетодаРезультатОтправкаПреобразованиеВЛомНаРегистрацию()) Тогда	
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура;
			Результат.Вставить("ИдентификаторЗапроса", ДанныеДляПреобразования.ИдентификаторЗапроса);
			Результат.Вставить("ИмяМетода", ИнтеграцияГИИСДМДК.ИмяМетодаРезультатОтправкаПреобразованиеВЛомНаРегистрацию());
			Пакеты.Вставить(НомерПакета, Результат);	
		Иначе
			СтруктураСтроки = Новый Структура;	
			СтруктураСтроки.Вставить("НомерСтроки", ДанныеДляПреобразования.НомерСтроки);
			СтруктураСтроки.Вставить("УИН", ДанныеДляПреобразования.УИН);
			МассивТоваровДляОтправкиВПакете.Добавить(СтруктураСтроки);
			НомераСтрокНеОтправлено.Добавить(ДанныеДляПреобразования.НомерСтроки);
			КоличествоВЗапросе = КоличествоВЗапросе + 1;
			Если КоличествоВЗапросе = ДопустимоеКоличествоПартий Тогда
				НомерПакета = НомерПакета + 1;
				Результат = Новый Структура();
				Результат.Вставить("Спецификации", МассивТоваровДляОтправкиВПакете);
				Результат.Вставить("НомераСтрокНеОтправлено", НомераСтрокНеОтправлено);
				Результат.Вставить("ИмяМетода", ИнтеграцияГИИСДМДК.ИмяМетодЗапросОтправкаПреобразованияВЛомНаРегистрацию());
				Пакеты.Вставить(НомерПакета, Результат);
				КоличествоВЗапросе = 0;
				МассивТоваровДляОтправкиВПакете = Новый Массив;
				НомераСтрокНеОтправлено = Новый Массив;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если КоличествоВЗапросе > 0 Тогда
		НомерПакета = НомерПакета + 1;
		Результат = Новый Структура();
		Результат.Вставить("Спецификации", МассивТоваровДляОтправкиВПакете);
		Результат.Вставить("НомераСтрокНеОтправлено", НомераСтрокНеОтправлено);
		Результат.Вставить("ИмяМетода", ИнтеграцияГИИСДМДК.ИмяМетодЗапросОтправкаПреобразованияВЛомНаРегистрацию());
		Пакеты.Вставить(НомерПакета, Результат);
		КоличествоВЗапросе = 0;
		МассивТоваровДляОтправкиВПакете = Новый Массив;
		НомераСтрокНеОтправлено = Новый Массив;
	КонецЕсли;
			
	Пока ДанныеДляПолученияВесаЧистоты.Следующий() Цикл
		Если ДанныеДляПолученияВесаЧистоты.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусОжиданиеОтвета() Тогда	
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура;
			Результат.Вставить("ИдентификаторЗапроса", ДанныеДляПолученияВесаЧистоты.ИдентификаторЗапроса);
			Результат.Вставить("ИмяМетода", "CheckGetBatch");
			Результат.Вставить("ИмяТаблЧасти", ДанныеДляПолученияВесаЧистоты.ИмяТабЧасти);
			Пакеты.Вставить(НомерПакета, Результат);	
		Иначе
			НомераСтрокНеОтправлено.Добавить(ДанныеДляПолученияВесаЧистоты.НомерСтроки);
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура();
			Результат.Вставить("УИН", ДанныеДляПолученияВесаЧистоты.ИНП);
			Результат.Вставить("НомераСтрокНеОтправлено", НомераСтрокНеОтправлено);
			Результат.Вставить("ИмяМетода", "SendGetBatch");
			Результат.Вставить("ИмяТабЧасти", ДанныеДляПолученияВесаЧистоты.ИмяТабЧасти);
			Пакеты.Вставить(НомерПакета, Результат);
		КонецЕсли;	
	КонецЦикла;		
	
	Пока ДанныеШапкиДокументов.Следующий() Цикл
		Если ДанныеШапкиДокументов.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех()
			И Не ДанныеШапкиДокументов.ИмяМетода = ИнтеграцияГИИСДМДК.ИмяМетодаЗапросПолученияСведенийОПартиях() Тогда
			Продолжить;
		ИначеЕсли ДанныеШапкиДокументов.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусОжиданиеОтвета()
			И Не ДанныеШапкиДокументов.ИмяМетода = ИнтеграцияГИИСДМДК.ИмяМетодаЗапросПолученияСведенийОПартиях() Тогда
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура;
			Результат.Вставить("ИдентификаторЗапроса", ДанныеШапкиДокументов.ИдентификаторЗапроса);
			Результат.Вставить("ИмяМетода", ИнтеграцияГИИСДМДК.ИмяМетодаРезультатОтправкаКомплектацийНаРегистрацию());
			Пакеты.Вставить(НомерПакета, Результат);	
		Иначе		
			НомерПакета = НомерПакета + 1;
			Спецификация = ПодготовитьДанныеДокументаСОперациейКомплектации(ДанныеШапкиДокументов, ДанныеПоТоварам, ДанныеПоДопМатериалам,
				ПоискТоваровПоВнутреннемуШК, ПоискТоваровПоПаспорту);
				
			Результат = Новый Структура();
			Результат.Вставить("Спецификации", Спецификация);
			Результат.Вставить("НомераСтрокНеОтправлено", 0);
			МассивСтрок = Новый Массив;
			Для Каждого ТекСтр Из ДанныеПоТоварам Цикл
				МассивСтрок.Добавить(ТекСтр.НомерСтроки);
			КонецЦикла;	
			Результат.Вставить("НомераСтрокДляЗаполнения", МассивСтрок);
			Результат.Вставить("ИмяМетода", ИнтеграцияГИИСДМДК.ИмяМетодЗапросОтправкаКомплектацийНаРегистрацию());
			Пакеты.Вставить(НомерПакета, Результат);
		КонецЕсли;	
		
	КонецЦикла;
		
	Пока ДанныеПоКамням.Следующий() Цикл
		Если ДанныеПоКамням.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусУспех() Тогда
			Продолжить;
		ИначеЕсли ДанныеПоКамням.СтатусЗапроса = ИнтеграцияГИИСДМДК.СтатусОжиданиеОтвета() Тогда	
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура;
			Результат.Вставить("ИдентификаторЗапроса", ДанныеПоКамням.ИдентификаторЗапроса);
			Результат.Вставить("ИмяТабЧасти", "Камни");
			Результат.Вставить("ИмяМетода", "CheckGetBatch");
			Пакеты.Вставить(НомерПакета, Результат);	
		Иначе
			НомераСтрокНеОтправлено.Добавить(ДанныеПоКамням.НомерСтроки);
			НомерПакета = НомерПакета + 1;
			Результат = Новый Структура();
			Результат.Вставить("УИН", ДанныеПоКамням.ИНП);
			Результат.Вставить("НомераСтрокНеОтправлено", НомераСтрокНеОтправлено);
			Результат.Вставить("ИмяТабЧасти", "Камни");
			Результат.Вставить("ИмяМетода", "SendGetBatch");
			Пакеты.Вставить(НомерПакета, Результат);
		КонецЕсли;		
	КонецЦикла;
	
	ДанныеДляОтправки.Вставить("Пакеты", Пакеты);
	
	Возврат ДанныеДляОтправки;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьДанныеПоОперации(НомерОперации, ПереходитьКСледующейОперации)
	
	Элементы.ГруппаОшибки.Видимость = Ложь;
	
	Если ИдетОтправкаДанных Тогда
		Возврат;
	КонецЕсли;
	
	НомерТекущейОперации = Число(Прав(НомерОперации,1));
	Если НомерТекущейОперации <> 1 
		И Элементы["ПолеГалка"+(НомерТекущейОперации - 1)].Картинка <> БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не отправлены данные по предыдущей операции");
		Возврат;
	ИначеЕсли НомерТекущейОперации = 5 Тогда
		Возврат;
	ИначеЕсли НомерТекущейОперации = 1 И Не ДанныеЗаполненыКорректно() Тогда
		Возврат;
	КонецЕсли;	
	ИдетОтправкаДанных = Истина;
	
	Если СписокСертификатов.Количество() > 0 Тогда
		ДополнительныеПараметрыПослеВыбораСертификата = Новый Структура;
		ДополнительныеПараметрыПослеВыбораСертификата.Вставить("НомерТекущейОперации", НомерТекущейОперации);
		ДополнительныеПараметрыПослеВыбораСертификата.Вставить("ПереходитьКСледующейОперации", ПереходитьКСледующейОперации);
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораСертификата", ЭтотОбъект, ДополнительныеПараметрыПослеВыбораСертификата);
		СписокСертификатов.ПоказатьВыборЭлемента(Оповещение, "Выберите сертификат");
	Иначе
		ПредварительныеДанныеДляОтправки = ПодготовитьДанныеКомплектацииТоваров(НомерТекущейОперации);
	
		Если ПредварительныеДанныеДляОтправки.Пакеты.Количество() > 0 Тогда
			ОтправитьИнформациюОКомплектации(Сертификат, ПредварительныеДанныеДляОтправки.Пакеты, 1, НомерТекущейОперации, ПереходитьКСледующейОперации);
		Иначе
			ИдетОтправкаДанных = Ложь;
			ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Нет данных для отправки");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры		

&НаКлиенте
Процедура ПослеВыбораСертификата(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не выбран сертификат по организации. Отправка информации о документе не возможна");
		ИдетОтправкаДанных = Ложь;
		Возврат;
	КонецЕсли;	
	
	Сертификат = Результат.Значение;

	ПредварительныеДанныеДляОтправки = ПодготовитьДанныеКомплектацииТоваров(ДополнительныеПараметры.НомерТекущейОперации);
	
	Если ПредварительныеДанныеДляОтправки.Пакеты.Количество() > 0 Тогда
		ОтправитьИнформациюОКомплектации(Сертификат, ПредварительныеДанныеДляОтправки.Пакеты, 1, 
			ДополнительныеПараметры.НомерТекущейОперации, ДополнительныеПараметры.ПереходитьКСледующейОперации);
	Иначе
		ИдетОтправкаДанных = Ложь;
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Нет данных для отправки");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНажатияКнопкиМеню(ИмяДействия, НомерОперации)
	Если ИмяДействия = "ОтправитьДанные" Тогда
		ОтправитьДанныеПоОперации(НомерОперации, Ложь);
	ИначеЕсли ИмяДействия = "ПоказатьОшибки" Тогда
		ПоказатьОшибки(НомерОперации);
	ИначеЕсли ИмяДействия = "ПоказатьЗапросы" Тогда
		ПоказатьЗапросы(НомерОперации);
	Иначе
		ОбщегоНазначенияКлиентСерверУ.СообщитьПользователю("Не описан обработчик для команды " + ИмяДействия);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНажатиеЗавершение(Команда, ДополнительныеПараметры) Экспорт

	Если Команда <> Неопределено Тогда
		ОбработкаНажатияКнопкиМеню(Команда.Значение, ДополнительныеПараметры.Элемент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОписаниеНажатие(Элемент)
	Если Элементы["ПолеГалка"+Прав(Элемент.Имя,1)].Картинка = БиблиотекаКартинок.СостоянияРасширенийПодключено Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Данные успешно отправлены'"), , НСтр("ru = 'Отправка данных в ГИИС ДМДК'"));
		Возврат;
	Иначе
		СписокКоманд = Новый СписокЗначений;
	    СписокКоманд.Добавить("ОтправитьДанные", НСтр("ru = 'Отправить данные в ГИИС ДМДК'"));
		Если Элементы["ПолеГалка"+Прав(Элемент.Имя,1)].Картинка = БиблиотекаКартинок.Остановить Тогда
			СписокКоманд.Добавить("ПоказатьОшибки", НСтр("ru = 'Показать данные с ошибками отправки в ГИИС ДМДК'"));
		КонецЕсли;
		СписокКоманд.Добавить("ПоказатьЗапросы", НСтр("ru = 'Показать запросы в ГИИС ДМДК'"));
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Элемент", Элемент.Имя);
		ОповещениеВыбораИзМеню = Новый ОписаниеОповещения("ОписаниеНажатиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
		ПоказатьВыборИзМеню(ОповещениеВыбораИзМеню, СписокКоманд, Элемент);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗапросовПриАктивизацииСтроки(Элемент)
	Если НЕ Элементы.СписокЗапросов.ТекущиеДанные = Неопределено Тогда	
		Отбор = Новый ФиксированнаяСтруктура("НомерСтроки", Элементы.СписокЗапросов.ТекущиеДанные.НомерСтрокиТаблЧасти);
		Если Элементы.СписокЗапросов.ТекущиеДанные.ИмяТабличнойЧасти = "" Тогда
			Элементы.ГруппаОшибкиПодробно.ТекущаяСтраница = Элементы["ГруппаШапка"];
		Иначе	
			Элементы.ГруппаОшибкиПодробно.ТекущаяСтраница = Элементы["Группа" + Элементы.СписокЗапросов.ТекущиеДанные.ИмяТабличнойЧасти];
			Элементы[Элементы.СписокЗапросов.ТекущиеДанные.ИмяТабличнойЧасти].ОтборСтрок = Отбор;
		КонецЕсли;
	Иначе
		Отбор = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы = Ложь Тогда
		ПараметрыЗаписи = Новый Структура;
    	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
		Записать(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры
